label agr_day_8:

    $ backdrop = "days"
    $ agr_new_chapter(12, "Подтверждение\nпокаяния")
    $ persistent.sprite_time = "night"
    $ night_time()
    play music rainy_tears fadein 4
    play sound3 rain_inside loop fadein 3
    pause(1)
    window auto

    scene bg int_clubs_male_rain with dissolve2

    "В помещении было прохладно, но одновременно с этим душно и накурено."

    window hide
    show sh serious hazmat_suit at right
    show el hs_normal hazmat_suit at left
    with dissolve
    window auto

    "Свет керосиновой лампы нежно огибал контуры стола, карты, тетрадок с пометками, ружья и трёх молодых людей."
    sh "Ну, выходит, прибиться можно будет только тут. Только надо быстрее с этим моментом разобраться."
    me "В плане?"
    sh "В плане: топлива немного у нас."
    "Кибернетик навис над картой, делая какие-то пометки."
    el "А химзащита?"
    "Химзащита была, ещё около семнадцати костюмов.{w} На самом деле двадцать, но три местами сгнили и были переоборудованы в резиновые мешки. Лена очень хорошо умела шить."

    show sh normal hazmat_suit with dspr

    sh "А в чём дело-то? Фактически, нужно только разок туда сгонять и посмотреть, что да как, какой фон, есть ли люди."
    me "Насчёт людей — это вряд ли. Свалили уже давно. Либо вымерли."
    sh "Ну хорошо, тоже верно. Эл, фотик не забыл?"

    show el hs_smile hazmat_suit with dspr

    el "С собой."
    "Откуда взялась такая оперативность? Да всё просто."
    "Шурик тогда серьёзно напортачил с расчётами. Находиться здесь дольше девяти месяцев, ну в крайнем случае год, было банально опасно."
    "Домики, в которых мы жили летом, находились в аварийном состоянии. У административного корпуса текла крыша, постепенно заканчивались противорадиационные препараты и всё чаще донимали собаки."
    me "Значит, что мы делаем? Вам автомат, мне ружьё. Через минут двадцать будете отчаливать?"
    sh "Да. Ты пока в бункере покопайся. И это..."
    "Шурик потупился."
    sh "Если найдёшь препараты с кальцием, отнеси Мику, пожалуйста."
    "Я кивнул."
    "До начала первых морозов перед нами стояли две задачи: разведка и, если всё успешно, приведение поезда в рабочее состояние."
    "Зимой через замёрзшую реку вполне можно было перенести всё необходимое: на самом-то деле недалеко! Фактически до станции было около получаса пешком."
    "И даже лыжи нашли: зимние-то смены в «Совёнке» были."

    stop music fadeout 3

    "Распрощавшись с кибернетиками, я вышел из здания клубов."

    scene bg black with dissolve2
    pause(0.2)
    play ambience rain_inside fadein 3
    scene bg int_dv_room_rain with dissolve2
    play sound sfx_bed_squeak1

    "Алиса спала. Я сел на кровать около неё."
    me "Ну чего ж ты так разболелась, девочка моя?"
    "Произнёс я шёпотом. Погладил её по шершавым, родным волосам."
    "На рукаве чёрно-бордовым пятном отпечаталась кровь. С утра из носа шла, минут пятнадцать не мог остановить — Славя даже предлагала к Виоле обратиться."
    "Радиация и жуткий недосып давали о себе знать."
    me "Вот увидишь, мы уедем и всё будет хорошо."
    me "Я...{w} {i}обещаю{/i}."
    "К горлу подкатил тяжёлый комок."
    me "Мы ведь справимся? Мы справимся."
    "Утёр непрошеные слёзы. Девушка проснулась."

    window hide
    show dv normal ag_sport close with dissolve
    window auto

    dv "Сёма? Ты чего..."
    me "Я тут."
    dv "Что решили с кибернетиками?"
    me "Через месяц свалим отсюда. В тайгу. Конечного решения пока что не приняли, но точно знаю, что свалим."
    me "Я скоро пойду вниз, в бункер. Надо проверить, как работает генератор, а то, сама видела, электричество сбоит."

    show dv guilty ag_sport close with dspr

    dv "Понимаю..."

    play sound kashel_devushka

    "Девушка закашлялась. Я погладил её по спине."
    me "Давай-ка поправляться. Что Виола говорила?"

    show dv sad ag_sport close with dspr

    dv "Это из-за слабых лёгких, воздуха не хватает."
    "Воздух и правда превратился в наждак. Атмосфера сходила с ума. Помню, папа мне говорил довольно умную штуку: солнце светит одинаково для всех, только по-разному гаснет."
    "Наше уже погасло{w=1}, а мы всё ещё жили."
    dv "Сём, ты иди. Только приходи побыстрее, хорошо?"
    "Я кивнул, поцеловал её в лоб и накрыл потеплее одеялом."

    stop ambience fadeout 3 
    window hide
    scene bg int_admins_corridor with dissolve
    $ persistent.sprite_time = "sunset"
    show sl normal coat far at right
    show un normal coat far
    with dissolve
    window auto
    play music music_list["afterword"]

    "В коридоре были Лена и Славя. Девушки стояли у двери соседней комнаты, ждали японку."
    "У Мику уже заметно округлился животик, Шурик вёл себя подобающе будущему отцу. На самом деле, никогда бы не подумал, что кибернетик может быть настолько заботливым."
    "Я, понятное дело, для своей рыженькой делал всё возможное, но главное, самое важное, что я мог сделать — ускорить переезд."

    window hide
    show sl normal coat at right
    show un normal coat:
        xalign 0.28
        xanchor 0.5
        yanchor 1.0
    with dissolve
    window auto

    me "Привет, девочки."
    sl "Привет. Как она?"
    me "Вроде получше."

    window hide
    show sl normal coat at fright
    show un normal coat:
        xalign 0.16
        xanchor 0.5
        yanchor 1.0
    with ease
    show mt normal sport with dissolve
    window auto

    mt "Сёма, тебе бы отдых не помешал."

    show mt angry sport with dspr

    mt "И эти кибернетики, блин, недели две как ездили, чуть не погибли, а снова едут. Мало вам было, что рвало и температурило?"
    "Я нахмурился."

    show un sad coat
    show mt shocked sport
    show sl sad coat
    with dspr

    me "Мы делаем всё для вас. Для вас всех."

    window hide
    pause(1)
    show mt sad sport with dspr
    window auto

    me "Я сейчас в бункер, надо поискать кое-что и генератор отладить, не жгите электричество зазря."
    "Девушки кивнули."

    scene bg int_admins_corridor with dissolve

    "Я поудобнее залез в свой плащ, заранее поменял фильтр в противогазе."

    stop music fadeout 3
    scene bg black with dissolve
    play sound2 nadel_protivogaz
    pause(0.5)
    play ambience light_rain fadein 3
    scene bg ext_admins_rain
    show gas_mask
    with dissolve
    play sound3 dychanie_w_protivogaze loop fadein 1.5

    "После того, как мы с Шуриком разгерметизировали хранилище, туда начал потихоньку поступать метан из подземелий. Как-то решать это надо, но вот как?"
    "Всё, что нуждалось в герметизации или могло быть отравлено парами, вытащили на поверхность и складировали в библиотеке."
    "Сухпайки, фильтры, что-то ещё из медицины."

    stop ambience fadeout 4
    extend " Но внизу ещё были и припасы, и некоторое оборудование."

    scene bg black with dissolve
    $ night_time()
    pause(0.3)
    play ambience rain_inside fadein 3
    scene cg int_old_building_luke
    show gas_mask
    with dissolve
    play sound sfx_open_metal_hatch

    "Знакомый люк неприветливо скрипнул, но поддался."

    stop ambience fadeout 2
    scene bg black with dissolve
    scene bg int_catacombs_entrance
    show gas_mask
    with dissolve
    play ambience ambience_catacombs fadein 3

    "Обволакивающая, противная сырость пробиралась сквозь кожу и одежду."

    scene bg black with dissolve
    scene bg int_catacombs_door
    show gas_mask
    with dissolve

    "Ещё ниже."

    play sound sfx_open_door_mines_metal
    scene bg black with dissolve
    scene bg int_bunker
    show gas_mask
    with dissolve

    "Ещё одна дверь."

    play sound otkryl_germodver
    scene bg black with dissolve
    pause(1.5)
    stop sound2 fadeout 3
    $ renpy.pause(5,hard=True)
    stop sound2 fadeout 2
    scene bg int_storage
    show gas_mask
    with dissolve
    play sound2 zvuk_rubilnika

    "Включив едва рабочий рубильник, я подождал, пока загорится рабочая часть ламп склада, и пошёл перебирать те отсыревшие коробки, которые ещё не были вскрыты."

    scene bg int_storage_2
    show gas_mask
    with dissolve

    me "Йод...{w=1} ещё йод, так.{w} Опа, глюконат кальция — это хорошо."
    "Я пихнул в небольшую сумку, кажется, от противогаза, склянки с йодом и таблетки для японки."

    play sound "<from 0.01>sound/sfx/grate_hand_fall.ogg"

    "Вскрыл ещё ящик."
    "Сухпайки уже все в плесени и отсыревшие. Выкинул. Простыни, керосиновая лампа, заржавевшие батарейки... Краем глаза поймал что-то новое."
    "Сзади меня, в стене, было какое-то еле заметное, при скудном освещении, углубление."

    stop ambience fadeout 3
    play music music_list["sunny_day"] fadein 3
    scene bg int_storage_door
    show gas_mask
    with dissolve

    "Дверь.{w} Проржавевшая окончательно."

    play sound2 kaszel_protivogaz
    play sound sfx_inhale
    show bg int_storage_door behind gas_mask:
        ease 0.7 zoom 1.3 truecenter

    extend " Прокашлялся сквозь противогаз, размял костяшки."
    "Дверь не поддавалась, как я ни старался. Выдохнул, выматерился. Спустя пару минут нашёл какой-то черенок от лопаты, не так сильно сгнивший."
    "Чёрт его знает, что он тут делал и как сохранился, но с ним дело пошло лучше, дверь чуть поддалась."

    play sound sfx_open_door_mines_metal

    "Промучившись минут десять, с помощью черенка и чьей-то матери, я всё-таки открыл дверь."

    scene bg int_storage_hidden_room
    show gas_mask
    with dissolve

    "Внутри было помещение: чуть меньше, чем КПП, но с большим количеством аппаратуры. ЭВМ, чуть мерцающая лампа, что-то похожее на радио и два стеллажа."
    "Рядом с ЭВМ стояла какая-то большая коробка. На столе лежала небольшая брошюра вроде тех, что были про гражданскую оборону и защиту населения."
    me "Порядок печати карт местности в условиях ядерной конфронтации."
    "Глаза загорелись, на лицо вылезла незваная улыбка."
    th "Это значит, что не придётся ехать наугад."
    "Схватив брошюру, я рванул из бункера. Надо срочно отзывать «Волгу», пусть возвращаются быстрее."
    
    stop music fadeout 3
    scene bg black with dissolve2
    pause(0.5)
    scene bg int_radioroom
    play sound2 dychanie_w_protivogaze loop fadein 1.5
    play sound3 rain_inside loop fadein 2.0
    show dv normal ag_sport far at cright
    show mi normal jacket far at cleft
    show gas_mask
    with dissolve2
    $ day_time()

    "В комнате, оборудованной под радиорубку, сидела проснувшаяся Алиса и Мику. Не обращая внимания на грязную одежду и не снятый по пути противогаз, я влетел в помещение."

    window hide
    show dv surprise ag_sport at cright behind gas_mask
    show mi shocked jacket at cleft behind gas_mask
    with dissolve
    window auto

    "Девочки уставились на меня обалделыми взглядами."
    mi "Сёмочка, ты сошёл с ума?"

    show dv sad ag_sport behind gas_mask with dspr

    "Искренне поинтересовалась японка."

    play sound kashel_devushka

    extend " Алиса прокашлялась."
    me "Во!"

    hide gas_mask with dissolve
    play sound gasmask_take_off
    stop sound2 fadeout 1.5

    "Замахал я брошюркой, стягивая противогаз."

    show dv normal ag_sport with dspr

    dv "Это что — «во»?"

    show mi normal jacket with dspr

    me "Это наша гарантия на жизнь."

    scene bg int_radioroom with dissolve

    "Я протянул девочкам находку, отдал Мику таблетки и уселся за радио."
    play sound sfx_radio_squelch_1
    me "{image=ag_speaker} «Волга», я «Совёнок», приём. Приём, «Волга»!"
    play sound sfx_radio_squelch_2
    sh "{image=ag_radio} «Совёнок», я «Волга»..."
    play sound sfx_radio_squelch_1
    me "{image=ag_speaker} Шура, давайте крайне оперативно, у нас тут находка. В ЭВМ разбираешься?"
    play sound sfx_radio_squelch_2
    sh "{image=ag_radio} Да, но мы же нашу разобрали..."
    play sound sfx_radio_squelch_1
    me "{image=ag_speaker} Тут другое. Подгони Электроника."
    play sound sfx_radio_squelch_2
    sh "{image=ag_radio} Через полтора часа будем, отбой."
    play sound sfx_radio_squelch_1
    me "{image=ag_speaker} Отбой."
    stop ambience fadeout 3
    play music music_list["smooth_machine"] fadein 3
    "Я с упоением откинулся на спинку стула, развернулся к девушкам."

    window hide
    show dv normal ag_sport at cright
    show mi normal jacket at cleft
    with dissolve
    window auto

    mi "Ох, собираться надо, наверное..."
    "Она погладила живот."
    me "Рано ещё, рано. Но потихоньку готовиться надо."
    dv "Ты вообще береги силы, тебе рожать ещё. Сём, иди сюда."

    show dv smile ag_sport close at right with dspr

    "Двачевская улыбнулась. Обескровленные губы после нескольких глотков дефицитного чая снова покраснели, было видно, что девушке становится лучше."
    "Она быстро поцеловала меня в щеку и крепко обняла, шепнув на ухо:"
    dv "Спасибо, родной."

    show mi smile jacket with dspr

    "Японка улыбнулась, чуть прищурилась."
    mi "Я, наверное, пойду."
    me "Да нет, сидите. Я сейчас в караул, потом дров в котельную подкину. Вам не холодно?"

    show dv shy ag_sport close with dspr

    dv "Нам хорошо. Только посиди ещё пять минут."
    "Алиса состроила милое личико."
    me "Ну разве что пять минут."
    "Я стянул куртку, поцеловал Алису в макушку. "

    window hide
    show mi smile jacket at center behind dv with ease
    show sl normal coat at fleft behind mi with dissolve
    window auto

    extend "Дверь комнаты открылась, вошла Славя."
    sl "А что у вас тут за переполох?"

    show dv smile ag_sport close with dspr

    dv "Семён нашёл в бункере комнату, где есть оборудование, которое печатает карты... "

    show dv surprise ag_sport close with dspr

    extend "а откуда?"
    me "Из космоса."

    show dv laugh ag_sport close
    show mi laugh jacket
    show sl surprise coat
    with dspr

    dv "Из космоса. И теперь мы точно будем знать, куда нам ехать!"

    show sl laugh coat
    show mi smile jacket
    with dspr

    sl "А где она, карта?"
    me "Ждём Шурика, он у нас по этой части."

    show dv smile ag_sport close
    show sl smile2 coat
    with dspr

    sl "Ух ты!"
    mi "Девочки, а как вы думаете, где будем жить?"
    sl "Я хочу, чтобы был лес. И речка, или озерцо хотя бы, только не как у нас."
    dv "А я бы в какой-нибудь деревне устроилась. Тепло, свой домик, камин... или печь, большая такая, тёплая."
    "Я покрепче приобнял Алису."
    mi "Я бы хотела посмотреть, как там с Японией, может, всё-таки, там вообще войны не было."
    mi "А если так, то я хочу куда-нибудь на юг. Не в пустыню, а ближе к морю."

    show sl smile coat with dspr

    sl "А ты куда хочешь, Сём?"
    "Я задумался, прикинул пару вариантов в голове и уверенно ответил:"
    me "Всё равно. Главное с вами и чтобы никто не болел."

    show dv guilty ag_sport close with dspr

    "Алиса фыркнула."
    dv "Ну, так неинтересно!"

    show dv normal ag_sport close with dspr

    me "Ну хорошо. Я хочу какой-нибудь посёлок на Урале оккупировать. Чтобы в горах."

    show dv smile ag_sport close with dspr
    stop music fadeout 5
    play ambience rain_inside fadein 5

    "Девочки мечтали и улыбались."
    "Мне самому хотелось мечтать и улыбаться, не думать о грязи и слякоти на улице, сидя в тепле с моей рыженькой."
    "Но чтобы мечты воплотились в реальность, нужно было ещё немного потерпеть, ещё немного поработать."
    me "Не теряйте, я пошёл!"
    sl "Удачи."
    dv "Не промокни!"
    th "Автомат сейчас у кибернетиков, а вот ружьё... ружьё у Ольги, наверное."

    scene bg int_admins_corridor with dissolve

    "Я вышел из комнаты, попутно вспоминая, куда пихнул дождевик, прошёл до конца коридора. Ольга и Виола жили в небольшой, чуть меньше нашей с Алисой, комнате."
    
    scene bg int_dv_room_rain with dissolve

    "Небольшое пространство на радость компенсировало то, что там почти всегда было солнечно."

    window hide
    show mt normal sport with dissolve
    window auto

    me "Оль, можно к тебе?"
    mt "Входи."
    "Бывшая вожатая лежала в своей койке, читая какую-то книгу в толстом, красном переплёте. Виолы не было. Наверное, собирает остатки лекарств в медпункте."
    me "Ружьё где?"
    "Девушка молча ткнула в угол комнаты. Двустволка, ещё влажная после дождя."

    show mt sad sport with dspr
    play music philosophy fadein 3

    mt "Знаешь, что, Сём... боязно мне как-то."
    me "Чего боязно?"
    mt "Уезжать. Здесь хоть и опасно, но там неизвестность. Я в Союзе до..."
    mt "Эх, короче, я только в Киеве была и в Москве. А больше нигде, понимаешь? Мы наугад поедем."
    me "Не совсем."

    show mt surprise sport with dspr

    "Я улыбнулся приподнявшейся на кровати девушке."
    mt "Это как?"
    me "Я нашёл ещё одну комнату в бункере: там есть оборудование, вроде рабочее... Ну да, рабочее, скорее всего: там внутри почти стерильно."
    me "Короче, можно карту получить: где радиация есть, где чисто."
    "Ольга медленно встала с кровати, потёрла виски."
    mt "То есть ехать наугад не придётся?"
    me "Ну, если наши светлые умы разберутся, как оно работает — вполне себе да."

    show mt smile sport with dspr

    "Ничего не сказала, тихо улыбнулась, обняла. Как-то не по-дружески, не как вожатая, а по-родственному, как мать."
    mt "Молодец ты, Сёмка.{w} Молодец."

    stop ambience fadeout 5

    mt "Пойду что-нибудь приготовлю, а то ребята голодные вернутся."

    stop music fadeout 3
    scene bg black with dissolve
    pause(0.5)
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    play ambience heavy_rain fadein 3
    # "Я прихватил ружьё, по пути зарядил. Спускались на нижний этаж вместе."

    stop sound3 fadeout 2.0
    scene bg ext_camp_entrance_fog_sunset with dissolve

    "Дождь на улице уже не казался таким противным. По пути встретил Ульяну — девочка возвращалась из медпункта, помогала Виоле. Спустя пару минут занял своё место у ворот, облокотился на статую девочки."

    stop ambience fadeout 2
    scene bg ext_road_sunset_rain with dissolve
    play sound cinematic_kick
    play music fortress_of_doom fadein 3

    "Мир вокруг застыл. Даже дождь превратился в какую-то густую дымку, обволакивающую лагерь и окрестности. С востока подул прохладный ветер."

    window hide
    show hl ghost with dissolve
    window auto

    hohol "И ты думаешь, ты сможешь их спасти?"
    "Василий уже дважды приходил: не то как видение, не то как какой-то внутренний голос."
    me "Смогу."
    hohol "Не сможешь. Я свою старушку не спас — и ты их не сможешь."
    me "Дурак ты."
    hohol "Не спорь со старшими."
    me "Тебя нет."
    hohol "Почему же, Персунов?"
    me "Я тебя убил."
    hohol "Да? И то верно."
    "Какое-то время голос молчал."
    hohol "Суки, вот хоть бы похоронили..."
    hohol "Эх, ладно. Мне пора."
    hohol "Прощай. К тебе гости."

    window hide
    hide hl with dissolve
    window auto
    play sound3 light_rain loop fadein 3

    me "Какие ещё гости?"
    "Ответа не последовало. Только сквозь дымку, вдалеке, были видны три силуэта, постепенно приближающиеся."
    "Я резво взвёл ружьё и спрятался за статуей. Колени задрожали, в ушах бил пульс."
    th "Кто это? А вдруг такие же сумасшедшие, как Василий..."
    th "Бежать в лагерь поздно, да и просто не нужно.{w} Думай!"
    "Когда дистанция между нами была метров двадцать, я вышел из-за укрытия."
    me "Стой, кто идёт?!"

    window hide
    show og normal pioneer at left
    show ac fear pioneer at right
    show ir shock pioneer at center
    with dissolve
    window auto

    "Люди остановились, даже чуть попятились назад."

    $ agr_meet('ir', u'Неизвестный')

    ir "Мы... Мы тут живём..."
    me "Врёшь! В лагере остался только наш отряд."
    ir "Ну тогда жили. Не стреляй, пожалуйста."
    "Он сделал шаг вперёд и вбок, закрывая девушку."
    me "Как тебя зовут? Какой отряд?"

    stop music fadeout 3

    ir "Игорь, второй отряд. Это Олег, у него горло очень болит, говорить больно, со мной был."

    $ agr_meet('ir', u'Игорь')

    me "За тобой кто?"
    ir "Алеся. Моя сестра."
    "Я опустил оружие, выдохнул."
    me "Давайте за мной."

    stop sound3 fadeout 3
    scene bg black with dissolve
    pause(0.5)
    $ persistent.sprite_time = "day"
    play sound3 rain_inside fadein 2.0
    play music music_list["trapped_in_dreams"] fadein 3
    scene bg int_admins_corridor
    show ir normal pioneer at cright
    show ac normal pioneer at center
    with dissolve
    $ day_time()

    "Собрались в коридоре. Из трёх пришельцев перед нами стояли двое — Алеся и Игорь. Олега сразу забрала Виола, у парня было серьёзное воспаление лёгких и гортани."
    "На испуганных подростках была порванная, пионерская форма, их плащи оставили на улице: счётчик потрескивал."

    window hide
    show us grin sport at fright with dissolve
    window auto

    us "Я её помню!"
    "Радостно воскликнула Ульяна и невежливо ткнула пальцем в сторону Алеси. Девушка тихо улыбнулась, и, казалось, даже приободрилась."
    me "Уверена?"

    show us smile sport with dspr

    us "Да. Мы им в пионербол продули..."

    window hide
    show un normal coat at cleft behind ac
    show sl normal coat at fleft
    with dissolve
    window auto

    sl "Что делать-то будем?"

    show un smile coat with dspr

    un "Ну не выгонять же их. Они голодные, больные. А мы всё-таки в одной смене были..."

    window hide
    show mt smile sport at fright behind us, ir with dissolve
    window auto

    mt "Согласна. Продовольствия нам до поездки хватит и на такую компанию."
    ir "Ольга Дмитриевна..."
    mt "Просто Ольга."
    ir "Вы сказали про поездку, куда вы собираетесь?"
    me "К чёртовой матери отсюда, в чистую зону."

    show ir surprise pioneer with dspr

    ir "А такие есть?..."
    "Повисла тишина. На улице нарастал знакомый гул мотора «Волги»."
    me "Вот сейчас и узнаем."

    show mt angry sport with dspr

    mt "Э, не, я что, обед зря готовила? Потом разберётесь, да и гостей надо как-то накормить."

    show sl smile2 coat with dspr

    sl "Пойдём, ребят."

    stop music fadeout 3

    "Блондинка лучезарно улыбнулась."

    stop sound3 fadeout 2.0
    play sound2 light_rain loop fadein 3
    scene bg ext_admins_rain with dissolve
    $ persistent.sprite_time = "sunset"
    $ night_time()

    "Импровизированная столовая находилась на первом этаже. Пока девочки обступили Алеся с Игорем, я рысцой рванул к кибернетикам. Как ни странно, оба были в костюмах."

    window hide
    show sh normal hazmat_suit at right
    show el hs_normal hazmat_suit at left
    with dissolve
    window auto

    sh "Чего у вас тут?"
    me "Гости!"
    "Принимая автомат из рук кибернетика насколько мог загадочно ответил я."
    el "Какие ещё гости?"
    me "Игорь, Олег и... "

    play sound sfx_suspence_bang

    extend "Алеся."

    show sh scared hazmat_suit with dspr

    "Шурик очень медленно развернулся в мою сторону с диким взглядом."
    me "Тихо, Шура, другая, не переживай."

    show sh serious hazmat_suit with dspr

    "Кибернетик сплюнул, протёр очки."
    sh "Ну пойдём знакомиться."
    el "А что за ЭВМ ты нашёл?"
    me "С помощью него можно получить карту местности, там что-то со спутником."

    show sh normal_smile hazmat_suit with dspr
    if persistent.easter_egg == True:
        play music music_list["always_ready"] fadein 3

    "Шурик чуть не подпрыгнул от счастья на месте, схватил меня за плечи."
    sh "Где?! Как? Обалдеть! Мы едем в чистую зо-ону-у-у!"
    me "Да!"

    show el hs_smile hazmat_suit at left

    el "А доступ ты получить сможешь?"

    stop sound2 fadeout 3
    stop music fadeout 3

    sh "Придумаем что-нибудь."

    play sound3 rain_inside loop fadein 2.0
    scene bg int_dining_hall_sunset with dissolve
    play music saya_sunset fadein 3
    $ sunset_time()

    "Мы вошли в здание. Кибернетики быстро скинули с себя химзащиту и двинулись за мной в столовую."

    window hide
    show sh normal shirt at right
    show el normal shirt at left
    with dissolve
    window auto

    me "А что вы химзу-то оставили?"
    el "Так мы почти не вылезали из машины, а на месте — там большущая аномалия, аккумулятор чуть с ума не сошёл."
    me "Там чисто?"
    sh "Да!"

    scene bg int_dining_hall_sunset with dissolve

    "Остальные ребята уже ели гречку с тушёнкой из запасов бункера. Алеся с Игорем сидели вместе и жадно, но стараясь соблюдать приличия, поедали обед."
    "В зале не было только Виолы. Мы положили свои порции и уселись на свободные места за одним, большим столом."

    window hide
    show dv normal ag_sport at fleft with dissolve
    window auto

    dv "Ребят, а как вы выжили?"
    "Будто, ни с того, ни с сего, спросила моя рыженькая. В зале повисла тишина."

    window hide
    show ac normal pioneer at cleft with dissolve
    window auto

    ac "В тот день мы решили сплавать на Ближний за земляникой, а потом на другой берег: Олег любил фотографировать, говорил, там пейзажи красивые."
    "Девушка говорила тихо, но уверенно и спокойно."

    window hide
    show ir normal pioneer at cright with dissolve
    window auto

    ir "Там нас и накрыло. Сначала плутали по лесу, потом в какое-то заброшенное село вышли — оно и до войны заброшено было."
    ir "Там пожили, но сейчас его затопило и пришлось уходить. Вышли на шоссе, потом ушли в лес, жили в шалаше, совсем отчаялись, решили дойти до лагеря."
    me "Во дела... А ели-то что?"

    show dv surprise ag_sport
    show ac cry pioneer
    with dspr

    ac "Голодно было постоянно. Доходило до того, что лягушек жарили."
    "Девушка тихо всхлипнула, но быстро взяла себя в руки."

    show ac normal pioneer with dspr

    th "Это мне ещё на жизнь жаловаться..."

    show dv normal ag_sport with dspr

    dv "Вас нужно нормально одеть. И в баню."
    "Почти приказательным тоном сообщила Алиса."
    "Славяна с Леной одобрительно закивали."
    ir "А вы? Вы тут как жили? И откуда электричество?"

    window hide
    show el normal shirt at fright with dissolve
    window auto

    el "Под лагерем бункер, там генератор, запитать лагерь труда не составило. Но поначалу да, тяжело было."
    me "Алис, я дрова притащу, тебе тоже в баньку хорошо бы, прогреться."

    show dv smile ag_sport with dspr

    "Алиса фыркнула, но возражать не стала."

    stop music fadeout 3
    scene bg int_dining_hall_sunset with dissolve
    play ambience ambience_dining_hall_empty fadein 3

    "После обеда мы разделились. Девочки пошли одевать и мыть гостей, Электроник, ничего не говоря, умотал проявлять фотографии, а мы с Шуриком в бункер."

    stop sound3 fadeout 2.0
    stop ambience fadeout 3
    scene bg black with dissolve
    pause(0.5)
    scene bg ext_square_sunset with dissolve
    play ambience ambience_ext_road_evening fadein 3

    "Пока шли, меня разбирало любопытство."

    window hide
    show sh serious shirt with dissolve
    window auto

    me "Шур, а что там, на станции?"
    "Кибернетик пощурился, вздохнул."

    show sh normal shirt with dspr

    sh "На самих путях только старые вагоны с углём, есть парочка пассажирских, состояние детально не видели. Но там по пути такое встретили... впрочем, потом."
    me "А на чём мы поедем?"

    show sh normal_smile shirt with dspr
    
    sh "Это самое интересное. Рядом стоит ангар, небольшой такой. И в нём, не поверишь — паровоз. Большой, с красной звездой."
    sh "Его бы в музей было хорошо, чем на рельсы, но какая разница? Руки, руки видишь?{w} Золотые!"
    sh "Поставим мы эту махину на пути и свалим в какой-нибудь Новосибирск, Иркутск, да хоть на Камчатку."
    
    stop ambience fadeout 3

    me "Во дела..."

    scene bg black with dissolve2
    pause(0.5)
    $ night_time()
    $ persistent.sprite_time = "night"
    play ambience ambience_catacombs fadein 3
    play sound3 dychanie_w_protivogaze loop fadein 1.5
    scene cg ch5_in_catacombs_sh
    show gas_mask
    with dissolve

    "Через какое-то время мы уже были в той самой комнате."
    "Шурик заметно нервничал, то и дело протирал очки, пока запускалось оборудование."
    sh "Ну что, машина, давай работать."
    "ЭВМ загорелась зелёным цветом."

    play sound staraya_klaviatura

    extend " Кибернетик, усевшись на стул, потихоньку что-то вводил на клавиатуре."
    sh "Ну же, ну же. Та-ак. Экстренный ввод... {w}Готово!"

    play sound pechataet_bumagu

    "Зашумела коробка рядом со столом и из неё медленно пополз большой лист бумаги с пометками."
    "Печатала коробка минуты три. Сразу разложив карту на столе, кибернетик медленно и вдумчиво рассматривал всё, что было напечатано."
    "В чёрных, местами плохо пропечатавшихся линиях, виднелся знакомый Союз."
    me "И что всё это значит?"
    sh "Смотри — на юг, в степи, нам никак нельзя. Новосибирск... Иркутск... странно. Не понимаю, не видел такого знака. Сейчас, погоди."

    play sound staraya_klaviatura_min

    "Он вернулся к ЭВМ, вбил какие-то цифры."
    me "Чего там?"
    sh "Вообще, это значит что уровень радиоактивного заражения не определён. Ты ловил сигналы оттуда?"
    me "Ни разу... разве что из правительственного бункера где-то в горах, но там... сам помнишь: придёшь — не жилец."
    sh "Так. Надо более подробные карты запросить."

    window hide
    show black with dissolve
    hide black with dissolve
    window auto

    "Просидели мы в бункере ещё порядка получаса."
    "Маска неприятно давила на переносицу и уже явно на ней отпечаталась, как вдруг Шурик, изучив очередную карту, смял её и бросил на пол. Кибернетик впился ногтями в ладони, поигрывая желваками."
    me "Ты чего? Шур?"
    sh "Подожди."
    me "Чё?"
    sh "Подожди, говорю."

    play sound chair_squeaky

    "Он хрустнул шеей, что-то проворчал, и обессилено опустился на стул."
    sh "Ты ж из Москвы?"
    me "Ну да."
    sh "А я из Киева. И Алиса твоя из Киева."
    me "И?"
    sh "И ни Москвы, ни Киева больше нет."

    play sound zvuk_AVM

    "В голову что-то ударило, на душе заскреблись кошки. Конечно, я особенно и не рассчитывал, но какая-то надежда теплилась. Была какая-то вера, что вернусь в свой дом, посижу с мамой на кухне с тёплым чаем, а всё, что происходит — просто чья-то глупая и жестокая шутка."
    "Тихо потрескивала, изредка мигая, лампа, шумела ЭВМ. Светлое прошлое, тёмное будущее."
    me "К чёрту. Забирай карты, пошли отсюда."
    "Кибернетик кивнул, достал со стеллажа какой-то металлический тубус, скрутил карты, положил их внутрь, но вдруг передумал."
    sh "Ща, погоди."
    me "Чего ещё?"
    sh "Хочу ближайший город по пути посмотреть."
    me "Валяй."
    "Он что-то недолго высматривал на карте, протёр очки в очередной раз и в конце концов медленно протянул:"
    sh "Хуцк."
    me "Не слышал о таком."
    sh "Небольшой городишко, там даже более-менее чисто. Не сказать, что пригодно для жизни, но переживать я бы не стал."
    me "Ну Хуцк, так Хуцк."

    stop sound fadeout 2
    stop sound3 fadeout 2
    stop ambience fadeout 3
    scene bg black with dissolve2
    pause(0.5)
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_ext_road_evening fadein 3
    scene bg ext_square_sunset with dissolve2

    "Кое-как вылезли из бункера, сразу стянули маски и вдохнули полной грудью жёсткий, как наждак, воздух. Откашлялись, потихоньку потащились в административный корпус."

    window hide
    show sh normal shirt with dissolve
    window auto

    sh "Вы мерили этих?"
    me "Дозиметром?"
    sh "Естественно. Они ж непонятно где шлялись, небось нахватались по самое не балуй."
    "Вообще Шурик говорил вполне ясную истину, но как бы они ни фонили, всё равно это были, похоже, первые люди, которые не были враждебно настроены к нам."
    hohol "Я, кстати, тоже не был враждебно настроен. Даже чаем напоил."

    stop ambience fadeout 3

    th "Изыди, падла."

    scene bg black with dissolve
    scene bg int_admins_corridor with dissolve

    "Настроение было упадочное. Хотелось спать, обниматься с Алисой, но точно не работать. Тем не менее реальность была несколько другого мнения."
    "Отправив Шурика с картами к вожатой — у неё хоть чуть-чуть побольше опыта — сам решил наведаться к медсестре: забрать пару таблеток от кашля для Алисы и посмотреть на Олега."
    
    show sl normal coat at left
    show un normal coat at right
    show ir normal pioneer
    with dissolve
    
    "В коридоре встретил Славю с Леной, активно окучивающих Игоря, уже переодетого в форму."
    me "Игорь, а Алеся где?"
    ir "Семён, да? Она устала, спит."
    me "Да, Семён. Хорошо."

    scene bg int_admins_corridor with dissolve
    play sound sfx_knock_door2

    "Перед тем как зайти в комнату, заменившую медпункт, по привычке постучался."
    cs "Войдите."

    scene bg int_aidpost_day
    show cs normal ag_civil
    with dissolve

    cs "Привет, Семён."
    me "И тебе не хворать. Как наш больной?"

    show cs shy ag_civil with dspr

    "Виола, столкнувшись со мной взглядом, неожиданно покраснела, отвернулась, продолжая протирать фотографию физрука в рамке."
    cs "Пациент в состоянии средней тяжести. Он пережил несколько пароксизмов, как я поняла, воспалённые лимфоузлы, в лёгких шумы, температура."

    show cs normal ag_civil with dspr

    "Виола вздохнула."
    me "Радиация?"

    show cs smile ag_civil with dspr

    cs "Не столько радиация, сколько обычная сырость. Я дала ему лекарства, ещё укольчик сделаем и всё совсем хорошо будет."
    me "В баню ему нельзя?"

    show cs normal ag_civil with dspr

    cs "С температурой — ни в коем случае."

    stop ambience fadeout 3
    play sound sfx_chair_fall
    with vpunch

    "Что-то сильно толкнуло в спину, я упал на колени."

    play music music_list["torture"] fadein 3
    show dv cry ag_sport at left with easeinleft

    "В дверях была Алиса, запыхавшаяся и с глазами навыкат."
    dv "Виола, там это... Алесе плохо."
    "Медсестра схватила небольшую сумку."
    cs "Веди!"

    scene bg black with dissolve
    scene bg int_dv_room_rain with dissolve
    play sound chastoe_dyihanie_devushki fadein 2.0

    "Алеся лежала на полу, тяжело дыша и поскуливая, держась за живот."
    "Рядом была лужа чего-то кровавого и отвратительно пахнущего."
    me "Чего случилось?!"

    window hide
    show dv cry ag_sport at cleft with dissolve
    window auto

    dv "Она спала, а потом её стошнило и..."

    window hide
    show cs normal ag_civil at cright with dissolve
    window auto

    cs "Семён, ко мне."
    "Виола перевернула девочку на спину."

    play sound chastoe_dyihanie_devushki fadein 2.0

    cs "Подушку подай. Хорошо, та-ак, милочка, что с вами, сейчас посмотрим..."
    "Она быстро расстегнула рубашку Алеси, пропальцировала живот."
    cs "Семён, укол этот дай, сейчас в медпункт её потащишь."
    me "Понял."

    show dv shocked ag_sport with dspr

    dv "Она выживет же? Да?"

    stop music fadeout 3

    cs "Не знаю ещё."

    scene bg black with dissolve
    play music AR_tabula fadein 3
    scene bg int_aidpost_day with dissolve
    stop sound fadeout 1.5

    "Естественно, ни одно повреждение не проходит даром. Облучение — да, это мы проходили. Когда трещит голова, из носа рекой льётся кровь, а желудок так и норовит вывернуться, начинаешь ненавидеть вообще всё, что с этой войной связано."
    "Но мы не переживали голод. Лютый и жгучий голод. А им пришлось — просто чтобы выжить. Алеся-то выживет, выкарабкается, но кто ей гарантирует, что желудок не выкинет ещё раз такую штуку?"
    "Дозиметр около неё слабо потрескивал. Она потихоньку приходила в сознание и даже просилась в баню вечером с девочками. Виола обещала подумать, а сама решила сгонять в бункер: нужен был активированный уголь, который, как назло, кончился."
    "Я не стал долго сидеть в импровизированном медпункте. Со мной всё это время была Алиса, и перед тем как выйти, я с какой-то бешеной страстью её поцеловал."
    "Милый лисёнок.{w} Она растворялась в доброте и нежности. Ну вот и хорошо, ей и не надо знать всех гадостей этого мира. А мне надо, тогда я смогу с ними бороться."

    scene bg black with dissolve
    pause(0.2)
    scene bg ext_camp_entrance_sunset with dissolve

    "А пока мою шею оттягивал только ремень ружья, а спина приложилась к мокрой кирпичной стене лагеря."
    "От холода полез руками в карманы и нащупал что-то знакомое. Эту пару штанин я не носил уже давно: в них я ездил в Красное."
    "Под дождик из кармана выскочило то самое золотое колечко."
    me "Ох ты, а я тебя искал!"
    "Появилось время поразглядывать его поближе. Внутри была выгравирована надпись «Спаси и сохрани»: выгравирована явно после покупки, непрофессионально, но красиво. Золото как-то по-особенному поблескивало от капель дождя."

    window hide
    stop music fadeout 3
    play ambience ambience_ext_road_evening fadein 3
    show el hs_normal hazmat_suit with dissolve
    window auto

    el "Семён, давай дуй греться. Шурик там фотографии проявил."
    me "О, пост сдал — пост принял."
    "Я передал ружьё подошедшему Электронику и двинулся в лагерь."
    th "Мы уедем из этого ада. И всё у нас хорошо будет. Подождать, надо просто немного подождать."
    "Появилась у меня весьма неплохая идея после того, как кольцо нашёл. Надо было только с Ольгой договориться, а перед этим хорошенько поразмыслить."
    "Как раз из Красного в тот раз Шурик вынес промышленное масло, а автомат уже просил чистки."

    stop ambience fadeout 3
    scene bg black with dissolve2
    pause(1.0)
    play ambience ambience_medstation_inside_night fadein 3
    scene bg int_dv_room_rain with dissolve

    "Нашёл где-то тряпки более-менее чистые, разобрал автомат — всё, как на уроках ОБЖ. Начал чистить от закопченности и копоти."

    play sound chistka_avtomata

    me "Нам нет восемнадцати, но какая разница — такой уж мир стал. Да и возраст не важен."
    me "А согласится ли она... ну, пожалуй, да. А вот смысл от этого?{w} Никакого."
    me "Хотя, ну почему же никакого: одно дело, когда девушка, другое — жена.{w} Жена!"
    "Достал шомпол, намотал какую-то тряпку, чуть капнул масла."
    me "А что я Ольге скажу? «Товарищ вожатая, жените нас, пожалуйста,» — ну что за детский сад?"
    "Я чувствовал, как краснеют уши и щеки. Оставалась крышка ствольной коробки и магазин — его я уже чистил после Красного — грязи много попало, пружина начала ржаветь."
    "Сзади прижалось что-то тёплое."
    dv "Милый, ты чего тут?"
    me "Я...{w} Автомат чищу."

    window hide
    show dv smile ag_sport with dissolve
    window auto

    "Алиса уселась на стол рядом с разобранным автоматом."
    dv "А ворчишь о чём?"
    "Я встал из-за стола, подошёл к ней вплотную."
    me "А секрет."

    show dv grin ag_sport with dspr

    "Алиса загадочно улыбнулась, фыркнула, но дала поцеловать в щеку. Сзади послышался скрип пола."

    show dv normal ag_sport with dspr

    us "Тили-тили-тесто, жених и невеста."
    th "Вот же, пальцем в небо, а попала, маленький бесёнок."
    me "Привет, Ульян. Тебе чего?"

    window hide
    show us smile sport at right with dissolve
    window auto

    us "Да так, я вообще Алису искала."
    me "Соскучилась по подружке?"

    show us grin sport with dspr

    us "Соскучилась в принципе. На охоту в такую погоду нельзя, да и живность в норках сидит. Рыбалка тоже отлетает."

    show us normal sport with dspr

    us "Одни книжки и радио, больше нет развлечений. Новенькие эти: двое в лазарете, а Игоря Славя с Ленкой заняли."
    
    show us sad sport with dspr
    
    "Девочка грустно опустила плечи и состроила печальное личико."
    th "Ладно, плясать, так по-крупному."
    me "Уль, а можешь за Ольгой сходить?"

    show dv surprise ag_sport with dspr

    dv "Зачем?"
    me "Так надо."

    show us smile sport with dspr

    us "Нет, пока не расскажешь, не пойду!"
    me "Обещаю, что всё увидите."

    window hide
    hide us with dissolve
    window auto

    "Девочка долго не думала и рванула на второй этаж, за вожатой."
    dv "Чего это ты придумал?"
    me "Раз один союз пал, другие надо заключать."

    show dv normal ag_sport with dspr

    "Рыженькая всё ещё ничего не понимала, но со стола не слезла и принялась активно думать."
    mt "Семён?"
    us "Он тут."

    window hide
    show mt normal sport at left behind dv
    show us smile sport at right behind dv
    with dissolve
    window auto

    me "Ольга Дмитриевна, ты у нас за старшую, так что..."

    show dv surprise ag_sport close with dissolve

    "Я взял Алису за талию и поставил на пол."
    "Не отрывая взгляда от моей девушки, которая уже сейчас грозилась перейти в другое формальное состояние, я заговорил:"
    me "Оль, ты была когда-нибудь на свадьбе?"
    mt "Да, бывала. Персунов, ты что делаешь?"

    stop ambience fadeout 3
    play music music_list["memories"] fadein 3
    show mt surprise sport
    show us surp3 sport
    with dspr

    "Я полез в карман и выудил кольцо. Не сразу вспомнил, что нужно встать на одно колено."
    me "Алиса, ты выйдешь за меня?"
    "Алису было жалко. Девушки пока что слабо понимали, что вообще происходит, а она понимала очень даже хорошо, только обалдела от всего происходящего настолько, что чуть не впала в ступор."
    dv "{cps=30}Я...{w} я...{/cps}"

    show dv grin ag_sport close with dspr

    dv "А пожалуй и выйду!{w} Я согласна!"

    show us surp1 sport with dspr

    us "Тили-тили..."

    show mt grin sport with dspr

    "Ольга шикнула в улыбающуюся и краснеющую одновременно Ульяну, хитро подмигнула мне."

    show mt smile sport
    show us smile sport
    show dv shy ag_sport close
    with dspr

    mt "{cps=50}Как старшая в лагере, объявляю вас{w=1.5} мужем{w=1} и женой.{w}{/cps} {size=25}Сём, ты где кольцо надыбал?{/size}"
    me "{size=25}А вот места надо знать.{/size}"
    "Я усмехнулся, поднимаясь с колена и надевая Алисе на палец колечко. Оно было ей чуть велико, совсем немного, но в наших-то условиях..."

    show dv grin ag_sport close with dspr
    
    dv "Это что я, больше не Двачевская?"
    us "У-у-у, что начинается..."
    mt "Можешь оставить фамилию, если хочешь."
    "Я кивнул в такт Ольге."
    dv "Алиса...{w=1.3} Алиса Персунова, Персунова..."

    show dv smile ag_sport close with dspr

    dv "Подходит, беру."

    show mt laugh sport
    show us laugh sport
    show dv laugh ag_sport close
    with dspr

    "Дружно рассмеялись."

    show dv smile ag_sport close
    show us grin sport
    with dspr

    us "Оль, а когда я найду себе жениха, ты нас поженишь?"

    show mt smile sport with dspr

    mt "Поженю. {w}Только, это, Сём, если Шурик сегодня что-то ещё такое с моим участием надумает, передай, что я вам не ЗАГС!"
    me "А по-моему, у тебя хорошо получается."

    show mt grin sport with dspr

    mt "Ох, Семён, тьфу на тебя. Вот был у нас один Персунов, а теперь их двое."
    me "Не бойтесь, мы памятники серой больше взрывать не будем."

    show mt smile sport with dspr

    mt "Хотелось бы верить."
    me "Для них лучше тротил."
    "Алиса заулыбалась, взяла меня за руку."
    dv "Слушай, какое красивое..."
    me "Только для тебя, милая."
    "И девушка ещё крепче вцепилась в меня. Тепло, по-доброму, без страха. Рядом лежал разобранный автомат, тихо-тихо трещала настольная лампа."

    play ambience ambience_medstation_inside_night fadein 3
    stop music fadeout 3
    window hide
    hide mt
    hide us
    with dissolve
    window auto
    
    "Ольга и Ульяна куда-то ушли, остались только мы. Температура в помещении потихоньку начинала падать и я отметил про себя, что хорошо бы подкинуть дров в котельной."
    if persistent.easter_egg == True:
        th "О чём я только думаю? Я только что женился, нахуй мне дрова?!"
    else:
        th "О чём я только думаю? Я только что женился, какие дрова?!"
    dv "Я тебя люблю, Сём."
    me "И я тебя, рыжее, моё счастье."

    show dv surprise ag_sport close with dspr

    "В комнату неожиданно кто-то вошёл. Послышался странный, для говорящей, тяжёлый и хмурый голос."

    show dv surprise ag_sport close:
        xalign 0.645
        xanchor 0.5
        yanchor 1.0
    with ease
    if persistent.easter_egg == True:
        show sl laugh swim far at fleft with easeinleft
        play sound2 oleg
        sl "У Олега сердце остановилось.{image=ag_lmao} {image=ag_lmao} {image=ag_lmao} "
    else:
        show sl scared coat far at fleft with easeinleft
        sl "У Олега сердце остановилось."


    play sound cinematic_kick
    scene bg black
    pause(2.5)
    play sound cinematic_kick
    play music release fadein 3
    scene bg int_aidpost_night
    show cs normal ag_civil

    me "Ну же, мать твою... Виола, вытащим его?"
    "Медсестра молчала, энергично делая непрямой массаж сердца."
    cs "Вытащу... должен, должен же. Подожди, малой ещё... Вот, вот так, дыши."

    stop music fadeout 3
    play ambience ambience_medstation_inside_night fadein 3

    "Парень вроде отходил, я поддерживал его голову так, чтобы проще было дышать и делать искусственное дыхание — чуть запрокинув назад."
    th "Обошлось."

    show ir shock pioneer at left behind cs with dspr
    play sound male_cough

    if persistent.easter_egg == True:
        "Олег закашлялся. В комнату влетел, чуть не сбив меня, Алису и Виолу И-и-и-игорь."
    else:
        "Олег закашлялся. В комнату влетел, чуть не сбив меня, Алису и Виолу Игорь."
    ir "Он живой?"
    me "Ну, как видишь."
    cs "Только без должного лечения, боюсь, ненадолго."
    ir "А как же?.."
    cs "Так, новобрачные и пионер, ну-ка, вон отсюда, больному покой нужен. Давайте, всё."

    scene bg int_admins_corridor with dissolve

    "В конце коридора Алеся о чём-то увлечённо болтала с Ульяной. Девочки, видимо, быстро нашли общий язык."
    "Пообещав Алисе очень скоро вернуться, во что сам свято верил, я отправился к Шурику за фотографиями. Было любопытно взглянуть на поезд и дорогу к нему."
    "Алиса от этой затеи почему-то отказалась, ушла в нашу комнату, видимо, любоваться кольцом. К Шурику я шёл в целом с поднятым настроением."
    me "Входить можно?"

    play sound sfx_knocking_door_2

    "Я постучался в дверь. Никогда фотографией не увлекался, зато хорошо знал, что для проявления плёнки свет губителен."

    stop ambience fadeout 3

    sh "Заходи."

    $ persistent.sprite_time = "night"
    play music seven_summer_day fadein 3
    scene bg int_clubs_male2_night_nolight
    show sh serious shirt
    with dissolve
    $ night_time()

    "Шурик сидел на табуретке посреди комнаты, сгорбившись и потирая щетинистый подбородок. В руках он держал уже готовый снимок."
    me "Чего грустим, кибернетика?"

    play sound sfx_paper_bag
    scene cg ch_8_busphoto with dissolve

    "Он протянул бумагу. В плохом освещении не сразу разобрал, что за куча мусора была на фотографии."
    "А потом я узнал родной «Икарус». Автобус перевернуло, крышу смяло, стёкла вылетели, были видны следы от огня. Угадывались еле различимые силуэты пассажиров."
    sh "Это моё покаяние. Алеся, та девочка, которую я убил, не факт, что была облучённой, это, скорее всего, от пожара."
    me "Но откуда? Откуда здесь... Сколько там до моста? Километров тридцать пять. Что там делал автобус?"
    "Я отложил фотографию. Смотреть на снимок было как-то боязно."

    play sound sfx_paper_bag
    scene bg int_clubs_male2_night_nolight
    show sh normal shirt
    with dissolve

    sh "Не понимаешь? Они не успели доехать до города, когда рвануло. Это, похоже, был последний в колонне автобус... не важно."
    sh "Они хотели вернуться в лагерь. Помнишь, когда ракета по ГЭС долбанула: сначала небольшой взрыв услышали, а только потом накрыло?"
    me "Да, помню."
    sh "А я всё головушку ломал. Это были два разных взрыва. Первым окончательно добили ближайший город, вторым — нашу ГЭС. Автобус повело, и... сам видишь."
    me "Что теперь?"
    sh "На север нам точно нельзя. Хотя, в общем-то, не особо и планировали. Да и на юг тоже нельзя. Восточное направление, Хуцк, потом, может, через Каспий — дальше как получится."
    me "Сколько ехать?"

    show sh serious shirt with dspr

    "Кибернетик развёл руками."
    sh "Придётся, если заведём ту махину, идти малым ходом. И постоянно дежурить. Аномалии, сломанные и затопленные пути, ещё чёрт знает что."
    sh "По мостам тоже небезопасно. Месяца три, может, и пять."
    me "Приехали..."
    "Нас ждало рандеву с дьяволом. Как-то не нравился мне и ветер с востока, и само это направление, а теперь нам только туда дорога."
    sh "Все мы там будем."
    me "Чего?"
    sh "В аду."
    me "Ты же не веришь."

    show sh normal shirt with dspr

    sh "Я вообще мало во что теперь верю. Но все мы там будем."
    "Сидели молча минут десять."
    "В дверь постучали."
    sh "Можно, войдите."

    show un shy coat at left with easeinleft

    "В дверях была Лена. Кибернетик быстро убрал злополучный снимок."
    un "Мальчики, а фотографии уже готовы?"
    sh "Да, смотри."
    "Он протянул Лене несколько снимков."

    show sh serious shirt with dspr

    sh "Вот дорога, вот подъезд к станции, аномалия местная. А вот и поезд, наш будущий дом."

    show un smile coat with dspr

    un "Красиво. Молодцы вы, всё-таки. Дом, говоришь? А как назовём?"
    "Мы с Шуриком переглянулись. В голову закралась забавная идея."
    me "Ну смотри, как лагерь наш называется?"
    "Лена посмотрела на меня недоуменно."
    un "«Совёнок»..."
    me "Ну вот, раз это — «Совёнок», то поезд — это уже «Филин»!"

    show un smile2 coat with dspr

    "Девушка улыбнулась."
    un "«Филин»? Ну пусть будет «Филин»."

    show sh smile shirt with dspr

    "Шурик чуть улыбнулся."
    sh "Гений философской мысли, куда ни глянь."

    show un smile coat with dspr

    un "Хорошего мужа себе Алиска нашла."

    show sh surprise shirt with dspr

    sh "Мужа?"
    me "Женюсь я сегодня."

    show sh normal_smile shirt with dspr

    sh "А почему я узнаю это последним?"
    me "Так ведь только что."
    un "Смешные вы, ребят. Пойду я, там Алиса на чаепитие приглашала."
    me "Девичник?"
    un "Нет, тебя тоже попросила забрать."

    show sh smile shirt with dspr

    sh "Иди, жених. Я тоже скоро с Мику приду, а если нет — на ужине ждите."
    "Я кивнул и двинулся за Леной."

    stop music fadeout 3
    play ambience ambience_medstation_inside_night fadein 3
    scene bg int_admins_corridor with dissolve
    $ persistent.sprite_time = "sunset"
    $ sunset_time()

    me "Чем сегодня занимались?"

    show un smile coat with dspr

    "Лена обернулась."
    un "Паковали какие-то вещи, что-то ещё по мелочи. Потом Игорю лагерь показывали."
    me "Понравился?"

    show un shy coat with dspr

    "Лена чуть покраснела, смутилась."
    un "Ну он довольно милый, но на Славю больше внимания обращает...."
    me "Я вообще-то спрашивал, понравился ли лагерь Игорю..."

    show un smile coat with dspr

    un "Ой, ну тебя!"
    un "Устали мы сегодня. Вот они нашлись, а как, интересно, остальные отряды, живы ли?.."
    "По совести полоснул калёный металл. Разбирала какая-то внутренняя, глубинная злоба. Когда хочешь всё вернуть как было, да нельзя. Невозможно."
    "Как только, сука, додумались — всю планету в пепел? Плевать я хотел и на партию нашу и на их демократию — «проклятый капитализм». Плевать хотел с высокой колокольни. Миллионы — одной кнопкой."
    "Ладно, сейчас не до этого. Я вообще-то женюсь!"

    scene bg int_dv_room_rain with dissolve
    stop ambience fadeout 3
    play music music_list["your_bright_side"] fadein 3

    "Пришли в нашу с Алисой комнату. Моя рыженькая уже сделала чай, всё тот же из дефицитных запасов. На подоконнике лежали три куска сахара."

    play sound nasvistyivanie_korotkoe

    "Я присвистнул."

    window hide
    show dv smile ag_sport at cright with dissolve
    window auto

    me "Родная, а ты это откуда?"
    "Я показал пальцем на сахар. Его было невероятно мало даже в запасах бункера, будто кто-то разворовывал периодически."

    show dv smile ag_sport close at right with dissolve

    "Алиса подошла ко мне, поцеловала, подмигнула. Она выглядела невероятно счастливо. Наверное, никогда её такой не видел: ни до катастрофы, ни после."
    dv "Ольга подарила."

    window hide
    show un shy coat at cleft with dissolve
    window auto

    un "Можно с вами?"

    show un smile coat with dspr

    dv "Естественно. Я третью кружку достала."
    "Недолго думая, Лена устроилась перед нами на моей кровати, которую редко использовали. Взгляд девушки был туманным и сонным."
    "Я сел на нашу с Алисой. Рыжик устроилась рядом, прижалась к плечу."

    play sound serbaet_chaj

    extend " Я отпил чуть сладкого чая, поцеловал её в макушку, поставил кружку чая на колени."
    un "Не возражаешь?"
    "Лена улеглась на подушку."
    me "Сколько угодно."
    me "Лисёнок, ты как себя чувствуешь?"
    dv "Замечательно!"
    "Она покрутила кольцом на пальце."
    me "Я тебя люблю."
    dv "Ты мой хороший."

    show un smile2 coat with dspr

    un "Вот смотрю я на вас и завидую. Всё у вас хорошо, а на нас больше мальчиков нет."
    "Девушка зевнула."

    stop music
    play sound sfx_muffled_explosion
    with hpunch
    show dv surprise ag_sport close
    show un surprise coat
    with dspr

    "Я хотел было что-то возразить, но в небе что-то гулко треснуло."
    me "Это ещё что?"

    play sound sfx_open_door_kick
    show el shocked shirt at fleft behind un with easeinleft

    "Лена чуть пролила свой чай, Алиса испуганно посмотрела на меня, а в комнату влетел обалдевший Электроник. Всё на секунду застыло."
    el "Аномаль!"

    scene bg black with dissolve
    pause(1)
    $ persistent.sprite_time = "night"
    $ night_time()

    camera:
        perspective True
        zpos -100
        linear 0.10 xpos 0.007
        linear 0.10 xpos -0.007
        linear 0.10 xpos 0.007
        linear 0.10 xpos -0.007
        repeat

    play music hammer volume 0.3
    scene bg int_dining_hall_night with dspr

    "Конечно, наши светила науки прогнозировали и такое, но я всей душой надеялся, что подобного никогда не произойдёт. Землю трясло, мы все сидели в здании столовой."
    "Свет вырубило практически сразу, барометр сходил с ума, а в ушах звенело."
    "Я обнимал едва слышно хлюпающую носом Алису."

    window hide
    show sh upset pioneer with dissolve
    window auto

    "Ко мне подполз Шурик. Его, видимо, тоже плющило сильно."
    me "Долго ещё это будет..? А то мы все тут скопытимся такими темпами."
    sh "Бля-я... не знаю я!"
    th "И, как назло, прям на свадьбу!"
    sh "Только эта штука... не локальная, такое щас много где, по всей планете..."
    me "Мне, мля, похрену, что там не локально...{w} Ну, давай же, кончайся!"

    window hide
    hide sh with dissolve
    window auto

    "Шурик ещё какое-то время сидел рядом, потом отполз назад. Из носа полилась кровь, оставалось сглатывать. Тяжелее всего переносили эту дьявольщину Ульяна и Алеся. Остальные были примерно в моём состоянии."
    me "Лисёнок, Алис, держись. Держись, пожалуйста, сейчас всё хорошо будет, скоро... как там... Отче наш, иже еси на небеси...."
    "Я тихо орал молитву, хоть на что-то надеясь, стараясь перекричать этот отвратительный монотонный гул в ушах."
    "Не помогало, естественно. Минут пятнадцать это ещё продолжалось, все то молились, то матерились, кто-то катался по полу от боли. "

    stop music fadeout 5
    camera:
        perspective True
        zpos -100
        ease 0.5 zpos 0 xpos 0.0
    play sound_loop2 heart_beat fadein 3
    stop sound_loop fadeout 3

    extend "И вдруг, так же неожиданно, как началось, всё закончилось."
    "Первое время все так и лежали, прижавшись к стенкам. Потом встала Виола, за ней Шурик, я, Ольга, Славя, Алиса."
    "О почётном ужине речи уже не шло. Ольга посмотрела на часы и так и застыла."

    play music exodus fadein 3
    stop sound_loop2 fadeout 3
    window hide
    show mt sad sport at left with dissolve
    window auto

    mt "Три с половиной часа прошло..."
    me "Чего? Мы тут минут тридцать-сорок, не больше."
    "Не бывает так, чтобы время меняло свой ход, такого просто не бывает!"
    "Шурик, отплевываясь от крови, прижал к себе Мику, которая, на удивление, легче всех переносила творящийся кошмар."

    window hide
    show sh upset shirt at right
    show mi serious jacket at fright
    with dissolve
    window auto

    mi "А я летала часто, у меня, этот, вестибулярный аппарат хороший!"

    show mi surprise jacket:
        pause 0.5
        ease 0.7 xalign 0.84 xanchor 0.5 yanchor -1.0
    with dspr
    pause(0.4)
    play sound sfx_chair_fall
    show sh scared shirt with dspr

    "И тут же упала без сознания. "

    scene bg int_dining_hall_night with dissolve

    extend "Шурик опешил, подхватил её, похлопал по щекам. Виола где-то нашла нашатырь, японку привели в чувства."

    window hide
    show dv sad ag_sport close with dissolve
    window auto

    dv "Сём, пошли на улицу, душно мне тут."
    "Умоляюще попросила Алиса."
    "Мне самому не хватало чистого воздуха."

    scene bg ext_dining_hall_near_night with dissolve

    "Часы Ольги не соврали, на улице был уже вечер, только было от чего-то очень холодно. Не сыро, не противно, даже не ветрено — просто холодно."
    "Я прижимал Алису к себе, слушая, как бьётся её сердце, гладил голову, и дышал ледяным, наполненным озоном воздухом."
    "Рядом кибернетики пронесли тело Олега. Парень не выдержал таких нагрузок, откачивать было уже бесполезно."
    "Не хотелось ни о чём думать, совершенно ни о чём, только я помнил, что говорил Василий."

    play sound sfx_wind_gust
    $ persistent.sprite_time = "sunset"
    scene bg int_unknown_house
    show hl smile_1
    show prologue_dream
    with flash
    $ sunset_time()
    hohol "Электронка-то? Как устроена, отчего образовалась — не знаю. Знаю только, что они не вечные и появляются после бури."

    play sound sfx_wind_gust
    scene bg ext_dining_hall_near_night with flash
    $ persistent.sprite_time = "night"
    $ night_time()

    "Аномалия, оберегающая наш поезд, скорее всего, переместилась куда-то. А значит, у нас оставалось максимум полтора месяца."

    window hide
    stop music fadeout 3
    scene black with dissolve2
    pause(4)


    jump agr_exodus