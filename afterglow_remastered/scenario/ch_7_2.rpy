label agr_day_7:

    $ backdrop = "days"
    $ agr_new_chapter(11, "За горизонт")
    $ persistent.sprite_time = "day"
    $ day_time()
    pause(1)
    window auto
    $ karta_otkrytie = False

    play sound_loop rain_inside fadein 5
    pause(2.5)
    scene bg int_house_of_dv_day with dissolve2
    pause(1)
    play music philosophy fadein 3

    "На улице моросило. Пожалуй, самая отвратительная погода, да что с ней поделаешь?"
    "Алиса, укутавшись в плед, лежала рядом, положив голову мне на плечо."

    window hide
    show dv normal ag_sport close with dissolve
    window auto

    dv "Ты уверен, Сём?"
    "Прошептала девушка, покусывая трескающиеся губы. Я тяжело вздохнул, погладил её по голове, потом не выдержал и прижал к себе."
    me "А когда мы были в последний раз хоть в чём-то уверены наверняка?"

    show dv smile ag_sport close with dspr

    dv "Да, верно..."
    me "Это же всё для вас. Для будущего нашего."

    show dv sad ag_sport close with dspr

    dv "А про меня ты подумал?"

    show dv shy ag_sport close with dspr

    extend " Я же тебя, дурака, люблю!"
    "На некоторое время повисла тишина."
    me "Подумал. Именно по-этому и еду. Так надо, солнце, поверь. Я вернусь."

    show dv sad ag_sport close with dspr

    dv "Береги себя, пожалуйста. Там фонит, наверное, сильно."
    "Она крепко сжала мою ладонь."
    me "Часть радиации уже ушла, да и в самое пекло мы как-то не собираемся."

    show dv normal ag_sport close with dspr

    dv "Они хоть водить умеют?"
    me "Понятия не имею."

    show dv laugh ag_sport close with dspr

    "Отчего-то рассмеялись."

    show dv smile ag_sport close with dspr

    me "Всё, пойду. А ты, давай, выздоравливай и не смей больше болеть."
    "Алиса мило улыбнулась, шмыгнула носом."

    scene bg int_house_of_dv_day with dissolve

    "На выходе из домика ждал знакомый костюм химзащиты, противогаз и автомат."
    "Противогаз в сумку, автомат на шею, химзащиту надел. Перед тем, как уходить, поцеловал рыженькую так, что даже забылся на минуту."
    "Перед тем, как выйти, погасил керосиновую свечу."

    stop sound_loop fadeout 3
    scene bg black with dissolve
    play ambience light_rain fadein 3
    pause(1)
    scene bg ext_house_of_dv_day
    show el hs_normal hazmat_suit at left
    show sh normal hazmat_suit at right
    with dissolve

    "На крыльце уже сидели кибернетики."
    me "Привет."
    el "И тебе не хворать."
    me "Папаша, а тебя как твоя отпустила?"
    "Шурик поднял голову, на которой уже виднелась седина не по годам."
    sh "Не спрашивай. Пошли?"
    el "Погоди, посидим на дорожку."
    "Присели на крыльцо. Отвратительная влага всё капала на лицо, будто проходя сквозь кожу, прямо к натянутым, как струна, нервам."
    "Какой-то тёмный страх, знакомый ещё с катакомб старого лагеря, прожигал сердце. Страх неизвестности, нового. Бутафория судьбы какая-то."

    stop music fadeout 4

    sh "Пошли."

    stop ambience fadeout 3
    scene bg black with dissolve
    $ set_mode_nvl()
    pause(1)
    if persistent.easter_egg == True:
        play music phonk_volga fadein 3
    else:
        play music black_volga fadein 3
    scene cg ch_7_volga with dissolve

    "Старенькая «Волга» завелась не сразу. Электроник за рулём всё ругался. В салоне было душно от изоляции. Окна закрыты, всё проклеено какой-то плёнкой. Что ж, хотя бы без противогаза, да и здоровье важнее."
    "Машина тронулась."
    sh "Во-о-от, пошла, родимая.{w} Да не гони ты так! Осторожнее на повороте..."
    "Электроник рулил неумело, но как мог старался."
    "У ворот стояла Лена, в руках у неё был замок. Она улыбнулась, помахала нам рукой, что-то сказала, но из машины нихрена слышно не было."
    me "Сколько времени?"
    sh "Половина девятого. К вечеру должны вернуться.{w} Надеюсь."
    el "Ты меньше разглагольствуй, за картой смотри. Куда на первом повороте?"
    sh "Налево. Сейчас пойдём аккурат около берега, потом через лес, а там и Красное."

    window hide
    show prologue_dream with dissolve
    $ nvl_clear()
    window auto

    "Всё началось с любопытства Слави.{w} Она как-то целый вечер после работы с посадками слушала радио и ухватила весьма интересный радиосигнал, как сейчас помню...{nw}"
    #*аудиодорожка, скину*
    extend " "
    "После этого целую неделю сидели, как мыши. И днём и ночью дежурили по очереди. Алиса тогда и простудилась, не сильно, конечно, даже температуры высокой не было, но в наших реалиях даже такая мелочь могла убить."
    "Виола с Ульяной постоянно возились с лекарствами для неё. Я сам помогал, чем мог, но большую часть времени приходилось уделять благоустройству административного корпуса."
    "Когда рыженькая пошла на поправку, Электроник предложил съездить в Красное. Как бы страшно то ни звучало, ехали мы не эвакуировать тех, кто остался в живых...{w} Точнее, это не было основной задачей."

    window hide
    stop music fadeout 3
    hide prologue_dream with dissolve
    $ set_mode_adv()
    play sound_loop sfx_intro_bus_engine_loop fadein 3
    play ambience rain_inside fadein 3
    window auto

    sh "Товарищи и будущие мародёры, не вешаем нос. Ориентировочно прибудем через сорок пять минут."
    sh "Семён, вон, рация стоит, проверь."
    "На соседнем сидении лежал небольшой мешок, сшитый явно из двух сумок от противогаза."
    "Щёлкнули выключатели, в наушники пошёл мерзкий, белый шум."
    
    play sound sfx_radio_squelch_1
    
    me "{image=ag_speaker} Совёнок, я Волга, как слышите?"
    "Несколько секунд продолжались помехи, и я уже было напрягся, но услышал знакомый голос вожатой."
    
    play sound sfx_radio_squelch_2
    
    mt "{image=ag_radio} Волга, я Совёнок, слышим вас хорошо. Проверка связи?"
    
    play sound sfx_radio_squelch_1
    
    me "{image=ag_speaker} Она самая."
    
    play sound sfx_radio_squelch_2
    
    mt "{image=ag_radio} Хорошо. Берегите себя, мальчики, ждём новостей.{w=1} Отбой."
    
    play sound sfx_radio_squelch_2
    
    me "{image=ag_speaker} Отбой."
    "Машина ехала медленно, осторожно огибая упавшие деревья и особо крупные трещины в асфальте. Природа побеждала остатки цивилизации."
    "Топлива было достаточно, хватило бы и до города доехать, но становиться камикадзе в наши планы не входило."
    el "Как думаете, что там будет?"
    sh "И думать не хочу. Люди сейчас опаснее любой радиации, хотя, может, и не озверели ещё."
    me "Не думаю, что там кто-то есть."
    sh "Тоже верно. Но, знаешь..."
    "Он обернулся и постучал по стволу автомата, лежащего у меня на коленках."
    
    # if persistent.easter_egg == True:
    #     play sound bezumno
    
    sh "Лучше иметь и не нуждаться, чем нуждаться и не иметь."
    el "Чья цитата?"
    sh "Моя."
    "Машина прибавила ходу. Местами из-за дождей асфальт совсем размыло. Появлялись неприятно отпечатывающиеся на теле бугорки и ямки."

    stop sound_loop fadeout 3
    stop ambience fadeout 3
    scene bg black with dissolve
    pause(1)
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    play sound nadel_protivogaz
    scene bg ext_bus_stop_abandoned
    show gas_mask
    with dissolve2
    play music metro_teardrops fadein 3
    play sound2 light_rain loop fadein 2
    play sound3 dychanie_w_protivogaze loop fadein 1.5

    "Не торопясь выходить из машины, я выдернул затычку из фильтра и аккуратно надел противогаз."
    "Хрустнул шеей, глубоко вздохнул, привыкая к холодной резине на голове, взял вещмешок, автомат и наконец-то вылез."
    "Перед нами была автобусная остановка. Краска и штукатурка уже порядком облезли, дорожный знак упал и начал покрываться ржавчиной."

    window hide
    show sh serious gas_mask_on at right behind gas_mask with dissolve
    window auto
    play sound tresk_gaygera_piz loop

    sh "Живописно."

    window hide
    show el hs_normal gas_mask at left behind gas_mask with dissolve
    window auto

    el "Чего?"

    show sh normal gas_mask_on behind gas_mask with dspr

    sh "Живописно, говорю.{w} Пошли, Красное — там."
    "Он махнул рукой за остановку. В противогазе было сложно разговаривать."
    "Счётчик методично потрескивал и этот треск отдавал в мозг, напоминая, что слишком долго на улице без противогаза нельзя."
    me "Что по фону?"

    show sh serious gas_mask_on behind gas_mask with dspr

    sh "Почти запредел. Тут всё умерло."
    me "Долго тут можно?"

    show sh normal gas_mask_on behind gas_mask with dspr

    sh "Даже в костюмах не дольше трёх часов, иначе опасно."
    "Вокруг всё и вправду было мёртвое: ни людей, ни зверья, вокруг была только смерть, грязь и слякоть."
    "Мы медленно двинулись по размытой дороге, ведущей в деревню. Вокруг была диковатая, страшная тишина. Ничего и никого вокруг, и эта тишина размеренно давила на мозги."
    me "Откуда здесь такой фон?"

    show sh serious gas_mask_on behind gas_mask with dspr

    sh "Я, в общем-то, не уверен, что здесь даже в противогазе дышать можно. Фильтры рассчитаны только на пыль и что-то ещё, по мелочи."
    
    # if persistent.easter_egg == True:
    #    el "Не боись, українська розвідка бессмертна."
    #else:
    
    el "Не бойся, разведка бессмертна."
    sh "А фон такой потому, что ближе к ГЭС."
    me "Но не до такой же степени!"

    show sh normal gas_mask_on behind gas_mask with dspr
    stop sound fadeout 1.0

    sh "И до такой тоже..."
    "Дальше шли молча, минут десять точно. Палец никак не хотел уходить со спускового крючка, хоть переживать было не о чем и оружие было на предохранителе."
    el "Это ещё что..."

    scene bg ext_grave_yard
    show gas_mask
    with dissolve

    "Я посмотрел в сторону, куда показывал кибернетик. Посреди местного кладбища лежали кучи какого-то мусора вперемешку с деревом, чем-то напоминающие тот самый, пионерский костёр."
    sh "Ну его, пошли. Мы здесь не за покойниками."
    "Мы развернулись в сторону деревни, даже прошли шагов пять, но..."

    $ agr_meet('hohol', u"Голос")
    stop music
    play sound sfx_scary_sting
    with vpunch

    hohol "Здоровенько, хлопцы."

    play music fortress_of_doom fadein 3

    "Неизвестный голос заставил вздрогнуть. Он казался неестественно громким, почти оглушительным в местной тишине."
    "И только сейчас дошло, что всё это время с кибернетиками мы разговаривали шёпотом, непонятно почему."

    play sound predochranitel_ak

    "Щёлкнул предохранитель автомата, я развернулся."

    window hide
    scene bg ext_forest_sunset
    show hl smile_2 at cleft
    show gas_mask
    with dissolve
    window auto

    "Перед нами стоял какой-то дед, лет шестидесяти пяти на вид. Одет он был вполне привычно для довоенного времени."
    me "И вам не хворать.{w} Вы кто?"

    stop music fadeout 3
    play ambience ambience_ext_road_night fadein 3

    hohol "Я-то? Василий Лаврентьевич, лесник я здешний."

    $ agr_meet('hohol', u"Василий")
    show hl wink_2 behind gas_mask with dspr

    hohol "Вы-то откудойть?"
    sh "Мы...{w} пионеры."

    show hl laugh_2 behind gas_mask with dspr

    hohol "О-о-о, пионэры. Ну что, пойдёмте тогда. Чай пить будем.{w=2} Пионэры..."
    el "Куда?"

    show hl smile_2 behind gas_mask with dspr

    hohol "Да тут недалеко, в лесу, избушка моя. А чой-то вы в резинках этих? Промокнуть боитесь?"
    me "Это химзащита, Василий Лаврентьевич. От радиации. У вас разве нет?"
    "Дед выглядел вполне здоровым."
    hohol "Так на кой мне-то? Я ж в избушке живу, со старушкой моей, сюда не выбираюсь."
    "Мы медленно двинулись за уходящим в лес стариком. Рядом стоящий Шурик зашипел."

    window hide
    show sh normal gas_mask_on close at fright behind gas_mask with easeinright
    window auto

    sh "Семён, валим. Не может он живым быть, если близко у него избушка: помер бы давно."
    hohol "Да и хренотень у нас какая-то в огороде завелась, искрится, свет сам горит — уже три лампочки поменял."
    th "Такая же аномалия, как у нас... вокруг нашей тоже чистое поле было, почти не потрескивал."

    window hide
    hide sh with easeoutright
    window auto

    "Шурик приутих."

    window hide
    show el hs_normal gas_mask at fright behind gas_mask with dissolve
    window auto

    el "Василий Лаврентьевич, а в деревне осталось вообще что?"

    show hl moody_1 with dspr

    hohol "Мородёрить не хорошо!"

    show hl idle_1 with dspr

    extend " Но чего уж там, сам грешен..."
    hohol "Да, кое-какие запасы есть: инструменты, удочек много, снасти..."

    show hl idle_2 with dspr

    hohol "Слышьте, молодцы, а вы как сюда припёрли-то? До лагеря и за день не дойдёшь, если пешкодралом."
    sh "У нас машина есть, еле завели."
    "Дед покачал головой."
    hohol "А по дороге никаких других не встречали?"
    hohol "Просто наши, деревенские, свалили уже как... не помню, недавно. Но их-то мало осталось, кто держался ещё."
    me "Дедуль, а что за хрень у вас рядом с кладбищем?"
    "Василий снова покачал головой, сплюнул."

    show hl idle_1 with dspr

    hohol "Этойть местные. Те, что со мной остались, видать, навсегда уже. От радиации померли, отсырело всё, сжечь не могу."

    scene bg ext_church
    show gas_mask
    with dissolve

    "Пробивал холодный ветер и сырость, наплевавшая на резину химзащиты. Мы прошли мимо какой-то церквушки, старой, покосившейся не только от погоды, но и от времени."
    "Над ней летали, непонятно откуда взявшиеся, вороны."
    me "Василий, а как вы тут, без противогаза?"
    hohol "Да нету у меня его, так что как-то живём пока что. А топлива в машине много у вас, ребятки?"
    "Шурик настороженно повернул голову в сторону деда."
    sh "Ну две трети бака, а чего спрашиваете-то?"
    "Дед откашлялся, замедлив шаг."
    hohol "Да я ить... А, потом расскажу."
    "И снова зашёлся кашлем."

    stop ambience fadeout 5
    play music AR_tabula fadein 3
    scene bg ext_dom_hohla
    show gas_mask
    with dissolve

    "Прошли по вытоптанной тропинке в лес. Там, среди деревьев, и правда, стоял небольшой домик. Почти сразу послышался знакомый треск аномалии."
    "Несмотря на осень и мёртвую зону вокруг, трава была ещё зелёной, будто застывшей во времени."

    stop sound3 fadeout 1.5
    scene bg ext_dom_hohla with dissolve
    play sound gasmask_take_off

    "Я стянул маску противогаза."

    window hide
    show sh normal hazmat_suit with dissolve
    window auto
    play sound tresk_gaygera_norm loop fadein 2

    sh "Тут чисто."
    "Кивнул Шурик, быстро сверившись с приборами."

    stop sound fadeout 2
    stop sound2 fadeout 2

    hohol "Проходите, ребятки."

    scene bg int_unknown_house with dissolve
    play sound zvuk_anomalii_ciho loop fadein 2
    play sound2 rain_inside loop fadein 2


    "В домике было уютно, тепло. Василий с ходу поставил чайник на видавшую виды газовую горелку. Раздеваться полностью не стали, только перчатки сняли."

    window hide
    show hl smile_2 with dissolve
    window auto

    hohol "Садитесь, садитесь, пионэры."
    "Выглядел он очень хозяйственно."
    me "А жена Ваша где?"
    hohol "Старушка моя... спит. На втором этаже."
    "Дед разлил горячий чай по кружкам. Или что-то на чай похожее."
    hohol "На чайном грибе. Я ещё пару стружек осиновых подкинул, для вкуса."
    "Несмотря на то, что вкус отвара был на любителя, тёплое было как нельзя кстати."
    "Руки отчего-то слегка дрожали, а повисшую тишину разбавлял только треск аномалии за окном."

    window hide
    show sh serious hazmat_suit at fright with dissolve
    window auto

    sh "Василий, а вы не знаете, отчего вот эта хрень образовалась?"
    "Шурик пальцем показал на окно."

    show hl smile_1 with dspr

    hohol "Электронка-то? Как устроена, отчего образовалась — не знаю. Знаю только, что они не вечные и появляются после бури."
    "Кибернетики с интересом посмотрели на Василия, а он чуть прищурился, сел за стол."
    hohol "Этойть, когда рвануло, местные наши собрали совет. Машины тогда все вышли из строя, не работало нихрена."
    hohol "Механик наш, Ярослав, пытался что-то сделать, но не успел."
    me "Почему?"

    show hl idle_1 with dspr

    "Василий отпил из своей кружки, поморщился."
    hohol "Да ить, у него малой был, Ярослав недоглядел, так пацанёнок искупался в речке."
    hohol "Короче, сгорел за неделю — сначала орал, потом только мычал."

    show hl moody_1 with dspr

    hohol "Ярослав не выдержал, напился да и повесился, царствие небесное."
    "Помолчали.{w} Новый мир учил ничему не удивляться и меньше сожалеть."
    "Потом Василий снова заговорил."

    show hl idle_1 with dspr

    hohol "А вообще, слышал я по радио, смог запитать от хренотени той...{w} Вы, ребятки, знаете, что она заряжает батареи?"
    "Мы не сговариваясь кивнули."
    hohol "Ну так вот, там какой-то физик что-то говорил про особую оболочку Земли, которая после войны повредилась.{w} Неосфера какая-то. Врата ада, короче."
    "Шурик насупился."
    sh "Неужели вы верите в эти сказки?"
    hohol "В страшные — да... А вот в добрые — нет."
    "Василий разлил всем ещё понемногу."
    hohol "Это мир без завтрашнего дня. В нём нет места мечтам, планам, надеждам."
    hohol "Чувства уступают место инстинктам, главный из которых — выжить. Выжить любой ценой."
    "Дед поигрывал желваками. Как-то недобро, странно."

    window hide
    show el hs_normal hazmat_suit at fleft with dissolve
    window auto

    el "Ну, мы, наверное, пойдём."

    show hl idle_2 with dspr

    hohol "Давайте, ребятки."
    "Я быстро взвесив все «за» и «против», выпалил:"
    me "Василий, вы с нами не хотите в лагерь?"

    show hl smile_1 with dspr

    hohol "Не-е, пионэры. Я тут, это, со старушкой моей перекантуюсь."
    hohol "Вот ещё карта вам, каб не заплутали."
    hohol "А хотите, я вам топлива дам, мне всё равно бензин... ну ни к чему, хотите? Только как насобираете барахла, ко мне заскочите."

    show el hs_smile hazmat_suit with dspr

    el "Спасибо большое за карту."
    el "И за топливо тоже, заранее большое спасибо. Оно очень сильно нам пригодится."

    scene bg ext_dom_hohla with dissolve
    play ambience ambience_ext_road_night
    stop sound fadeout 2.0

    "Быстро распрощавшись, отправились по размытой тропинке в сторону Красного."

    stop music fadeout 5
    show blink
    $ renpy.pause(1.5,hard=True)
    play sound nadel_protivogaz
    play sound2 dychanie_w_protivogaze loop fadein 2
    scene bg ext_forest_sunset
    show unblink
    show gas_mask
    with dissolve
    pause(1.0)
    play sound3 light_rain loop fadein 2

    "По пути всё же нацепили противогазы — мало ли что дед говорит. Дождь потихоньку набирал силу."
    th "Вот же, блядь, вовремя пошли на вылазку."

    window hide
    show sh normal gas_mask_on at right behind gas_mask with dissolve
    window auto

    sh "Василий этот, он вроде дельный мужик. Только странный он тип, я бы не предлагал такому в лагерь, да и провизия..."
    me "Шурик, это первый выживший за всё это время. Время ж такое теперь, если ты не странный, то это уже подозрительно."
    me "Ни один нормальный человек морально такое без последствий не перенесёт."
    sh "Тоже верно. Так, Семён, времени у нас мало, надо много чего обследовать и собрать."
    "С ещё не до конца проветренной, после хренового сна в душной машине, головой, я соображал не очень, двигаясь подобно заправскому алкоголику в поисках опохмела."
    me "Шур, может быть, раз такое дело, разделимся?"
    sh "Не думаю, что это хорошая идея, вдруг кто выжил? А если кто провалится?"
    el "А мне кажется, что всё нормально. Сам посуди, город пуст, да и оружие у нас есть. Если же кто-то да провалится, отыщем, не велика проблема."
    sh "Да, но…"
    "Задумался было Шурик, однако сразу сообразив, что так быстрее, а времени мало, согласился."
    sh "Ай, ладно. Разделяемся."
    me "Тогда кто и куда пойдёт?"
    el "Я с Шурой берём на себя западную часть посёлка, а ты, Сём, возьмёшь восток."
    me "Хорошо, встречаемся где?"

    scene bg ext_church
    show gas_mask
    with dissolve

    "К этому времени мы подошли к большому, деревянному зданию, с крестом на крыше."

    show sh normal gas_mask_on at right behind gas_mask with dissolve

    sh "Пожалуй тут и встретимся."

    hide sh with dissolve
    window hide
    show el hs_normal gas_mask at left behind gas_mask with dissolve
    window auto

    "Шурик отошёл ненадолго, сказав что по делу, мы остались с Элом возле церквушки."

    window hide
    hide el with dissolve
    window auto
    play sound dog_whining_short
    pause(1)
    play sound predochranitel_ak
    pause(1)
    play sound2 zatvor_ak

    "Вдруг из-за забора послышался не то вой, не то писк. Я снял с предохранителя автомат, передёрнул затвор и только тогда разрешил себе развернуться."
    "За небольшим забором сидел облысевший, трёхногий пёс. Половины уха у него не было."
    el "Тьфу, гадость. Пристрели, а?"
    "Электроник отвернулся. А мне почему-то не хотелось тратить патроны, и жалко стало."
    "Я понимал, что псу так ведь лучше будет, раз — и всё, но в полуслепых глазах читалась какая-то надежда. Искренняя, настоящая."
    "Я опустил автомат."

    window hide
    show el hs_normal gas_mask at left behind gas_mask with dissolve
    window auto

    el "Ну, давай!"

    menu:
        "Застрелить":

            $ pristrelit_psa = True
            
            play sound odin_ak
            pause(0.2)
            play sound2 dog_whimpering

            "Прозвучал выстрел. Пёс упал замертво, лишь раз проскулив, перед концом."
            th "Сейчас мир таков, ничего не поделать, всё же лучше это, чем умереть от радиации."
            el "Вот и всё. Пошли."
            me "Да."

        "Оставить в живых":

            $ pristrelit_psa = False
            
            me "Жалко."
            "Я похлопал товарища по плечу, подталкивая вперёд."
            el "Сдурел?!"
            "Чуть не завопил Электроник сквозь резину."
            me "Пошли, говорю! Сам сдохнет."
            "Тоже повысил тон я."

    stop ambience fadeout 3
    hide el with dissolve

    "На том и порешили. К этому моменту вернулся Шурик и забрав с собой Эла, оставил меня один на один с городом и своими мыслями."

label karta_sela:

    $ karta_otkrytie = False

    if search_cnt != 2:

        "Куда пойти?"

        stop sound2 fadeout 2
        hide gas_mask with dissolve
        play sound gasmask_take_off
        call screen agr_scripted_map
    else:

        "Уже темнеет, надо идти в точку сбора."

        jump vstretil_ela

label sklad:

    stop sound3 fadeout 2.0
    scene black with dissolve
    pause(1.5)
    $ storage = True
    $ search_cnt += 1
    scene bg sklad
    show gas_mask
    with dissolve
    play sound2 dychanie_w_protivogaze loop fadein 2
    play sound3 rain_inside loop fadein 2

    "В самом здании было пыльно, пахло маслом и железом. Видимо после смерти механика, сюда никто и не заходил."
    th "Надеюсь что не зря зашёл сюда, может бензин какой, да инструменты найду."
    "Я принялся шерстить по полкам, стараясь найти всё, что только может понадобиться."
    th "Так, это нет, это тоже нет. А вот это может пригодиться, беру с собой."

    show blink
    $ renpy.pause(2,hard=True)
    scene bg sklad
    show gas_mask
    show unblink
    with dissolve

    "Прошло уже минут десять, но найти чего-то сильно нужного не получилось."
    "Думал уж было уходить, но подметил какой-то чёрный, потрёпанный ящик в углу."
    th "А это у нас что..."

    play sound zvuk_plastic_yachik

    "Открыв его, я понял, что нашёл очень нужную вещь. Чемодан с инструментами."

    if aptechka == True:
        th "Вот только проблема, взять всё сразу не получится, надо решить что важнее, медицинское или инженерное."
        
        menu:
            "Взять ящик":

                $ instrumenty = True
                $ aptechka = False

                th "Потом как-нибудь заберу медицинское, лучше бы взять для кибернетиков инструменты."

            "Оставить мед. инструменты":

                th "Нет, мед. инструменты поважнее будут. Мало ли чего произойдёт."
    else:

        $ instrumenty = True

        th "Заберу их с собой, а то меня Шурик убъёт."
    
    "Спаковав всё, я отправился дальше."

    jump karta_sela


label bolnica_lekarstwa:

    scene black with dissolve
    pause(1.5)
    $ hospital = True
    $ search_cnt += 1
    scene bg bolnica_ext
    show gas_mask
    with dissolve
    play sound2 dychanie_w_protivogaze loop fadein 2

    th "Вот и больница, надеюсь получится найти хоть что-то нужное." 

    play sound sfx_door_squeak_light
    stop sound3 fadeout 2.0

    "Дверь была не заперта, заскрипела и отворилась."

    scene bg koridor_bolnica
    show gas_mask
    with dissolve

    "Внутри всё ещё пахло спиртом и \“запахом медицины\”. Со стен послетала штукатурка." 
    "Продвигался я аккуратно, с настороженностью, опасаясь каждого шороха. Неизвестно что могло находится в этой больнице." 
    "Но через некоторое время расслабился, привыкнув к атмосфере." 
    th "Не уверен, что смогу найти хоть что-то. Жители, скорее всего, разобрали всё, что только можно." 
    th "Но оставлять больницу непроверенной нельзя."
    "В первом кабинете ничего не было, во втором тоже, и в третьем…" 
    th "И правда, похоже разобрали всё, гадство." 

    window hide
    pause(1.5)
    scene bg palata
    show gas_mask
    with dissolve
    window show
    play sound rain_inside loop fadein 1.5
    play sound3 sfx_door_squeak_light

    "Ещё одна пустая комната, уж было подумал я."
    "Зайдя внутрь, я заметил практически пустые полки, где раньше стояли лекарства и вещи." 
    "Просмотря все полки, я смог найти антибиотики, жаропонижающее, болеутоляющее и ещё что-то по мелочи. Чуть найдя место для них в сумке, сложил их и пошёл дальше."
    
    play sound3 sfx_door_squeak_light
    stop sound fadeout 1.5
    scene bg koridor_bolnica
    show gas_mask
    with dissolve
    window hide
    pause(1.5)
    window show

    "Ещё несколько пустых кабинетов, где ничего не осталось." 
    
    me "Класс хирургии."

    play sound sfx_door_squeak_light
    play sound3 rain_inside loop fadein 1.5
    scene bg klass_hirurgii
    show gas_mask
    with dissolve

    "Отворив скрипучую дверь, я увидел ту же картину, пустой кабинет, со стульями, но в середине стояло кресло и какой-то небольшой ящик." 

    play sound zvuk_metal_yachik

    "Подойдя поближе и открыв его, я немного удивился. Лежали там, чистые инструменты, скальпели, ножницы – всё что надо для проведения операции." 
    th "Отлично, остаётся только найти место для этого, поскольку рюкзак уже забит."

    if instrumenty == True:

        "Покопавшись в рюкзаке, я не нашёл ничего, что можно было бы выкинуть. Оставался выбор между чемоданом и инструментами для операции."

        window hide

        menu:
            "Оставить чемодан":

                th "Нет, это всё же важнее, кибернетикам понадобится."

            "Взять мед. инструменты":

                $ aptechka = True
                $ instrumenty = False

                th "Не думаю, что этот чемодан важнее, чем инструменты для операций, они могут понадобится в критической ситуации."
    else:

        $ aptechka = True

        th "Ладно, понесу в руках."
    
    "Я вышел из кабинета."

    stop sound3 fadeout 1.5
    play sound sfx_door_squeak_light
    scene bg koridor_bolnica
    show gas_mask
    with dissolve

    th "Не уверен, что смогу найти ещё что-либо здесь, время надо экономить."
    "Проверив ещё несколько кабинетов по пути, которые оказались пустыми, я пошёл к выходу." 
    th "Слава богу, что ничего не произошло пока был тут."

    play sound sfx_door_squeak_light
    play sound3 light_rain fadein 2
    scene bg bolnica_ext
    show gas_mask
    with dissolve
    window hide

    jump karta_sela

label w_dome_kolco:

    stop sound3 fadeout 2
    scene black with dissolve
    pause(1.5)
    $ ring = True
    $ search_cnt += 1
    $ kolco = True

    scene bg black with dissolve
    pause(1)
    scene bg int_red_house_abandoned
    show gas_mask
    with dissolve
    play ambience rain_inside fadein 3
    play sound2 dychanie_w_protivogaze loop fadein 2

    th "Даже дверь не закрыли. Небось думали вернуться. Наивно."

    window hide
    play music AR_exodus fadein 3
    $ set_mode_nvl()
    window auto
    $ persistent.sprite_time = "night"

    "Внутри было пусто, холодно и влажно. Я раскрыл вещмешок. Осмотрел печку, стол, кухонную мебель — почти всё пустовало. Только потёки, пыль и тишина. Иногда лишь поскрипывали половицы под резиновыми чулками костюма."
    "В комоде нашёл пару рубашек, явно мужских, и какой-то ключ."
    "На нижних полках лежало какое-то оборудование: вроде исправный паяльник, успевшие проржаветь гвозди и двадцать рублей, мелкими купюрами, тоже отсыревшие и ни на что уже не годные."
    "В холодильник, несмотря на маску противогаза, решил не лезть. Вспомнил, что Шурик говорил про время и решил особо не церемонясь, кинуть всё как попало, да двигаться назад."
    "В самом конце только заметил, что в уголке что-то странно, металлически поблёскивало. Там был какой-то ларец, похоже, самодельный. Ключ в комоде явно был от него."
    "Перед тем, как открывать, сменил фильтр в противогазе. Внутри была рукопись, на которой значилось «А. Солженицын. Раковый корпус», несколько фотографий какой-то девушки и золотое колечко."
    "Не раздумывая пихнул всё, кроме кольца, в вещмешок. Золотую безделушку я пристроил в нашитый, нагрудный карман, плотно закрыв его."
    "Я уже представлял, как буду надевать его на пальчик Алисе, и был очень доволен собой."

    window hide
    stop ambience fadeout 3
    scene bg black with dissolve
    $ set_mode_adv()
    pause(1)
    scene bg ext_church
    show gas_mask
    with dissolve
    play sound3 light_rain loop fadein 2
    window auto

    jump karta_sela

label vstretil_ela:

    play sound3 light_rain loop fadein 2
    scene black with dissolve
    pause(1.5)
    scene bg ext_church
    show gas_mask
    with dissolve

    "На обратном пути встретил Эла. Он долго возился с радиодеталями старого телевизора, но в итоге решил стащить его целиком. К тому же он нашёл технический спирт, головку от молотка и топорик."

    show el hs_normal gas_mask at left behind gas_mask with dissolve

    me "Где Шурик?"
    el "Сказал идти к церкви."
    me "Хорошо. Как мы это всё упрём?"
    el "Как-как... по вещмешкам, а телевизор на руках."
    me "Ладно, давай. И на кой хер вам железка эта..."
    "Я принял на себя часть груза. На обратном пути в сторону двора, где видели собаку, предусмотрительно не смотрели — да и сложно было смотреть куда-то, кроме размытой каши под ногами, а то не дай Бог навернёшься."
    el "Вот ещё домик есть, надо бы проверить."
    me "Давай в следующий раз, на лучевое похмелье мы уже точно заработали. Ты фильтр менял?"
    el "Да, не забыл. А насчёт облучения... может, костюмы и выдержат, не знаю."

    window hide
    hide el with dissolve
    window auto

    "У церкви, минут пять, ждали Шурика. Его вещмешок был забит под завязку гвоздями, лесками, рыболовными крючками и даже пряжей."

    window hide
    show sh normal gas_mask_on at right behind gas_mask with dissolve
    window auto

    sh "Вы нахера эту байду притащили?"
    "Усмехнулся кибернетик, глядя на телевизор."

    window hide
    show el hs_normal gas_mask at left behind gas_mask with dissolve
    window auto
    
    el "Там провода, там медь, цветмет!"
    sh "Ладно, раз притащили, давайте к Василию за топливом. Кстати, именно эти костюмы сжечь придётся, скинете у ворот лагеря."
    me "Добро."

    scene bg ext_forest_sunset
    show gas_mask
    with dissolve

    "Настроение потихоньку поднималось. Несмотря на грязь, слякоть и дождь, мы совсем скоро могли оказаться дома."
    th "Алиска, небось, соскучилась."
    "Я взглянул на серое небо сквозь запотевшие линзы противогаза и только сейчас заметил некую странность."
    "Возможно, я не заметил этого раньше из-за запотевания линз и слабого, но потихоньку набирающего силу кислородного голодания. С небес шёл совсем не дождь."
    "Медленно, зло кружился над нашими головами мокрый, тяжёлый пепел."

    scene bg ext_dom_hohla
    show gas_mask
    with dissolve

    me "Василий Лаврентьевич!"
    "Завопил я в противогаз ещё на подходе. Плечо тянул тяжёлый автомат, перед глазами бегали точки."

    stop music fadeout 2
    play sound drobovik
    play sound2 televizor_razbili

    "Дверь домика отворилась, я прикрыл глаза, но вдруг из телевизора, в наших с Электроником руках, разлетелось стекло во все стороны."

    play music music_list["doomed_to_be_defeated"] fadein 3

    sh "ЛОЖИСЬ!"

    stop ambience fadeout 3
    show bg ext_dom_hohla:
        ease 0.5 zoom 1.5 ypos 1.0
    play sound zatvor_ak

    "Я упал на мягкую, холодную грязь, прильнул к ней, всё ещё не понимая, что происходит. Мы с Электроником закатились не то в овраг какой-то, не то просто в яму."
    hohol "Пионэры, бля, юные... {b}ХЕР ВАМ{/b}, а не моё топливо..."
    hohol "Где? Где вы, суки, машину заныкали? Люба, не высовывайся... Скоро лечить вас повезём... В Киеве врачи есть, повезём!"

    play sound drobovik

    "Раздался ещё выстрел, что-то неприятно укололо плечо — не больно, будто порезался."
    me "Сука!" with vpunch
    $ persistent.sprite_time = "night"

    menu:
        "Встать и стрелять":

            jump vylez_srazu

        "Подождать":

            jump podozdal

label podozdal:

    th "Надо подождать, может получится удачный момент словить."

    window hide
    play sound drobovik
    $ renpy.pause(2,hard=True)
    play sound drobovik
    pause(1)
    play sound drobovik
    window show

    "Было слышно ещё пару выстрелов, после чего они прекратились, видимо дед решил не тратить просто так патроны."

    scene cg ch_7_shootout
    show gas_mask
    with dissolve

    "Я выглянул, не увидев никого в дверном проёме, решил встать, направив дуло оружия в ту сторону."

    play sound drobovik

    "Прозвучал выстрел."

    play sound ochered_ak

    "Я стал отстреливаться в ответ. Из-за адреналина стояла пелена в глазах, сердце билось как бешеное."

    play sound drobovik
    stop music fadeout 3

    "С той стороны прозвучал ещё один выстрел и всё резко стихло, а я так и стоял, с трясущимися руками и автоматом, направленным в сторону двери."
    "Стоял я так секунд тридцать, после чего ко мне подбежали кибернетики."
    sh "Ты как? Ты чего вылез так внезапно?!"
    el "Не подстрелили?"
    me "Нет."
    "Я до сих пор не опускал автомат, слушая и отвечая на автомате."
    sh "Семён, а почему у тебя кровь на костюме?"
    me "В смысле?..."

    window hide
    stop sound3 fadeout 2
    show blink
    play sound2 sfx_body_bump
    $ renpy.pause(2,hard=True)
    play sound3 normal_heartbeat loop fadein 2
    scene bg ext_forest_sunset
    show gas_mask
    show prologue_dream
    with dissolve
    play sound gas_mask_hard loop fadein 2
    window show

    sh "Тащим его к машине, быстрее мать твою!"

    window hide
    stop sound fadeout 2
    show blink
    $ renpy.pause(2,hard=True)
    scene bg ext_bus_stop_abandoned
    show gas_mask
    show prologue_dream
    with dissolve
    play sound gas_mask_hard loop fadein 2
    window show

    sh "Затаскивай!"
    el "А рюкзак?!"
    sh "Блять, да хоть с ним, быстрее!"

    window hide
    stop sound fadeout 2
    show blink
    $ renpy.pause(2,hard=True)
    play sound_loop2 sfx_intro_bus_engine_loop fadein 2
    scene bg potolok_avto
    show prologue_dream
    with dissolve
    window show

    sh "Гони быстрее!"
    el "Опасно же, разобьёмся."
    sh "Гони говорю! Иначе труп довезём!"

    window hide
    show blink
    stop sound_loop2 fadeout 1.5
    $ renpy.pause(2,hard=True)
    play sound_loop2 sfx_intro_bus_engine_loop fadein 2
    scene bg potolok_avto
    show prologue_dream
    with dissolve
    window show

    sh "Оля, скажи Виоле быстро подготовить медпункт, у нас тут огнестрельное ранение. В Семёна попали."

    window hide
    show blink
    stop sound_loop2 fadeout 1.5
    $ renpy.pause(2,hard=True)
    scene bg potolok_avto
    show prologue_dream
    with dissolve
    window show

    sh "Теперь надо перенести его до медпункта, там Виола ждёт уже. Давай Семён, ты выживешь."

    window hide
    show blink
    stop sound_loop2 fadeout 1.5
    $ renpy.pause(2,hard=True)
    scene bg ext_camp_entrance_night
    show prologue_dream
    with dissolve
    window show

    if aptechka == True:
        jump podozdal_vyzil

    else:
        jump podozdal_umer

label podozdal_umer:

    window hide
    show blink
    stop sound_loop2 fadeout 1.5
    $ renpy.pause(2,hard=True)
    scene bg medpunkt_noch
    show prologue_dream
    with dissolve
    window show

    cs "У него вена задета, я не смогу закрыть кровотечение, нет зажима и остальных приборов."
    cs "Он не выживет, я могу только отсрочить его смерть, максимум на…"

    window hide
    show blink
    stop sound3 fadeout 2.0
    $ renpy.pause(2.0, hard=True)
    scene bg medpunkt_noch
    show unblink
    with dissolve
    window show
    play music AR_exodus fadein 4
    $ renpy.pause(1.5,hard=True)
    show blinking

    th "Где я?... Чего ж так хреново…"
    "Появилась резкая боль в боку и я вспомнил."
    th "Пуля попала. Твою мать."
    "Вдруг в кабинет порвалась Алиса."

    play sound2 sfx_open_door_strong
    scene cg alisa_obnimaet_semen with dissolve

    dv "Нет… Сёма, не уходи от меня…"
    "Она плакала и вся дрожала."
    dv "Виола сказала, что у нас есть… минут десять. Не получится закрыть кровотечение…"
    "Я не был удивлён, я был готов к смерти ещё в тот момент когда прилетела бомба. Единственное чего я боялся, это потерять Алису."
    me "Прости меня, рыжая моя."
    dv "Глупый… Глупый, глупый, глупый, ну зачем ты полез туда? Зачем я тебя отпустила?!"
    "Алиса уже практически кричала."
    me "Кто же знал что так получится. Ты никак не виновата в этом…"
    dv "Знаю, я знаю что не виновата, но почему всё так вышло? Ну почему?!"
    dv "Я не хочу тебя терять, не уходи от меня, не оставляй меня одну…"
    me "Не от меня это зависит, уже ничего не поделать."
    "Алиса замолчала, видимо поняла, что ничего не изменить и просто улеглась на мою грудь, смотря мне прямо в глаза."
    "По её глазам было видно, что она боится. Боится того, что я уйду, что останется одна."
    th "Что же она делать будет, без меня?"

    scene cg alisa_pochti_lezit_na_semen with dissolve

    dv "Помнишь, как мы впервые поцеловались?"
    "Достаточно неожиданно сказала Алиса, с каким-то спокойствием, как бы вспоминая старые времена, до всего этого."
    me "Конечно помню, не думал что ты запомнила."
    "С уставшей ухмылкой сказал я."
    dv "Дурак. Как я могу не помнить о таком, это был и остаётся одним из самых важных моментов для меня..."
    me "Да ты врёшь, вижу же."
    dv "Не вру я тебе!... Я правда очень рада что встретила тебя…"
    me "Я тоже, мой лисёнок."
    "Сил уже было очень мало, я чувствовал, как хочу закрыть глаза и просто поспать."
    me "Только не смей тут вытворять всякого, после того как меня не будет. В особенности памятники подрывать."

    play sound slow_heart_1 loop fadein 1.5

    me "Мы были готовы к тому что умрём, и я рад, что мои последние мгновения провожу рядом с тобой."

    stop sound fadeout 1.5
    scene cg alisa_lezit_na_semen with dissolve
    play sound2 slow_heart_2 fadein 1.5

    me "Я тебя люблю."
    dv "И я тебя…"

    stop music fadeout 4
    show blink
    play sound slow_heart_3
    stop sound2 fadeout 2.0

    "Последнее что я увидел, это было лицо Алисы, заплаканное, но красивое, она лежала у меня на груди, мысленно прощаясь."

    play sound slow_heart_3

    th "Прощай, Алиса..."

    stop sound fadeout 2
    window hide
    $ renpy.pause(2, hard=True)

    return

label podozdal_vyzil:

    cs "У него вена задета, я не смогу закрыть кровотечение, нет зажима и остальных приборов."
    sh "Вот, Семён нашел в больнице это, надеюсь с этим получится."
    cs "Да, хорошо, быстро, нужна будет ещё ваша помощь."

    window hide
    show blink
    $ renpy.pause(2,hard=True)
    scene cg alisa_lezit_na_semen with dissolve
    show unblink
    with dissolve
    stop sound3 fadeout 2.0
    window show
    play music AR_exodus fadein 4
    $ renpy.pause(1.5,hard=True)
    show blinking

    "Проснулся я с болью в боку и в голове. Сил подняться с кровати не было."
    me "Твою мать, как же хреново..."
    "Найдя силы и приподняв голову, я увидел Алису, спящую у меня на груди."
    th "Точно. Дед. Пуля. Вот почему так хреново."
    me "Алиса, просыпайся."

    scene cg alisa_pochti_lezit_na_semen with dissolve

    "Алиса вроде как услышала меня, дёрнулась, стала открывать глаза."

    scene cg alisa_obnimaet_semen with dissolve

    "Увидев меня и то, что я проснулся, резко кинулась обнимать меня."
    dv "Дурак! Ты бы знал как сильно я волновалась, я думала что ты больше не проснёшься!"
    "Алиса плакала, но не от горя, а от радости. Радости, что всё обошлось."
    me "Не дави ты так, больно же..."
    dv "Ой, прости, я специально."

    scene bg medpunkt_noch
    show dv smile pioneer
    with dissolve

    "Уже с улыбкой сказала Алиса."
    me "Узнаю свою рыжую."

    show dv shy pioneer with dspr
    pause(0.5)
    show dv smile pioneer with dspr

    "Алиса чутка покраснела, но потом, протерев слёзы, вернула свою ехидную улыбку."
    "На это время, в её глазах, появились те самые, давно забытые и потеряные в этом мире, искорки."
    "Я был рад видеть её такой радостной. Хотя боль давала о себе знать."
    me "А что произошло то? Нихера не помню, отрывки одни."
    dv "Тебя тот дед, вроде, подстрелил, кибернетики рассказывали. Так они потащили тебя в машину, да повезли как можно быстрее в лагерь."
    dv "По рации говорят, мол, пулевое ранение тут, Семёна подстрелили. Ты бы знал как сильно я тогда испугалась, как сумашедшая понеслась к выходу."
    dv "Видим, что кибернетики выносят тебя из машины, а там крови столько, я чуть было в обморок не свалилась."
    dv "Понесли тебя к Виоле, она уже подготовила медпункт, а потом закрыли дверь, чтобы никто не мешал."
    dv "Я так и просидела на крыльце, ожидая пока они выйдут. А длилось это не один час."
    dv "Оказываеться часть пули задела вену, ты понимаешь, что мог умереть от потери крови?!"
    me "Понимаю я, но всё же обошлось?"
    dv "Это тебе ещё крупно повезло, без того набора с инструментами, ты бы не выжил, нечем больше зашивать было."
    th "И правда, повезло так повезло."
    me "Да я просто везунчик, вон, с тобой ещё повезло."

    show dv shy pioneer with dspr

    "Алиса смутилась, да поцеловала в лоб."

    show dv smile pioneer with dspr

    dv "Выздоравливай и не смей так больше делать, понял меня? А то два раза уже чуть не умер, ещё раз повториться и я сама прибью тебя."
    me "Понял я, понял. Не повториться, обещаю."
    dv "Вот и хорошо, теперь тебе ближайшие два месяца нельзя напрягаться, а первый и ходить куда-либо нежелательно. Буду следить за тобой."
    me "Но..."
    dv "Отказы не принимаются. За тобой надо ухаживать, даже вставать опасно, в особенности в первое время."
    "Я как будто вернулся обратно, в лагерь, в тёплые, светлые времена. Когда Алиса постоянно улыбалась, не было никаких забот и мы просто веселились и наслаждались временем, проведённым друг с другом."
    me "А как твоя болезнь?"
    dv "Мне уже лучше, но Виола сказала пока что не ходить особо по улице и быть аккуратнее."
    me "Хорошо, я ещё нашёл в той деревне лекарства, надо будет съездить и забрать."
    dv "Это обязательно, братья по переменному току, как только ты упал, сразу же кинули все вещи там, возле домика того деда."
    me "Точно, а что с дедом?"
    dv "Мёртв, ты в него попал, он вот последним патроном по тебе и выстрелил, по крайней мере так Шурик сказал."
    "Появилось неприятное чувство, доселе незнакомое."
    th "Я убил человека, впервые."
    "Ощущения после убийства человека, меня начало чутка трясти и появилось сильное волнение."

    scene cg alisa_pochti_lezit_na_semen with dissolve

    "Алиса, видимо, заметив это, легла рядом."
    dv "Всё хорошо. Он первый начал стрелять, ты ничего плохого не сделал."
    "На этом моменте я заплакал, не сильно, не навзрыд, но пустил слезу, надо как-то вымещать свои эмоции."
    "Так и лежали с Алисой ещё некоторое время, пока я не успокоился и меня не перестало трясти."

    scene bg medpunkt_noch
    show dv smile pioneer
    with dissolve

    dv "Так, Сёма, мне идти надо, буду каждый день следить за тобой, но это только первое время, работа не ждёт."
    me "Говоришь прямо как настоящий пионер."
    dv "Дурачок."
    me "Всё, иди работать."
    "Чмокнув меня, Алиса вышла из медпункта."

    hide dv with dissolve

    "Думал я уже поспать, как кто-то зашёл. Это была Виола."

    show cs normal with dissolve

    cs "Ну что, пионер, как себя чувствуешь?"
    me "Неплохо для того, в кого стреляли. Только бок и голова болят, терпимо."
    cs "Что болят, это нормально, как ты ещё хотел, пулевое всё же."
    me "Хотелось бы без боли."
    cs "Я и так вколола обезболевающее, терпи, мужик же. Знаешь же, как сильно тебе повезло?"
    me "Да, Алиса уже рассказала."
    cs "Ну вот."

    hide cs with dissolve

    "Виола села за столик и стала что-то писать, то ли в дневник, то ли в тетрадь."
    me "Послушай, Виола, а ты была в последней смене старого \”Совёнка\”?"
    "Она отложила свою ручку, повернулась ко мне."
    cs "Ну была, а ты откуда узнал?"
    me "Да вот, когда с Шуриком в старый лагерь ходили, нашёл ваше фото в доске почёта."
    cs "Вот как, значит. Да, была, хочешь что-то узнать?"
    me "Да, вы знаете кто такой Кир Белый?"

    show cs fear med_gown with dissolve

    "Я впервые увидел медсестру удивлённой, такого даже во время взрыва не было."
    cs "А это ты откуда узнал?"

    show cs normal with dspr

    me "Ну, узнал, вот так вот получилось. Так всё же вы что-то да знаете?"
    cs "Да, понятное дело, ведь из-за него лагерь и прикрыли."
    th "Чего?"
    cs "Он тогда двух девочек убил, пионерок из его отряда, где он вожатым был, одну нашли в озере, другую отыскать не смогли."
    me "А вот мы нашли, она лежала мёртвая, в катакомбах, с дощечкой, на которой была выцарапана полынь."
    cs "Боже, бедная девочка..."
    "Это были не наигранные чувства."
    th "Не знал, что Виола и так реагировать умеет."
    me "А что потом было?"
    cs "Потом его в город увезли, больше ничего не известно. Так ты скажи, откуда узнал?"
    me "Да у меня галлюцинация была в катакомбах, там про этого Кира Белого, вот и спрашиваю."
    "Она посмотрела на меня с опаской и подозрением."
    cs "И часто у тебя галлюцинации?"
    me "Да вот только тогда и были."
    "Медсестра выдохнула, опаска пропала из глаз."
    cs "Тогда хорошо, интересно конечно, как это могло быть про Кира Белого. Так, а теперь тебе нужен отдых, я буду находиться здесь и смотреть чтобы ничего не произошло."
    me "Хорошо, спасибо."
    cs "Не за что благодарить, пионер, работа такая."

    show blink

    "Я улыбнулся напоследок и закрыл глаза, в попытке уснуть."
    th "Сколько же всего произошло, надо как можно быстрее восстановиться, а то зимой совсем туго будет."
    "Сил особо не оставалось, поэтому я заснул."

    window hide
    stop music fadeout 2
    $ renpy.pause(1,hard=True)
    $ set_mode_nvl()

    if kolco == True:

        "Оставшееся время, до зимы, я выздоравливал. Изначально, а точнее первые недели две, Алиса постоянно мне помогала и следила за мной, находясь рядом. Когда мне стало лучше и я уже мог сам вставать и ходить, то она занялась работой, которая была нужна для лагеря. Меня успели навестить все, от Шурика до Жени, все были рады видеть что я иду на поправку. Кибернетики съездили в то село ещё раз, забрали всё что нашли. Во всю шла подготовка к зиме."
    else:

        $ kolco = True

        "Оставшееся время, до зимы, я выздоравливал. Изначально, а точнее первые недели две, Алиса постоянно мне помогала и следила за мной, находясь рядом. Когда мне стало лучше и я уже мог сам вставать и ходить, то она занялась работой, которая была нужна для лагеря. Меня успели навестить все, от Шурика до Жени, все были рады видеть что я иду на поправку. Кибернетики съездили в то село ещё раз, забрали всё что нашли. Шурик дал мне кольцо, которое нашёл, чтобы я сделал предложение Алисе. Во всю шла подготовка к зиме."

    window hide
    $ renpy.pause(2,hard=True)

    jump agr_day_8

label vylez_srazu:

    scene cg ch_7_shootout
    show gas_mask
    with dissolve
    play sound ochered_ak

    "Я снял предохранитель автомата, выпрямился, дал короткую очередь в дверной проём, потом ещё одну."

    scene anim ch_7_shootout_blur # Из-за вашего АНИМЕ код не работает!
    show gas_mask

    "В глазах побелело, уши заложило, я упал обратно в яму."
    me "Серёга, живой?"
    el "Живо-ой!"
    "Протянул он, вытаскивая из перчатки небольшой осколок экрана телевизора."
    me "Шура?!"
    sh "Живой. Не вставайте!"

    stop music fadeout 3
    play ambience heavy_rain fadein 3

    "Пролежали ещё минут десять. Тело било крупной дрожью."
    th "Я убил человека... {w}{b}Я УБИЛ ЧЕЛОВЕКА!{/b}"

    with vpunch

    th "Надоело!"

    scene cg ch_7_shootout_red
    show gas_mask
    with dspr

    "Я встал, выставил автомат вперёд себя и двинулся вперёд. Глаза налились красным, противогаз мешал."
    sh "Стой, дурак!"

    stop sound3 fadeout 2.0
    play sound_loop rain_inside fadein 3
    stop ambience fadeout 3
    scene bg int_unknown_house
    show gas_mask
    with dissolve

    "В домике, прижавшись спиной к столу, лежал Василий."

    play music AR_violent fadein 3

    extend " Охотничье ружьё вылетело из руки, на пузе, под жилеткой, были видны маленькие дырочки и большие, кровавые круги."
    me "Про... стите, Василий, я... не хотел. Ну зачем вы по нам стреляли? Я же предлагал вам в лагерь..."
    "Мёртвые никому и ничего не расскажут. Мёртвым плевать на живых и на других мёртвых тоже."
    "На столе лежала карта, окровавленая, на ней карандашом нарисован маршрут. Подошедший сзади Шурик взял автомат из моих рук, аккуратно, словно у кота мышку отбирал."
    
    scene bg int_unknown_house with dissolve
    play sound gasmask_take_off
    stop sound2 fadeout 2
    
    "Шурик зашёл в соседнюю комнату, но тут же вышел. Стянули противогазы."

    window hide
    show sh normal hazmat_suit with dissolve
    window auto

    sh "Ты... это, не ходи."
    "Он ткнул пальцем в комнату."
    "Я кивнул."
    sh "Нормально?"
    me "Да. Домой хочу. Зачем он так?!"

    show sh serious hazmat_suit with dissolve

    sh "Сука он потому что...{w=1} Похоронить бы по-хорошему надо."
    me "К чёрту."

    stop sound_loop fadeout 3
    scene bg black with dissolve2
    pause(0.5)
    play sound_loop2 sfx_intro_bus_engine_loop fadein 3
    scene cg ch_7_volga
    with dissolve2

    "Я не запомнил дорогу до машины. Помню только, что стащили с себя грязные костюмы и как были, в противогазах, залезли в салон. В багажник кинули вещмешки и баклажку бензина."
    "Бил озноб, из глаз текли непрошенные слёзы."
    el "Сёмыч, ты чего? Такая жизнь теперь, ты не виноват."
    sh "Да, Сёмыч. Давай, успокаивайся, завтра отлежишься — мы твои дела на себя возьмём!"
    "Отвечать не хотелось. Я тупо пялился в проплывающий за окном пейзаж."
    sh "Опа, ребят, смотрите-ка."
    "На руках у Саши лежала карта Василия."
    sh "Так... Смотрите, этот дед, видимо, думал, что поезда ещё ходят. Вот станция."
    el "Думаешь, там поезда ещё есть?"
    sh "Надо проверить. А ты, Семён, лучше поспи."

    window hide
    stop music fadeout 3
    stop sound_loop2 fadeout 3
    show blink
    pause(3)
    play music music_list["sparkles"] fadein 3
    scene prolog_1
    show prologue_dream
    with dissolve2
    $ prolog_time()
    window auto

    "Мне снилось что-то доброе и невразумительное. Мы с Алисой плыли по бесконечной речке на лодке. Девушка молча смотрела на воду."
    me "Скоро мы будем дома."

    window hide
    show dv sad pioneer behind prologue_dream with dissolve
    window auto

    "Она посмотрела на меня отсутствующим взглядом."
    dv "Меня с вами уже не будет. Скоро {b}вы{/b} будете дома. Я останусь здесь."
    me "Почему?"
    "Она покачала головой."
    dv "Тебе пора просыпаться, Семён. Ну, давай, вставай!"
    "Лодка опрокинулась."

    stop music fadeout 3
    scene bg black with dissolve
    pause(1)
    $ sunset_time()
    play sound_loop sfx_intro_bus_engine_loop fadein 3
    scene cg ch_7_volga
    with dissolve

    "Что-то орал Электроник."
    el "...еду я!"
    sh "Быстрее, бля. Да сигналь ты, сигналь."
    "Сквозь совсем уже запотевший противогаз не было ничего видно."
    me "Чего там.."

    play music music_list["drown"] fadein 3

    "Фары высветили женский силуэт на дороге, и ещё пять звериных. Метров тридцать. Машина отвратительно сигналила и медленно ехала от чего-то."

    scene bg ext_camp_entrance_fog_sunset
    show sl scared coat far
    with dissolve

    "Я стащил маску и, как был, в свитере выскочил на улицу."
    me "Славя, беги! Беги, дура!"

    play sound predochranitel_ak

    "В руке знакомо щёлкнул предохранитель."
    th "Патронов десять точно есть..."

    play sound odin_ak

    "Выстрел в воздух. Твари не то испугались, не то просто переключились на более обнаглевшую добычу."

    stop sound_loop fadeout 3

    "Машина сзади заглохла совсем, из неё истошно орали кибернетики."
    th "Если их отвлечь на себя, смогу выиграть время."
    "Это были оголодавшие, сильно облучённые, одичавшие псы. Из Красного, как тот. Или, может, ещё откуда-то. Неважно."
    me "Ну, давай, сука!"
    "По ушам бил пульс. В глазах плыли красные круги."
    th "Страшно..."

    menu:
        "Бежать к машине":

            window hide
            hide sl with dissolve
            window auto
            play sound ochered_ak_3

            "Страх взял своё. Автомат пару раз боднул в плечо отдачей, и я рванул назад, к спасению."
            "Псы, заметив, что противник бежит, с жутким лаем рванули за мной."

            play sound sfx_bodyfall_1

            extend " До машины оставалось метров семь, но какая-то трещина в асфальте не дала добраться."

            play sound sobaki_rychat

            "Обалдело орали из машины кибернетики. Что-то противно впилось в ногу, спину, шею."
            th "Как же больно, твою мать!"
            me "Отцепитесь твари!"
            "Сил отбиваться больше не оставалось."
            th "Больше не могу..."

            stop sound fadeout 2.0
            window hide
            show blink
            pause(3)

            return

        "Пристрелю тварей":

            play music music_list["scarytale"] fadein 3

            window hide
            hide sl with dissolve
            window auto
            play sound2 predochranitel_ak
            pause(0.5)
            play sound odin_ak

            "Переводчик на одиночные, мушку с целиком."

            play sound2 sloman_cherep

            extend " Череп ближайшей собаки треснул, полетели кровавые ошмётки на асфальт."

            play sound skulit_i_otstupaet

            "Стоящая рядом тварь заскулила, потихоньку начала пятиться назад."

            play sound odin_ak
            pause(0.5)
            play sound odin_ak

            extend " Ещё два выстрела, не прицельно."

            play sound2 sobaki_ogryzautsa

            "Вся стая, огрызаясь и противно тявкая, всё быстрее отступала назад."

            play sound odin_ak
            stop sound2 fadeout 1.5

            "Ещё выстрел. Ещё. Остатки стаи бежали куда-то в лес."

    stop music fadeout 3
    play ambience ambience_ext_road_evening fadein 3

    me "Славя?!"

    window hide
    show sl scared coat far with dissolve
    window auto

    "Блондинка показалась из-за ворот с несколько испуганным видом."
    sl "Ты живой?"
    me "Я-то да, ты как?"
    "Девушка только кивнула."

    # "*переход, около библиотеки, осень(скину), Алиса в пальто*"
    stop ambience fadeout 3
    scene bg black with dissolve2
    pause(1)
    play ambience ambience_camp_center_evening fadein 3
    scene bg ext_libriary_autumn_sunset
    show dv angry coat
    with dissolve2
    $ karta_otkrytie = True

    dv "Ну нельзя же так было!"
    "Девушка закашлялась."
    me "Поверь, оно того стоило. Солнце, мы уедем отсюда. В чистую зону, обещаю, уедем!"
    me "Уедем..."

    show dv normal coat with dspr

    dv "Куда?"

    play music saya_sunset
    stop ambience fadeout 3

    me "За горизонт."
    "Внутри загорелось что-то новое. Настоящая надежда, что мы всё-таки найдём место, где не будет голодных тварей, радиации, будет еда. Где будет видно солнце."

    # "*переход, столовая, вечер*"
    scene bg black with dissolve
    pause(1)
    scene bg int_dining_hall_sunset with dissolve

    me "Какие у нас варианты?"

    window hide
    show sh serious pioneer at right with dissolve
    window auto

    sh "До февраля отсюда убраться. Неохота мне, чтобы мой ребёнок жил во всей этой жести."

    window hide
    show mt normal pioneer at fleft with dissolve
    window auto

    mt "Как именно?"
    sh "Поездом. Ты, Оль, наверное, лучше остальных знаешь, что за рекой есть остановка."
    mt "Да, нас инструктировали, чтобы дети не сбегали... как-то я не подумала."

    window hide
    show mi smile pioneer at fright with dissolve
    window auto

    mi "А что там рядом, зачем остановка?"

    show sh normal pioneer with dspr

    sh "А это, милая, не самая приятная часть всей этой затеи. Как я понял, оттуда, по прямой, была дорога до ГЭС и посёлка, где жили её работники."

    window hide
    show cs normal ag_civil at cleft with dissolve
    window auto

    cs "Как там с радиацией, предположения есть?"
    "Медсестра серьёзно смотрела на кибернетика. Шурик едва заметно улыбнулся."
    sh "Её там нет."

    show mt surprise pioneer
    show cs smile ag_civil
    show mi shocked pioneer
    with dspr

    "На этом моменте охренели все. Кибернетик выдержал театральную паузу."

    show sh normal_smile pioneer
    show mi smile pioneer
    with dspr

    sh "Пока ехали, я заметил место, откуда более или менее видно эту станцию. Семён тогда спал."

    window hide
    show us surp2 sport with dissolve
    window auto

    us "И?!"
    "Девочка чуть ли не прыгала на месте от любопытства."

    show sh normal pioneer with dspr

    sh "Вытащил бинокль, посмотрел, и увидел старую знакомую. Там электромагнитная аномалия, ещё одна. У других двух всё было чисто, помните?"

    show mi shy pioneer with dspr

    mi "Обалдеть..."

    window hide
    hide mi with dissolve
    window auto

    "Японка опустилась на стул, погладив едва заметный животик."

    window hide
    show sl normal pioneer at left with dissolve
    window auto

    sl "Когда вещи собирать?"
    "Деловито уточнила активистка."

    window hide
    show el normal pioneer at fright with dissolve
    window auto

    el "Пока рано. Надо, чтобы лёд встал — тогда все вещи можно будет перетащить."

    window hide
    hide us with dissolve
    show mz normal glasses pioneer with dissolve
    window auto

    mz "Серёж, я принесла, что ты хотел... ой."
    "В дверях показалась Женя, с каким-то свёртком в руках. Через минуту на столе лежала карта Союза и прилегающих стран."

    scene cg ch_7_ussr_map with dissolve

    me "Какие будут предложения?.."

    window hide
    pause(2)
    window auto

    me "Ладно, а что наука говорит?"
    el "Наука говорит, что чем дальше, тем лучше."
    me "Информативно."
    dv "Иркутск."
    sh "Ась?"
    dv "Иркутск. В сам город, понятное дело, не выйдет — его наверняка бомбили."
    sv "Но рядом Байкал — это чистое, пресное озеро. Там можно жить."
    cs "Хорошая идея. Я — за."
    sh "Вполне возможно, я посмотрю дороги..."

    window hide
    scene bg int_dining_hall_sunset with dissolve
    window auto

    "На том и порешили. Быстро поев жареные грибы с каким-то пресным бульоном, я понял, что чертовски устал. В голове кружилась тысяча событий."

    scene bg ext_dining_hall_near_sunset with dissolve

    "Взяв за руку Алису, я вышел из столовой. В этот день мы в последний раз ночевали в домике. Впереди были холода, жуткие и противные. Осень взяла своё."

    scene bg ext_square_sunset with dissolve

    "Мы быстро разобрались, как провести электричество в административный корпус от генератора в бункере."

    scene bg ext_house_of_dv_sunset with dissolve

    "На улице меня трясло. Не то от холода, не то от всего, что сегодня произошло."

    scene bg int_house_of_dv_sunset with dissolve

    "Я никогда больше не забуду взгляда Василия, его голоса. "

    show blink

    extend "Этой ночью я не спал."
    "Но огонёк надежды, загоревшийся внутри, превратился в пламя огромного, пионерского костра."

    window hide
    stop music fadeout 3
    scene black
    pause(4)

    jump agr_day_8