label agr_day_5:

    $ backdrop = "days"
    $ agr_new_chapter(9, "Право на\nискупление")
    $ prolog_time()
    pause(1)
    play music saya_sabbath

    scene black
    show cg white_simon_mirror_1
    show cg2 white_simon_mirror_2
    show cg3 white_simon_mirror_3
    show prologue_dream
    with dissolve
    pause(2)
    window auto

    me "Хм..."
    "Аккуратно подвязав галстук, я вышел на улицу под свет фонарика."

    scene bg white_ext_simon_home_1
    show bg2 white_ext_simon_home_2
    show bg3 white_ext_simon_home_3
    show prologue_dream
    with dissolve

    "Свечение было каким-то странным, холодным, совсем не тем, к какому привыкли глаза."

    scene bg white_ext_houses_night_1
    show bg2 white_ext_houses_night_2
    show bg3 white_ext_houses_night_3
    show prologue_dream
    with dissolve

    "Все домики пустовали по понятной причине."
    "За мной выскользнула небольшая, стелющаяся по асфальту тень."

    play sound meow_cat

    "Знакомый кот посидел на крыльце, после чего хищно облизнулся и скрылся в кустах."
    "Я неспешно двинул в сторону площади, сунув руки в карманы."
    "Мерно мелькали фонарики, домики, кусты, деревья."

    scene bg white_ext_square_night_1
    show bg2 white_ext_square_night_2
    show bg3 white_ext_square_night_3
    show prologue_dream
    with dissolve

    "Вот и площадь."

    window show
    show boy dead pi at cleft behind prologue_dream
    show girl dead pi at cright behind prologue_dream
    with dissolve
    window auto

    "На противоположной стороне стояли две фигуры - мальчик и девочка."
    "Белые рубашки, белые босоножки, белые носки и белые галстуки."
    "Белая, мертвецкая кожа и такие же белые глаза. Всё было белым."
    "В руках они держали каменную трубу и барабан — истинно пионерские, музыкальные инструменты."
    "Они медленно шли ко мне."
    "Статуи у входа, главная легенда «Совёнка», после смерти мира, вдруг ожила."

    stop music fadeout 3
    window hide
    show blink
    play ambience ambience_int_cabin_day fadein 2 
    pause(3)
    scene bg int_simon_home
    $ day_time()
    $ persistent.sprite_time = "day"
    show unblink
    pause(1)
    window auto
    $ karta_otkrytie = True

    "Я нехотя разлепил глаза."
    "Судя по отсутствию методичного постукивания по крыше, погода подуспокоилась."
    "Рядом лежал Барсик."
    me "Опять ты пробрался сюда, котяра эдакий."
    "В голову, потихоньку, приходили воспоминания ночи, лицо скорчилось, брови сами полезли на лоб."
    me "Тьфу."
    "Рыжие куда-то слиняли, несмотря на ранние часы."

    stop ambience fadeout 3

    "Напялив шорты, я босиком, вразвалочку, прошёл к двери и высунул голову на улицу."

    play ambience ambience_camp_center_day fadein 3
    scene bg ext_houses_day with dissolve

    "Непривычная, странная свежесть, озоном вдарила в нос."
    "На дорожке единственной улицы расплескались, голубыми пятнами, лужи. Солнце периодически выглядывало из-за облаков, к счастью, совершенно белых и пористых. Ненастье миновало."
    me "Ну, слава богу."
    "Чуть приободрившись, я напялил рубашку, по старой памяти подвязал галстук, надел босоножки и вышел из домика."

    scene bg ext_square_day with dissolve

    "Я на автомате добрался до площади, где никого не было, почти как во сне…"

    scene bg ext_dining_hall_away_day with dissolve
    pause(1)
    scene bg ext_dining_hall_near_day with dissolve

    "Причиной тому было то, что весь отряд собрался у столовой. Лица у всех напряжённые, даже напуганные."

    play music music_list["just_think"] fadein 2
    stop ambience fadeout 3
    window hide
    show mt sad pioneer at cright with dissolve
    window show

    mt "...вы предлагаете делать? Ладно еда, но без воды как?! Виола, точно в водосточную нельзя?"

    window hide
    show cs normal at cleft with dissolve
    window show

    cs "Только через мой труп. Там не столько радиация, сколько токсины, пепел, короче столько гадости, что отравление гарантировано."
    me "Что случилось?"

    window hide
    show mt normal at center
    show cs normal at left
    with move
    show sl scared pioneer at right with dissolve
    window show

    sl "Вчера на столовую рухнула ветка, разбила тару, где была питьевая вода."
    "В глазах Слави была какая-то нерешительность и страх."
    me "Сколько её у нас осталось?"

    window hide
    hide mt
    hide cs
    hide sl
    with dissolve
    show sh serious pioneer with dissolve
    window show

    sh "Литр-полтора."
    "Шурик стоял, облокотившись на лавочку, периодически покусывая губы, но выглядел, в целом, довольно уверенно, почти как обычно."
    "Но я видел, что что-то изменилось в кибернетике, и знал почему."
    "Его глаза."
    "Странный, заумный взгляд куда-то исчез, остался в них только жутковатый камень, холодный и расчётливый."
    "И так всегда, кроме тех моментов, когда он смотрел на очаровательную японку."

    window hide
    hide sh with dissolve
    window show

    "Пусть хотя бы так, всяко лучше, чем запой или депрессия."

    window hide
    show dv normal pioneer with dissolve
    window show

    dv "Нужно коллективно решать этот вопрос. И как можно скорее."
    "Голос рыжей был странно серьёзным, почти официальным, с отточенной, металлической изюминкой."

    window hide
    hide dv with dissolve
    show cs normal at left with dissolve
    window show

    me "Виола, есть хоть какие-то варианты? Даже самые глупые и нереальные, нужна какая-то основа, нужно понимать, куда мыслить."
    "Медсестра молчала."

    window hide
    show el sad pioneer at cright with dissolve
    window show

    el "А если фильтрами нашими?"
    cs "Без толку."
    sl "Спиртом, дезин...{w=1}{nw}"

    show el upset pioneer with dspr

    cs "Без толку...{w} Даже таблетки, дезинфицирующие воду, не помогут. Саша, наверное, в курсе, о чём я. У него отец - военный."

    window hide
    hide cs
    hide el
    with dissolve
    stop music fadeout 3
    play ambience ambience_camp_center_day fadein 3
    window show

    "Вдруг стало душно — только не снаружи, а где-то внутри. Повисла тишина, только лёгкий ветерок покачивал деревья вокруг."

    window hide
    show mt sad pioneer with dissolve
    window show

    mt "На утро всем по сто грамм, уже дальше посмотрим..."
    "Сбивчиво и безэмоционально произнесла вожатая."
    "Я даже не сразу обратил внимание на случайный каламбур от вожатой."
    th "Да, стопочка сейчас не помешала бы..."

    window hide
    hide mt with dissolve
    show sh normal pioneer with dissolve
    window auto

    "Понуро и серо отряд зашёл в столовую. На крыльце остались только я и Шурик."
    "Кибернетик кивнул в сторону, мол, разговор есть."

    scene bg ext_dining_hall_away_day with dissolve

    "Чуть отойдя от столовой, Шурик заговорил."

    show sh normal with dissolve

    sh "Семён, тема серьёзная."
    me "Валяй."

    play music music_list["you_lost_me"]
    stop ambience fadeout 5

    sh "Помнишь, дня три назад, я рассказывал про катакомбы под старым лагерем?"
    me "Да, что-то такое припоминаю."
    sh "Я порасспрашивал вожатую. Это не просто тоннели, Сём, это бомбоубежище. Построили лет 20 назад, то ли в шестьдесят втором, то ли в шестьдесят третьем."
    me "Так, и?"
    sh "Если это бомбарь, и если его не обнесли, там могут быть запасы воды хоть какие-то, или таблетки, Виола говорила, сам слышал. И в самом старом лагере может быть что-нибудь."
    me "Ты хочешь сходить туда?"
    sh "Именно."
    me "И когда?"
    sh "Сейчас. Я сегодня часов в шесть встал, успел собрать то, что может помочь. У нас, оказывается, есть рабочий фонарик. Заряда на несколько часов хватит."
    me "Почему именно сейчас, а завтрак?"

    show sh rage pioneer with dspr

    sh "Ты в своём уме?"
    "Чуть не зашипел кибернетик."

    show sh serious pioneer with dspr

    sh "Ты думаешь, они нас отпустят?"
    me "Резонно...{w} Ладно, пойдём, дорогу хоть знаешь?"

    stop music fadeout 3
    play ambience ambience_camp_center_day fadein 3
    show sh normal_smile pioneer with dspr

    sh "Обижаешь, естественно."
    "Где-то между рёбер приятно потянуло."
    th "Надежда умирает последней. А у нас, кроме неё, хоть что-то, да осталось?{w} Вот-вот."

    stop ambience fadeout 3
    scene black with dissolve2
    pause(0.5)
    play ambience ambience_camp_center_evening fadein 3
    scene bg ext_square_day
    show sh normal pioneer
    with dissolve2

    me "Ты как, после вчерашнего?"

    show sh serious pioneer with dspr

    "Шурик в секунду изменился в лице."

    show sh normal pioneer with dspr

    sh "Погано, хоть на стену лезь."
    sh "Будто сердце вырвали, размяли под гидравлическим прессом и обратно пихнули."
    "Я хлопнул парня по плечу."
    me "Прорвёмся!"

    stop ambience fadeout 3

    "Кибернетик кивнул."

    scene black with dissolve
    pause(1)
    play music exodus fadein 3
    scene bg int_clubs_male_day with dissolve

    "В клубах нас ждали два рюкзака, укомплектованные на скорую руку."
    "Мысли ушли куда-то в сторону, реальность стала бесшовной, аморфной."
    th "А Алиса? Она наверное будет ругаться, испугается почём зря, а мы только туда и обратно.{w} Так ведь?"
    "Мысли о девушке как-то незаметно сложились в единую картину, появилось доселе незнакомое чувство."
    "Хотелось закрыть её от всего зла этого жестокого мира. Закрыть и больше никогда не отпускать."
    me "Ружьё?"

    window hide
    show sh normal pioneer with dissolve
    window auto

    sh "За столом лежит. Но только я эту железку в руки больше не возьму."
    me "Это ещё почему?"
    sh "Противно мне. От самого себя противно. Пошли."
    "Прихватив оружие, я вышел из клубов первым."

    scene bg ext_clubs_day with dissolve

    "Шурик какое-то время ещё что-то искал, однако, видимо, не нашёл и вышел вслед за мной."

    scene black with dissolve2
    stop music fadeout 3
    pause(0.5)
    play ambience ambience_forest_day fadein 3
    scene bg ext_path2_day with dissolve2

    "Погода, вопреки ожиданиям, портилась на глазах."
    "Когда я с Шуриком прошли, от силы, половину пути, с неба постепенно начало капать."
    "Мелкие, неуловимые и донельзя противные пары, будто облака спустились на землю."
    th "И небеса разверзлись."
    sh "Чёрт, вовремя же."
    "Покрепче вжавшись в вещмешок, я прибавил ходу."
    th "Вот ты какой теперь, союз нерушимый, могилок уютных. На кого ты нас оставил, скажи? За что?"
    "Через три дня — обезвоживание, через неделю — смерть. Не проще ли так — ружьём?"
    "Губы-то, вон, уже сухие, а терплю."
    "И Саня терпит."
    "Всем нам сейчас надо немного потерпеть, как-нибудь выкарабкаемся."
    "Я же ей обещал."

    stop ambience fadeout 4

    "Среди бесконечных деревьев замаячил просвет."

    play music fortress_of_doom
    scene bg ext_old_building_day with dissolve2
    pause(1)

    "Ещё через минуту мы вышли с тропинки на асфальтированную дорожку. Впрочем, дорожкой это было назвать очень трудно: поросший мхом и травой, асфальт всюду потрескался, оставив лишь напоминание, что когда-то, здесь была цивилизация."
    "Единственное, уцелевшее здание жилого корпуса, казалось, было готово рухнуть в любую секунду. Лагерь-загадка, лагерь-призрак."
    "Шурик сначала сбавил ход, а потом и вовсе остановился, разглядывая здание."

    window hide
    show sh serious pioneer with dissolve
    window auto

    sh "Быстро же здесь всё прогнило."
    me "Ты здесь был когда-нибудь?"
    sh "Нет, но хорошо знал, где это место находится."
    me "Профессиональный интерес?"

    show sh smile pioneer with dspr

    "Шурик усмехнулся."
    sh "Какой там… Байки местные, карты с прошлых смен…{w} и ещё по мелочи."

    show sh normal_smile pioneer with dspr

    sh "Мелкие на полном серьёзе утверждали, что призраков двух пионерок тут видели."
    "Кибернетик поправил очки на переносице."
    "На его лице проступили едва заметные скулы."
    me "А что ты?"

    show sh normal pioneer with dspr

    sh "А что я, Семён? Я сейчас хоть в дьявола готов поверить."
    sh "Ладно, двинули."

    window hide
    hide sh with dissolve
    window auto

    "Вокруг потихоньку сгущался туман."
    th "Здесь всё пропахло смертью."
    "Этот запах нельзя почувствовать в привычном смысле, его можно только осознать."
    me "Как на кладбище."
    "Шурик промолчал."
    "Пальцы сами сняли предохранитель двустволки." 
    "Повисла тягучая тишина."
    "Лагерь-призрак, погребённый лагерь."
    "Дверь в здание была запечатана. {nw}"

    stop music fadeout 3

    extend "Но к счастью, кто-то до нас заботливо вынес окно."

    window hide
    play sound sfx_jump_over_hole
    pause(1)
    scene bg int_old_building_day with dissolve
    play music music_list["sunny_day"] fadein 3
    pause(1)
    window auto

    "Стоило только пролезть в оконный проём, как тишина сменилась на что-то более зловещее."
    "Всё вокруг давило на мозг."
    "За каждым шорохом виделся рисуемый подсознанием монстр. Темнота обволакивала тело целиком, затягивая в бездну с неизбежным концом. Дополняла всю атмосферу странная акустика, из-за которой каждая капля, падающая с протекающей крыши, разносилась эхом по стенам корпуса, создавая непривычный звук."
    
    window hide
    show sh normal pioneer with dissolve
    window auto
    
    "Шурик стянул с плеч вещмешок и достал два фонарика."
    sh "Одни из последних, рабочих. Заряда хватит часа на четыре, но в любом случае, расходуй экономно. На всякий случай я захватил пару свечек."
    me "Тогда разделимся, я наверх, ты тут посмотри, если будет что-нибудь ценное, то зови."
    sh "План эвакуации попробуй найти, поможет найти вход в бомбоубежище."
    me "Постараюсь."

    window hide
    hide sh with dissolve
    window auto

    "Осторожно ступая по скрипящим ступенькам, я поднялся наверх."
    "В нос ударил запах плесени, давно поселившейся в здании."
    "Фонарик освещал небольшой коридор."
    "Я шагнул в неизвестность."

    scene bg black with dissolve
    scene bg int_old_building_day with dissolve

    "Всё вокруг говорило, что когда-то здесь всё же были люди."
    "По полу были разбросаны какие-то бумаги, уже давно истлевшие, нечитаемые."
    "На стене висела доска почёта."
    "Не знаю, что мной руководило в этот момент, но я решил разглядеть данный артефакт прошлого."

    scene cg ch5_hall_of_fame with dissolve

    "Все ячейки пустовали. Все, кроме одной."
    "В ней была чёрно-белая фотография чем-то знакомой девушки."
    "Осторожно, чтобы не порвать, я вынул фотографию из рамки."

    play sound sfx_paper_bag
    scene cg ch5_hall_of_fame_cs with dissolve

    "Она могла так и остаться неизвестной, если бы не две отличительные особенности: гетерохромия, узнаваемая даже через чёрно-белую призму, и улыбка."
    "Её я бы ни с кем не перепутал."
    me "Виола…"
    "Выходит, наша медсестра была здесь давным-давно, в последней смене первого «Совёнка»."
    th "Надо будет её спросить, как вернёмся."
    "Я сложил фотографию пополам и сунул в карман шорт."

    scene bg black with dissolve
    scene bg int_old_building_day with dissolve

    "Осмотр следующих двух комнат не дал ничего — только старая, прогнившая мебель."
    "Третья была почти пустой, но где-то в глубине комнаты что-то негромко тикало."
    "Я медленно пошёл на звук, одной рукой держа фонарик, а другой ружьё, от веса которого рука чуть дрожала."

    scene bg int_old_building_room_day with dissolve

    "Ступал медленно, напряжённо."
    "Источником звука оказались старые часы, непонятно как сохранившие трудоспособность."
    "Под часами лежал небольшой лист бумаги, похожий на ту, на которой Лена рисовала стенгазету."
    "Этот самый лист, хоть и пожелтевший, выглядел новее всего, что меня окружало, на десяток лет."
    "Я осторожно поднял листок и развернул."

    play sound sfx_paper_bag
    scene cg ch5_note with dissolve
    
    th "Оставь надежду всяк сюда входящий."

    play sound sfx_paper_bag
    scene bg int_old_building_room_day with dissolve

    "По спине пробежал холодок, я неприязненно поморщился, отбросил листок в темноту комнаты."
    "Снизу послышался голос Шурика:"
    sh "Семён, давай обратно, я нашёл!"

    scene bg int_old_building_day with dissolve

    "Я неспешно спустился к кибернетику, которого обнаружил, не сразу, разглядывающим небольшой стенд."

    scene cg ch5_escape_plan with dissolve

    sh "И что мы тут копошились, спрашивается? Вот он, весь план лагеря!{w} Нам в соседнюю комнату."

    window hide
    scene bg int_old_building_day with dissolve
    show sh serious pioneer with dissolve
    window auto

    me "Как думаешь, надолго мы тут застрянем?"

    show sh normal pioneer with dspr

    sh "О девчонках волнуешься? Электроник за ними приглядит…{w} Да они и сами… наши ведь девчата."

    scene bg int_old_building_luke_day with dissolve

    me "Хотелось бы верить, что не напрасно я в эту дыру лезу."
    sh "Не ты один."
    "Из открытого люка разило сыростью, старым бетоном и плесенью. В подкорку уже потихоньку закрадывался страх."
    "Там темно, сыро и холодно.{w} Но надо."

    stop music fadeout 3

    "Лестница уже порядком проржавела, но ещё могла удержать вес пионера."

    scene black with dissolve2
    pause(1)
    # Спуск по лестнице сос звуком найти. забудь.
    play music storm_is_cuming fadein 3
    scene bg int_catacombs_entrance with dissolve2

    "Первое, что я почувствовал — мрак вокруг, будто стал осязаемым, как жидкость. Он пронизывал тело, проникал в душу, наплевав на свет фонариков."
    "Ноги подрагивали, было непривычно и неприятно."
    "От спины к шее пронёсся табун мурашек. С минуту мы так и стояли, будто поражённые."
    "Первым в этот раз, как ни странно, ожил я."
    "Перехватил поудобнее ружьё, кивнул головой Шурику, мол, идём."
    "Сначала шли неуверенно, реагируя на каждый шорох, но вскоре более или менее свыклись с мраком и напряжением."
    "То и дело под лучом фонарика пробегали крысы, с потолка свисали мох и корни деревьев."

    play sound sfx_scary_sting
    with vpunch

    sh "Твою мать!"

    stop music fadeout 3

    "Я повернул фонарик в сторону, куда смотрел Шурик."

    stop ambience fadeout 3
    play music music_list["drown"] fadein 3
    scene anim ch5_body_in_catacombs with dissolve

    "На холодном полу, прижавшись к стене, сидел человек."
    th "Как в том сне..."
    me "Какого?…"
    "Я не успел договорить."
    "Из человека буквально выбежала крыса."
    "Лица кибернетика в тот момент я не видел."
    "Глаза уже привыкли к окружающей тьме, что, как бы странно то не было, совсем не радовало."
    "Назад — значит всё напрасно. "
    "Вперёд — страшно."
    "Я покрепче сжал двустволку в руках и медленно, натянутыми, как струны на гитаре, шагами двинул вперёд."
    "Шурик так и стоял."
    "Десять метров…"
    "Восемь…"
    "Пять…"
    "Передо мной лежал обглоданный крысами, переломанный от времени скелет."
    th "Откуда {b}это{/b} здесь?!"
    "Руки дрожали, не исключая вероятность шального выстрела куда-то между впадинами, в которых раньше были глаза."
    "Рядом валялась гнилая дощечка с непонятным знаком в кружке, напоминающем какое-то растение."
    me "Сюда иди."
    sh "Не могу."
    me "Иди, говорю."
    "Кибернетик не двинулся с места, только начал неторопливо стягивать рюкзак."
    me "Что там? Шура, заканчивай! Пошли, потом разберёмся."
    "Наконец кибернетик поднял голову и, не говоря ни слова, прошёл мимо."
    "Я, стараясь не задеть остатки покойной, пошёл за ним."

    play ambience ambience_catacombs fadein 3
    stop music fadeout 3
    scene bg int_catacombs_entrance with dissolve

    "Мрак, пустота и больше ничего."
    th "И всё-таки, откуда здесь быть мёртвой пионерке? Допустим, она ходила в этот лагерь, заблудилась в катакомбах или ещё чего… но ведь мы тоже здесь!"
    "Загнав мысли о собственной участи куда подальше, я шёл навстречу пустоте."
    "Как там Алиса говорила? \"Вглядываясь в бездну, помни, что бездна тоже взглянула на тебя\"."
    th "Чёрт, отчего так погано, голова кружится…"

    scene bg int_catacombs_door with dissolve

    "Тем временем, лучи фонарей встретились с какой-то преградой, упёрлись в стену."
    sh "Пришли, похоже."
    "Его голос был какой-то отстранённый, механический."
    sh "Помоги."
    "Совместными усилиями, со второй попытки и с помощью припасённого Шуриком ломика, мы открыли дверь."

    play sound sfx_open_door_mines_metal
    scene black with dissolve
    pause(1)

    "Сначала было так же темно, как и в тоннеле, но стоило кибернетику пощёлкать кнопки на каком-то выключателе рядом, "

    play sound click_menu
    scene bg int_catacombs_living with flash

    extend "как вдруг яркая вспышка ослепила меня на время."
    "Подождав, пока глаза привыкнут к свету, я оглядел помещение."
    me "Что-то мне подсказывает, что бомбоубежище не так выглядит."
    "Всего две кровати, металлические шкафчики, стол с приборами."
    "Однако по сравнению с тоннелем, здесь всё было стерильно — только пыль и запах, чем-то отдалённо напоминающий тот, что был в медпункте."
    sh "А это и не бомбоубежище."
    "Абсолютно спокойно ответил Шурик."
    sh "Это что-то вроде…{w} КПП."

    scene cg ch5_in_catacombs_sh with dissolve

    "Он протёр очки и быстрым шагом пошёл к столу, включил какую-то коробку, похожую на то радио, которое я видел три дня назад в клубах, напялил наушники, покрутил ползунки."
    "Минута тишины и…"

    stop ambience fadeout 2.0
    window hide
    pause(3)
    scene bg int_catacombs_living
    show sh normal_smile pioneer
    with dissolve
    window auto
    
    "Шурик вскочил, как ошалелый, и засмеялся."
    sh "Не одни!{w} Семён, мы не одни!"

    window hide
    hide sh with dissolve
    show black with dissolve
    $ renpy.movie_cutscene("afterglow_remastered/images/cg/supers/sovyonok_super.webm")
    hide black with dissolve
    window auto

    "Я рванул к наушникам, вжался в них что было силы и услышал то, чего никак не ожидал.{w} Музыку."

    window hide
    play music music_list["lightness_radio_bus"] fadein 2
    pause(5)
    show sh laugh pioneer with dissolve
    window auto

    sh "Ну-ну, дай мне тоже! Подумать только!"
    me "Только не говори, что ты потащишь эту металлическую дуру с собой."

    show sh smile pioneer with dspr

    sh "И то верно. Я завтра с Электроником сюда приду, будем разворовывать казённое имущество."
    "Саша впервые за два дня искренне улыбнулся, блеснул очками и погрузился в мир радиоволн."

    window hide
    stop music fadeout 3
    hide sh with dissolve
    play music sunday fadein 3
    window auto

    "Не желая тратить времени зря, я полез в стальные ящики, стоящие рядом."
    "В первом лежали три небольших, пластиковых, оранжевых квадратика."
    me "АИ-2?"
    "В памяти мгновенно всплыли уроки ОБЖ."
    "Аптечка индивидуальная."
    "Не без усилия открыв крышку квадратика, я увидел составляющие: шприц и несколько ампул, на которых были выгравированы буквы."
    me "Противорвотное…{w=0.8} Противоядие ФОВ…{w=0.8} Радиозащитное!{w} Шура, смотри, что нашёл!"
    "Шурик молча указал на вещмешок и снова расплылся в улыбке."
    "Когда последняя, пятая аптечка неаккуратно полетела в вещмешок, я полез во второй ящик."
    "Шурик, тем временем, стянул наушники и занялся другой стороной."
    "Противогазные фильтры, пара прорезиненных костюмов, ящик самих противогазов в сумках, более продвинутый дозиметр, который, несмотря на немалый вес, Шурик перекинул через плечо."

    window hide
    show sh serious pioneer with dissolve
    stop music fadeout 4
    play ambience ambience_catacombs fadein 5
    window auto

    me "А неплохо мы тут, Саш."
    sh "Неплохо, да только это просто КПП. Склад дальше. Главное — не заблудиться в этих лабиринтах, иначе нас никакой противогаз с дозиметром на пару не спасёт."
    me "Верно. Двинули?"

    show sh normal pioneer with dspr

    sh "Погоди, дай посижу.{w} И тебе советую."
    me "Тоже верно."

    window hide
    hide sh with dissolve
    window show

    "Кровать была жёсткая, солдатская."
    "Видимо предполагалось, что смена на КПП должна была идти двенадцать часов, иначе на кой им здесь быть?"
    "Ну и чёрт с ним."
    th "А может, мы это заслужили? Заслужили искупление, оставшись в семи шагах от смерти."
    "Шурик за секунду снова стал серьёзным."
    
    window hide
    show sh serious pioneer with dissolve
    stop ambience fadeout 3
    play music olesya_death
    window auto

    sh "Видал, что на той дощечке у трупа вырезано было?"
    me "Какое-то растение, я как-то не придал этому значения, а что?"
    sh "А ничего. Я похожий знак видел, когда в лес ходил, решил тогда… Развеяться решил."
    me "Врёшь же. Ну?"
    sh "Я нашёл там похожую, старенькую дощечку, но саму надпись было видно более отчётливо."
    me "И что?"

    show sh normal pioneer with dspr

    sh "Полынь."
    "Где-то я уже слышал про полынь."
    "Нахрен запрещу Ульяне соваться в эту сторону леса."
    "А лучше вообще ничего не говорить, а то запретный плод сладок."
    sh "Тебе этот знак что-то говорит?"
    me "Да. Был тут один вожатый, достаточно давно, он закопал тут какой-то клад. Улька с пацанами его искали, да не успели."
    me "Та, что в коридоре, видать, тоже искала."

    stop music fadeout 3
    play ambience ambience_catacombs fadein 3
    show sh serious pioneer with dspr

    sh "Паршивенько. Всё, пошли."
    me "Подожди-ка."
    "Глаз подметил ещё один ящик, армейского, зелёного цвета."
    "Достал, поставил на стол.{w} Внутри что-то звякнуло."
    "Мы с Шурой переглянулись."

    # play sound "sound/sfx/open_metal_hatch.ogg" volume 0.5
    show sh normal_smile pioneer with dspr

    sh "Калашников!"
    "С восхищением констатировал Шурик."
    "Естественно, на КПП военного объекта должно быть оружие."
    "Я с неуверенностью и каким-то мальчишечьим восторгом взял оружие в руки."

    stop ambience fadeout 3
    show sh serious pioneer with dspr
    play music hate_life fadein 3

    "Рядом лежал металлический ящик, который Шурик обозвал \"цинком\"."
    "В нём лежали пачки с патронами. Шурик прикинул что-то в голове."

    show sh normal pioneer with dspr

    sh "Тысяча восемьдесят патронов получается. Это ж на небольшую войну хватит! И с охотой вопрос отпадает, и с обороной, если придётся."
    me "Обалдеть, Шур."
    "Я присел на полу, вжавшись в автомат. А в голове одно:"
    th "Спасибо, родимый."

    window hide
    hide sh with dissolve
    window auto

    "\"Цинк\" оказался до чёртиков тяжёлым, килограммов пятнадцать-семнадцать. Минуты три провозившись со вскрытием коробки, мы складировали по пять пачек патронов в каждый вещмешок."
    "Вскрытый \"цинк\" мы решили оставить на видном месте, обещаясь забрать остальное на втором заходе."
    "Автомат я повесил себе через плечо, после чего, Шурик нашёл ещё какой-то выключатель и выкрутил все переключатели в рабочее положение."

    stop music fadeout 3
    scene bg int_catacombs_entrance with dissolve
    pause(4)
    play ambience ambience_catacombs fadein 3

    "Вопреки ожиданиям, на выходе из помещения, света не появилось."
    me "Вот она, хвалёная, сука, советская проводка."
    sh "Нам так или иначе на склад, это прямо."
    me "Ну, ни пуха…"
    "Прошли метров сорок, голова снова закружилась. Как её, клаустрофобия? Будь она проклята, ваша эта клаустрофобия — я дойду, я обязан."
    "Вдруг луч моего фонарика вырвал из уже наскучивших, бетонных стен что-то новое."

    scene bg int_catacombs_hole with dissolve

    "Посреди тоннеля был непроходимый завал, а перед ним зияла дыра в полу."
    "Шурик грязно выругался, хрустнул шеей, но с места не сдвинулся."
    
    window hide
    show sh serious pioneer with dissolve
    window auto

    me "Чё делать будем?"
    sh "Лезть, понятное дело."
    me "Ты больной, Шур?"

    show sh normal pioneer with dspr

    sh "Сёмыч, там склад. Там то, что может дать нам воду. Помнишь, Виола говорила? Не помогут таблетки."
    sh "А специальные фильтры вкупе с теми же таблетками, наверное, смогут сделать более или менее безопасную воду."
    "Я вгляделся в эту пропасть. Глубина — метра два, назад вылезти несложно, только внизу что-то отвратительно хлюпало."
    me "Раз так… А наверх как?"
    sh "Придумаем, пошли."

    window hide
    stop ambience fadeout 3
    hide sh with dissolve
    play sound sfx_jump_into_hole_2
    pause(1)
    scene bg int_catacombs_gas with vpunch
    play ambience ambience_catacombs_stones fadein 3
    $ karta_otkrytie = False

    "Кое-как сползли вниз, ноги в ту же секунду погрузились по щиколотку в какую-то жижу."
    "Над жидкостью витали какие-то отвратительно пахнущие перегноем пары."
    "Двигались медленно, в башке трещало, видимо, не только у меня."

    stop ambience fadeout 3
    show anim_bg int_catacombs_gas at drunk with dissolve
    play music glaucoma

    "В какой-то момент рюкзак стал чертовски тяжёлым, затошнило."

    play sound upal_w_wodu fadein 2
    
    extend " Я качнулся и завалился назад, упав в объятия холодной, мерзкой жижи."

    window hide
    $ prolog_time()
    show blink
    pause(4)
    scene cg ch5_old_camp_flashback_1:
        zoom 1.3 xpos -0.3
    show prologue_dream
    show unblink
    pause(2)
    window auto

    "Мускулистый мужчина в милицейской форме с отвращением наблюдал за тем, как двое других ведут какого-то паренька в форме вожатого, скрутив ему руки за спиной."
    "Рядом стояла невысокая девушка с заплаканными глазами, виднеющимися за большими очками."
    vj "Вы… Вы представляете, утром явился, рубашка рваная, весь в крови, глаза безумные."
    nagiev "Сколько, говорите, девочек пропало?"
    vj "Две… Оля Новикова и Ксюша Добронравова."

    show cg ch5_old_camp_flashback_2:
        zoom 1.3 xpos -0.3
        ease 0.7 zoom 1.3 xpos 0.0

    "Паренька, злобно скалящегося на мир, подвели к милиционеру."

    window hide
    $ night_time()
    show blink
    pause(4)
    scene bg int_catacombs_gas
    show anim_bg int_catacombs_gas at drunk
    show sh scared pioneer
    show sh scared pioneer at drunk_spr
    show unblink
    pause(2)
    window auto

    sh "Чёрт, Семён, не теряйся…{w} Семён!"

    window hide
    $ prolog_time()
    show blink
    pause(4)
    scene cg ch5_old_camp_flashback_2:
        zoom 1.3 xpos 0.0
    show prologue_dream
    show unblink
    pause(2)
    window auto

    nagiev "Вот и он, сукин сын. Куда девчонок дел?"
    "Паренёк заржал, плюнул в сторону вожатой."
    kb "Надеюсь, хых… товарищ вожатая, вы никогда не забудете своего лучшего вожатого — Кирюшу Белого. Будьте вы все прокляты! Будьте прокляты, суки!"
    vj "Уйди, уведите его.{w} Сволочь!"

    show cg ch5_old_camp_flashback_2:
        zoom 1.3 xpos 0.0
        ease 0.7 zoom 1.3 xpos -0.3

    "Вожатая снова расплакалась. Вожатого увели."
    nagiev "Извините, ещё пару вопросов, товарищ Никифорова. Служба."
    vj "Да-да?..."

    window hide
    $ night_time()
    show blink
    pause(4)
    scene bg int_catacombs_gas
    show anim_bg int_catacombs_gas at drunk
    show sh normal gas_mask
    show sh2 normal gas_mask at drunk_spr
    show unblink
    pause(2)
    window auto
    show blink
    scene bg black with dissolve
    play sound nadel_protivogaz fadein 1
    play sound2 dychanie_w_protivogaze loop fadein 3
    scene bg int_catacombs_gas
    show anim_bg int_catacombs_gas at drunk
    show sh normal gas_mask
    show sh2 normal gas_mask at drunk_spr
    show unblink
    show gas_mask
    with dissolve
    
    sh "Персунов, про… противогаз. Давай, давай, пошли."
    "Я опёрся о крепкое плечо кибернетика."
    "Всё было как во сне, если бы не дикая боль."
    th "И отчего так башка болит?…"

    window hide
    stop sound2 fadeout 2
    $ prolog_time()
    show blink
    pause(4)
    scene cg ch5_old_camp_flashback_1:
        zoom 1.3 xpos -0.3
    show prologue_dream
    show unblink
    pause(2)
    window auto

    nagiev "В каких отношениях состояли потерпевшие и подозреваемый?"
    vj "Ну в каких… общались понемногу, он их вожатым был. Добренький такой в начале был, весёлый, опрятен всегда был."
    "Вожатая протёрла запотевшие очки."
    nagiev "Может недавно происходило чего?"
    vj "Да нет вроде... Только может, с какого-то моменты он будто бы поменялся, стал угрюмым, как будто подменили..."
    nagiev "Езжайте в город. Мы с ребятами сейчас лес прочешем, а там видно будет. Может и живы ваши пионерки."
    nagiev "Василько, Приходько, вызывайте ещё наряд. Надо весь лес прочесать."
    
    window hide
    $ night_time()
    show blink
    pause(4)
    scene bg int_catacombs_gas
    show anim_bg int_catacombs_gas at drunk
    show gas_mask
    show unblink
    play sound dychanie_w_protivogaze loop fadein 2
    pause(2)
    window auto

    "Теперь уже я тащил Шурика, еле волоча ватные ноги."
    me "Сука ты, Кир Белый."
    "Непонятно для кого констатировал я."

    scene bg vybor_katakomby at drunk
    show gas_mask
    with dissolve

    me "А это ещё что... Твою мать, развилка."
    "Чуть осознавая где я нахожусь и что происходит, сказал я. Шурик был без сознания."
    th "И куда идти?"

    $ vybor_katakomby_klochok = False
    window hide
    
    menu:
        "Направо.":
                $ vybor_katakomby_klochok = True
                
        "Налево.":
                $ vybor_katakomby_klochok = False
    
    if vybor_katakomby_klochok == True:
        jump pravilnaya_doroga
        
    if vybor_katakomby_klochok == False:
        jump ne_pravilnaya_doroga

label ne_pravilnaya_doroga:

    stop music fadeout 5
    stop sound fadeout 2
    scene black with dissolve2
    pause(2)
    scene bg ext_path2_night
    show prologue_dream
    with dissolve2
    play music music_list["torture"] fadein 3
    $ set_mode_nvl()
    pause(1)

    kb "Одно тело в катакомбах, хрен кто там будет Оленьку искать. А эту я утоплю, главное — поглубже, поглубже."
    kb "Как же сочно они пытались визжать! Какие вещи можно сотворить одной только зажигалкой и иголками!"
    kb "Им же совсем не больно, они просто притворяются, они счастливы — я знаю."
    kb "Эх, надо было ещё молоточком, но кирпич тоже сойдёт, сойдёт. А как она извивалась, когда я пихал в неё эту арматурину!"
    kb "Можно было и глубже ведь, жаль, жаль. Но я дал им себя, они счастливы."
    kb "Я — Бог! " with vpunch
    extend "Я — Всё!" with vpunch
    kb "{b}Счастье для всех{w}, даром{w}, и пусть никто не уйдёт обиженным{/b}!"
    kb "Вот и речка.{w} А Ксюша наша ещё жива, дышит."
    kb "Интересно…"

    $ set_mode_adv()
    stop music fadeout 2
    scene bg int_catacombs_gas
    show anim_bg int_catacombs_gas at drunk
    show gas_mask
    with dissolve
    play sound gas_mask_hard loop fadein 2
    pause(2)
    $ set_mode_adv()

    th "Чёрт, надеюсь в правильную сторону пошёл..."
    "Ноги уже чуть держали меня и Шурика, было трудно ориентироваться."
    "Лёгким не хватало воздуха. Единственное что оставалось, это надежда."
    "Сил больше не оставалось, стараясь не упасть, я шёл вперёд.{w} Но конца не было видно."
    th "Где же он? Где?!"

    stop sound fadeout 2
    show blink
    pause(4)
    scene bg tupik_katakomby
    show anim_bg tupik_katakomby at drunk
    show unblink
    show gas_mask
    with dissolve
    stop sound fadeout 2
    play sound gas_mask_hard_2 loop fadein 3

    th "Нет... Нет, нет, нет! Твою мать тупик..." 
    th "Больше не могу..."

    play sound2 sfx_body_bump
    scene black with vpunch

    "Я без сил рухнул на пол, вместе с Шуриком. Тысячи мыслей проносились у меня в голове."

    scene bg tupik_katakomby
    show anim_bg tupik_katakomby
    show gas_mask
    show unblink
    pause(1.5)
    show blinking_loop

    th "Нет, я не хочу умирать... Только не так... А Алиса как без меня жить будет? Надо подняться. Надо сражаться за свою жизнь..."
    "Но тело не могло, попросту отказывалось что-либо делать."
    me "Алиса... Прости..."

    stop sound fadeout 2.0
    pause(2)
    play sound gas_mask_death fadein 1
    show blink
    $ renpy.pause(3,hard=True)
    stop sound fadeout 3
    window hide
    $ renpy.pause(2,hard=True)

    return

label pravilnaya_doroga:

    stop music fadeout 5
    stop sound fadeout 2
    scene black with dissolve2
    pause(2)
    scene bg ext_path2_night
    show prologue_dream
    with dissolve2
    play music music_list["torture"] fadein 3
    $ set_mode_nvl()
    pause(1)

    kb "Одно тело в катакомбах, хрен кто там будет Оленьку искать. А эту я утоплю, главное — поглубже, поглубже."
    kb "Как же сочно они пытались визжать! Какие вещи можно сотворить одной только зажигалкой и иголками!"
    kb "Им же совсем не больно, они просто притворяются, они счастливы — я знаю."
    kb "Эх, надо было ещё молоточком, но кирпич тоже сойдёт, сойдёт. А как она извивалась, когда я пихал в неё эту арматурину!"
    kb "Можно было и глубже ведь, жаль, жаль. Но я дал им себя, они счастливы."
    kb "Я — Бог! " with vpunch
    extend "Я — Всё!" with vpunch
    kb "{b}Счастье для всех{w}, даром{w}, и пусть никто не уйдёт обиженным{/b}!"
    kb "Вот и речка.{w} А Ксюша наша ещё жива, дышит."
    kb "Интересно…"
    
    stop music fadeout 2
    scene bg black with dissolve2
    $ set_mode_adv()
    scene bg int_catacombs_entrance
    show gas_mask
    with dissolve
    play ambience ambience_catacombs fadein 3
    with dissolve
    play sound dychanie_w_protivogaze loop fadein 3
    $ karta_otkrytie = True

    "Шурик сидел напротив, прижавшись к стене тоннеля."
    th "Мы как тут оказались? Что за дерьмо? Что я сейчас видел, сон?"
    th "Кир Белый… Вот кто ты такой был, гнида… или это просто мой бред?"
    "Я всё ещё туго соображал, рядом валялся включённый фонарик, ноги ныли от порезов и холода."

    play sound2 prinyal_tabletku

    "Кое-как добрался до кибернетика, вытащил из его вещмешка АИ, нащупал там синий тюбик противорвотного."

    stop sound fadeout 2
    hide gas_mask with dissolve
    play sound2 gasmask_take_off

    "Сняв противогаз с меня и Шурика, я проглотил две таблетки разом."
    "Похлопал Шурика по щекам, он что-то промычал, дёрнулся в сторону от меня, его вырвало."

    window hide
    show sh surprise pioneer with dissolve
    window auto

    "Отплевавшись, повернулся на меня, я дал ему ту же дозу таблеток."

    show sh upset pioneer with dspr

    "Ещё какое-то время сидели, потом кое-как встали и молча пошли по тоннелю, опираясь друг на друга."

    window hide
    hide sh with dissolve
    show bg black with dissolve2
    $ renpy.pause(4, hard=True)
    scene bg int_bunker with dissolve
    pause(1.5)
    window auto

    "Через минуту показалась тяжёлая гермодверь, Шурик вроде так её назвал, над ней висела рабочая лампа."
    "Мы потушили фонарики, вместе налегли на проржавевший переключатель и дверь пошла."
    "Сначала медленно, неуверенно, скрипя, но всё же отрабатывая должный цикл."

    window hide
    play sound otkryl_germodver fadein 1
    stop ambience fadeout 3
    show black with dissolve2
    $ renpy.pause(5,hard=True)
    scene bg int_storage with dissolve2
    pause(2)
    stop sound fadeout 2.0
    play music "<loop 1.41>afterglow_remastered/sound/music/sunday.mp3"

    "Внутри и правда был склад."
    "Помещение слабо освещали лампы с потолка."
    "Такого разнообразия, я ещё нигде не видел."
    "На металлических стеллажах лежали коробки, на них - другие."
    "Всё было чисто, будто ещё вчера здесь проходили люди."
    "Это был наш шанс, наше право на искупление."
    "Наш лучик света в кромешной тьме."
    "Мы улыбались как идиоты, сквозь боль и усталость."
    th "Мы смогли!"
    "Всё равно, как придётся идти назад, что придётся пережить."

    stop music fadeout 3

    "Я отбросил, непонятно как оставшееся при мне, грязное ружьё."

    play music music_list["afterword"] fadein 5

    sh "Ты бери левые, я правые."
    me "Ага."

    window hide
    show black with dissolve
    hide black with dissolve
    window auto

    "В первой коробке лежали сухпайки, которые я отгрузил в рюкзак не задумываясь."
    "В другой тушёнка."
    "В третьей - две пустые фляги."
    "В четвёртой - упаковки батареек."
    "Мы набивали сумки из-под противогазов и вещмешки дрожащими руками, с такой неописуемой надеждой и безумным наслаждением…"
    sh "Семён! Сюда давай!"

    window hide
    show sh normal pioneer with dissolve
    window auto

    "Шурик стоял перед приличных размеров коробкой."
    me "Чего нашёл?"
    "На коробке значилось ФВЭ-1С."

    show sh normal_smile pioneer with dspr

    sh "Спасение."

    window hide
    hide sh with dissolve
    window auto

    "Труба с небольшим насосом оказалась основной целью экспедиции."
    "Это был какой-то экспериментальный фильтр для воды."
    "Не найдя решения лучше, мы сделали из чёрной изоленты, найденной рядом, ручки для коробки, после чего было принято решение валить на поверхность."

    window hide
    stop music fadeout 3
    show sh serious pioneer with dissolve
    play ambience ambience_catacombs fadein 3
    window auto

    me "И что нам, обратно?"

    show sh normal pioneer with dspr

    sh "У бомбоубежища всегда есть запасные выходы. В конце концов — вентиляционная система. Ты как хочешь, Сём, а я больше к этой жиже ни ногой."
    me "Тут я с тобой солидарен."

    show sh serious pioneer with dspr

    "Процедил я, подбирая ружьё."

    play sound sfx_drop_alisa_bag

    "Вещмешок казался неподъёмным."
    me "Помнишь, как выбрались?"

    show sh normal pioneer with dspr

    sh "Не-а."
    me "И я не помню. Что это было - тоже без понятия."
    sh "Зато я знаю."
    "Шурик присел на нижнюю полку стеллажа."
    "Я вопросительно выгнул бровь."

    show sh serious pioneer with dspr

    sh "В шахтах бывают газы. Метан, углекислый, азот, серные испарения. Вот мы и надышались."
    sh "Я как увидел, что ты свалился, схватился за первый попавшийся противогаз, но он, видимо, на размер больше моего."
    "Он постучал по голове пальцем."

    show sh normal pioneer with dspr

    sh "Так что не сильно помогло."
    me "Ладно, потом обсудим, давай выход искать."

    scene black with dissolve
    stop ambience fadeout 3
    pause(1)
    play music cornfield_chase
    scene bg int_catacombs_path with dissolve

    "Спустя какое-то время был найден небольшой проход, который вёл в закрытую комнату, а из неё в другую."
    "Фонарики уже помирали, а идея, искать батарейки в набитых до отказа рюкзаках, нисколько не радовала."
    "Вдруг, нос уловил свежий воздух и спустя пару минут поиска, была найдена лестница наружу, ведущая в другую комнатушку."

    scene bg int_mine_room with dissolve

    "Комната напоминала заброшенный бомжатник — всюду валялись разбитые бутылки и окурки."
    "Кое-как подняв вещмешки и коробку с фильтром мы решили немного передохнуть прямо тут."
    "Переводя дух, мы неспешно осматривали комнату и увидели лучи света, пробивающиеся откуда-то с потолка."

    scene bg int_mine_exit_night_light with dissolve
    $ karta_otkrytie = False

    "Щель была найдена мгновенно, ей оказался люк на замке, возиться с которым не было уже никакого желания."
    play sound "<from 0.0 to 2.0>sound/sfx/break_grid.ogg"
    "Я стал бить по замку прикладом ружья, надеясь сбить его как в фильмах."
    sh "Семён, это так не работает!"
    "Я был уже озлобленный и не слушая Шурика,"
    
    play sound vystrel_v_reshotku

    extend " выстрелил прямо в замок, а после продолжил сбивать его прикладом."

    window hide
    play sound sfx_break_grid
    pause(2)
    scene bg int_mine_exit_night_light_open with dissolve
    window auto

    "На этот раз замок поддался и люк наконец-то открылся."

    window hide
    stop music fadeout 3
    play ambience ambience_camp_center_evening fadein 3
    $ day_time()
    $ persistent.sprite_time = "night"
    scene bg noch_ploshad_bez_dozdz with dissolve
    window auto
    
    "Выбрались мы, к огромному удивлению, на улицу лагеря.{w} Нашего лагеря."
    "Мы, грязные, ободранные, уставшие как собаки, сбросили вещмешки и упали на зелёную, мягкую траву."
    "Собрав последние силы, я завопил:"
    me "Алиса-а-а!{w=1.5}{nw}" with vpunch

    window hide
    pause(1)
    show dv shocked pioneer at center with dissolve
    pause(0.7)
    show un shocked pioneer at cleft behind dv with dissolve
    pause(0.7)
    show sl scared pioneer at cright behind dv with dissolve
    pause(0.7)
    show us surp2 pioneer at fleft behind un with dissolve
    pause(0.7)
    show mi shocked pioneer at fright behind us with dissolve
    pause(3)

    stop music fadeout 4
    stop ambience fadeout 4
    scene black with dissolve2
    pause(4)
    show prologue_dream with dissolve2
    $ sunset_time()
    $ persistent.sprite_time = "night"
    $ set_mode_nvl()
    play music music_list["confession_oboe"] fadein 3
    pause(2)
    window auto

    "Никогда раньше я не получал такое удовольствие просто от того, что лежал на кровати. Мы оказались идиотами, самыми последними!"
    "Когда зашёл разговор о дефиците воды, все думали о озёрах, о очищении дождевой. Уверен, у каждого была своя теория на этот счёт."
    "Но мы коллективно забыли самое простое, до жути банальное.{w} Мы забыли про баню."
    "В избушке оказалось два резервуара, вполне пригодной для употребления, воды, по двадцать пять литров каждый."
    "Хватило бы ненадолго, но забыть такую мелочь было смертельно глупо."

    window hide
    hide prologue_dream with dissolve
    $ set_mode_adv()
    scene bg int_house_of_dv_night
    show dv angry pioneer at right
    show unblink
    window auto

    "По комнате вперёд-назад ходила Алиса и ругалась."

    hide unblink
    show dv angry pioneer at frtl_e

    dv "Нет, Персунов, ну как это называется? Вот нельзя что ли было предупредить?! Мы весь лагерь в поисках вас облазили."

    show dv angry pioneer at fltr_e

    me "А вы бы отпустили?"
    "Беззлобно усмехнулся я."

    show dv angry pioneer at frtl_e

    dv "Нет конечно! Ты себя видел вообще? Бледный, глаза краснючие, ноги в порезах. И с автоматом."
    "Ноги мне заботливо обработала Виола, после того, как я, опираясь на Алису, доковылял до крыльца своего домика."

    show dv angry pioneer at fltc_e

    dv "Где вас вообще так угораздило?"
    me "В катакомбах под лагерем."

    show dv surprise pioneer with dspr

    dv "Каких катакомбах?!"
    me "Самых обычных."
    me "Там газ какой-то, еле выкарабкались. Молодым было бы слишком обидно умирать."
    me "Не бери в голову, короче говоря."

    show dv angry pioneer with dspr
    stop music fadeout 4

    dv "Дурак."

    play ambience ambience_int_cabin_evening fadein 3

    window hide
    show dv smile pioneer with dspr
    $ renpy.pause(0.2, hard=True)
    show dv smile pioneer at sit_close
    window auto

    "Остывая констатировала рыжая, присаживаясь рядом со мной на кровать."
    dv "Дай хоть поцелую."

    window hide
    show blink
    pause(1.5)
    hide dv
    window auto

    "Двачевская улеглась рядом. Целовалась отчаянно, беспокойно, будто в последний раз."
    "Моя рука сама легла на холодную ляжку, поползла вверх. "

    window hide
    show dv surprise pioneer
    hide blink
    show unblink
    pause(1.5)
    window auto

    extend "Алиса резко оторвалась от моих губ."
    dv "Ты… ты чего?"
    me "Она сама."
    dv "Ты сейчас хочешь? Больной ведь совсем…"
    me "Ты имеешь что-то против?"

    show dv normal pioneer with dspr
    stop ambience fadeout 5

    "Девушка колебалась всего мгновение, вернулась к поцелую."

    window hide
    play music music_list["i_dont_blame_you"] fadein 5
    show blink
    pause(2)
    $ set_mode_nvl()
    scene bg semen_i_alisa_ebla
    show prologue_dream
    with dissolve2
    pause(1)
    window auto

    "Щёлкнула пряжка ремня.{w=0.5} В соседнем домике Мику громко отсчитывала Шурика."
    "Щёлкнула застёжка лифчика.{w=0.5} Боковым зрением увидел спящего на соседней кровати Барсика."
    "Она сидела на мне совсем голая, холодная, стесняющаяся.{w=0.5} Нам двоим сейчас было необходимо выпустить накипевший стресс, так почему бы не сделать это вместе?"
    "Забыть к чёртовой матери весь этот проклятый мир, остаться вдвоём. Только мы и эта бесконечная свобода."
    "Впереди были два месяца изнуряющей работы - подготовки к зиме.{w} Дрова для котельной, чистая вода, медикаменты, тёплые вещи, переоборудование административного корпуса."
    "Это всё потом{w=0.5}, пока были только мы{w=1}, и лето 1987-го года."

    window hide
    scene bg black with dissolve2
    stop music fadeout 3
    pause(3)

    jump agr_day_6