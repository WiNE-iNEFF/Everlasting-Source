 label agr_exodus:

    $ backdrop = "days"
    $ agr_new_chapter(12, "Исход")
    $ persistent.sprite_time = "night"
    $ prolog_time()
    $ set_mode_nvl()
    play music sunday fadein 3
    play ambience ambience_cold_wind_loop fadein 3
    pause(1)
    window auto
    scene cg ch_9_camp_winter with dissolve

    "Оставались последние сборы. Как могли, по максимуму, забивали машину, перевозили всё необходимое, пришлось учиться водить."
    "Кибернетики чуть ли не переехали на станцию. Повезло, что аномалия ушла недалеко после последнего выброса. На исходе было топливо и нервы."
    "Игорь, кстати, оказался вполне деловым пареньком: помогал кибернетикам с реанимацией «Филина», успевая ухаживать за Славей. Вытащили из бункера всё, что оставалось: все фильтры, лекарства, какие-то старые бушлаты."
    "На улице уже неделю как мело: зима пришла рано. Дров для отопления хватало, только приходилось дежурить по ночам в котельной. По очереди, понятное дело."
    "Ульянка хорошо сдружилась с Алесей и уже не скучала. У Мику всё более отчётливо виднелся животик, а Виола всё переживала, что если в пути рожать, то это будет худший из возможных вариантов, но выхода особо не было."
    "Кое-как, с помощью машины и ручной дрезины, подогнали к ангару с «Филином» пассажирский вагон, а за ним два с углём."
    "Все готовились к дальней и, соответственно, долгой дороге."

    $ set_mode_adv()

    "Это был последний день в лагере. Так же набивали до упора «Волгу» тёплыми вещами и сухпайками."
    "Ещё и встали ни свет ни заря, в пять утра. Всё топливо, что было, и что чисто в теории могла есть машина, влили в бак и так по частям перевозили наше скромное барахло."
    "За рулём сначала был Электроник, но часам к десяти он банально начал засыпать и его подменил сначала я, а меня, потом, Шурик. Славя, Алеся и Игорь у самого поезда принимали всё, что мы привозили, к ним, по мере перевозки, присоединялись остальные."
    "В лагере остались я с Алисой, Виола и Ольга."
    "Мне пришлось волочь увесистый мешок, кажется, с нижним бельём из клубов по улице. Сзади меня шла Алиса с коробкой, где хранили патроны, кряхтя и изредка ругаясь под нос."
    me "Ничего, солнце, ща погрузим ещё немного и всё. Спать будешь под стук колёс поезда."
    "Конкретно сейчас верилось во всё это с трудом. Уставший, как собака, был каждый из нас."
    dv "Ага, под стук. Щас бы дотащить это всё до поезда..."
    me "Потерпи. Чуть-чуть ведь осталось."
    "Давно мёртвый «Совёнок» прощался со своими обитателями по-особенному. Хрустом снега под ногами, холодно светящим солнцем в зените и сдавливанием в сердце."
    th "Прости нас, дом родной."

    stop music fadeout 3
    window hide
    show bg ext_square_winter behind anim with dissolve
    $ set_mode_adv()
    show sh normal hazmat_suit at right behind anim with dissolve
    window auto
    play sound3 zvuk_motora_ciho loop fadein 1.5

    "Притащились на площадь, скинули мешки в заведённую машину. У «Волги» в утеплённой химзащите курил Шурик, сняв резиновые рукавицы."
    me "Дай закурить."

    play sound2 zajigalku_zakryili
    pause(1.5)
    play sound zazigalka
    pause(1.5)

    "Он кивнул, вытащил смятую пачку и протянул мне суховатую беломорину. "

    play sound sigareta
    
    extend "В горло ударил гаденький, но тёплый дым, ноги чуть расслабились."

    window hide
    show dv normal coat behind anim with dissolve
    window auto

    dv "Едет?"
    sh "Да чёрте как: ухабы такие, что раз в триста метров останавливаешься, буксует, а там ещё и дозиметр трещит."
    sh "Как Эл ехал, вообще не известно."
    me "Ещё пару заездов и всё, на поезд?"
    sh "Не-а, ещё раза три: вас забрать надо, сейчас Ольгу отвезу, потом Виолу."
    "Помолчали."
    dv "А сколько тут, если через реку?"
    "Шурик недоуменно посмотрел на Алису, вздохнул."

    show sh serious hazmat_suit behind anim with dspr

    sh "Ну, считай, метров семьсот. Радиации особо не нахватаете, но..."
    dv "Надоело по холоду шататься."

    show dv guilty coat close behind anim with dspr

    "Жена, как маленький котёнок, уткнулась мне в грудь, грея руки за моей спиной."
    th "А ведь права она, холодно жутко."
    me "Шурик, что там твой термометр показывает?"
    "Кибернетик потёр щетину, заглянул в салон."
    sh "Минус двадцать."

    show dv sad coat close behind anim with dspr

    dv "Жуть, Сём, жуть!"
    me "Жуть."
    "Согласился я."
    "Греться было особо негде, в здании тоже прохладно уже небось. Идея с костром отметалась, особого тепла маленькое пламя не давало, да и разжигать его разве что книжками, которые остались в библиотеке."
    dv "Ладно, пойдём мы, вроде у Виолы ещё осталось какое-то барахло."

    window hide
    hide sh with dissolve
    window auto

    "Кибернетик кивнул, на дальнем конце площади как раз показалась Ольга с тремя, набитыми до отказа неизвестно чем, вещмешками и двумя ружьями."
    me "Во даёт."
    dv "Вожатая наша бой-баба, не забывай."
    "Алиса ёжилась и чуть дрожала от холода. С неба шёл мелкий... Нет, не снег. Это был лёд."
    "В голову пришла идея."
    me "Алис, подойди."

    play sound2 zajigalku_zakryili
    pause(0.5)
    play sound zvuk_chirkanya_zajigalki

    "Я достал из кармана зажигалку, поджёг, поднёс поближе к ней. Она вынула руки из рукавов пальто, начала греться."
    "Здоровье рыженькой было очень относительно, нестабильно и заболевать ей ещё раз ни в её, ни в тем более мои планы не входило."

    show dv shy coat close at sit_close behind anim 
    play sound zajigalku_zakryili

    "Она вдруг резко захлопнула зажигалку, сократила до миллиметров дистанцию между нами и поцеловала."
    dv "Спасибо, родной."
    me "Да грелась бы дальше, успеваем."

    play sound2 zajigalku_zakryili
    pause(0.5)
    play sound zvuk_chirkanya_zajigalki

    "Она улыбнулась, смущённо повела плечами, снова вспыхнул огонёк зажигалки. Так догорало наше бесконечное лето."
    "Последние мечты, надежды на нормальную жизнь. Но мы, хотя бы, не дали им замёрзнуть в ядерном холоде."

    stop sound3 fadeout 2.0
    window hide
    show black
    hide dv
    with dissolve
    $ set_mode_nvl()
    hide black with dissolve
    window auto

    "Из музыкального клуба, где временно хранилось содержимое медпункта, всё выносили в три захода. Лекарства, какое-то простое оборудование, пару десятков книг по медицине, небольшой ящик специальных фильтров для работы со спорами, в бункере нашли, и ещё что-то по мелочи."
    "Под конец Виола, хитро улыбаясь, выдала мне два презерватива и ушла в сторону площади, где собирала наши рюкзаки Алиса."
    "Ни одно здание мы не запирали. Медпункт же Виола заперла ещё два дня назад, не желая никому отдавать родное здание."
    "Я шёл ровно за ней, след в след. Возникло странное ощущение, будто откуда-то со стороны леса кто-то наблюдает. Неприятное ощущение, жгущее спину: я позволил себе обернуться, но ничего не увидел."
    th "Ну кто бы это мог быть? Кир Белый ещё скажите."
    th "Что ж, сукин сын, прощай. Больше мы не увидимся точно. Катись прочь из нашего лагеря."

    window hide
    $ set_mode_adv()
    window auto
    play sound3 zvuk_motora_ciho loop fadein 1.5

    "Шурика ждали недолго. Кибернетик уже заметно устал и замёрз."
    "Алиса сидела на лавочке, съёжившись, как воробушек."
    "Я оперативно сунул, почему-то оказавшиеся в руке, резиновые изделия в карман, помог загрузить машину."
    "Виола, закутавшись в какой-то дырявый плед, уселась на переднее сидение."

    window hide
    show sh normal hazmat_suit behind anim with dissolve
    window auto

    sh "Тебе ружьё или автомат? А то тут и так впритык всё."
    "Размышлял я недолго."

    menu:
        "Взять ружьё":
            me "Давай ружьё."

        "Взять автомат":
            me "Автомат давай."
            $ good_gun = True

    sh "Хорошо, держи. Ещё такой вопрос, мне вас забирать или сами через лёд пойдёте?"

    me "Насколько опасно?"

    sh "Я проверял, лёд толстый, ничего не должно произойти. Если же подождёте минут тридцать, то я успею туда и обратно.{w} Хотя хрен знает хватит ли топлива на всё..."

    me "Доберёмся сами, не проблема."

    window hide
    if good_ending == True:
        $ agr_show_achievement("the_long_way_home")
    else:
        $ agr_show_achievement("wonderful_world")
    hide sh with dissolve
    window auto
    pause(2)
    play sound zvuk_motora_gromko fadein 1.5
    stop sound3 fadeout 2

    "«Волга» взревела мотором и рванула в сторону ворот. Остались только я с Алисой. Одни в пустом лагере. Я взял рюкзак, повесил оружие."

    window hide
    show dv smile coat behind anim with dissolve
    window auto

    "Она вдруг взяла меня за руку и потянула в сторону домиков."
    me "Ты чего, солнце?"
    dv "Пошли. Хочу кое-что напоследок сделать."
    "Противиться я не стал."

    scene bg black with dissolve
    $ persistent.sprite_time = "day"
    scene bg ext_house_of_mt_snow
    show dv smile coat
    with dissolve
    play music metro_teardrops fadein 3

    "Конечной точкой нашего маршрута стал бывший домик вожатой."
    th "И чего мы сюда пришли?"
    dv "А ты разве не помнишь?"
    me "Я это вслух сказал?"
    dv "Ты громко думал."
    "Она усмехнулась. А я вспомнил. Понял, о чём она говорила."
    "Помнится, я запоздал с тем, чтобы сдать обходной листок, возвращались вечером. Мы тогда с ней в первый раз обнялись."

    play sound padenie_veshej

    "Я снял рюкзак, скинул оружие, подошёл к ней вплотную, именно на то место, сантиметр в сантиметр, прижал к себе."

    show dv cry coat with dspr

    "Она плакала."
    dv "Прощай, «Совёнок»."
    "Я сглотнул подкативший к горлу ком."
    me "Прощай."
    "С неба падал снег."

    stop music fadeout 3
    scene bg black with dissolve
    play sound nadel_protivogaz
    pause(1)
    $ persistent.sprite_time = "night"
    scene bg ext_ice_lake
    show gas_mask
    with dissolve
    play sound3 dychanie_w_protivogaze loop fadein 1.5
    play sound2 shagi_po_snegu loop fadein 1.5

    "Лёд стоял уже неделю, и был уже достаточно плотный. Кибернетики пару дней назад гуляли, мерили радиацию, оказалась вполне приемлемой, но противогазы мы всё-таки нацепили.{w} На всякий случай, а то ещё детей рожать."
    "Прошли остров, тот, который ближний. По льду мело мелкими хлопьями снега. Я держал её за руку, холодную, покрасневшую, с кольцом на безымянном пальце."
    "Молчали. Было грустно, было паршиво, но я чувствовал, что нас ждёт что-то новое, и это новое было чем-то определённо хорошим."

    play sound voj_volkow

    "Сквозь кромешную тишину, нарушаемую лишь порывами ветра, я услышал едва различимый протяжный звук. Знакомый, отвратительный. Вой. Причём не один."

    stop sound2 fadeout 1.5
    pause(1)
    play sound2 odna_upala

    "Одним рывком скинул с себя вещмешок."

    window hide
    show cg ch_9_wolfs behind gas_mask, anim with dissolve
    stop ambience fadeout 3
    play music walking fadein 3
    window auto

    if good_gun == False:

        play sound sniatie_s_pred

        "Обернулся, шагнул чуть вперёд и вбок, закрыл собой жену, видимо, ещё ничего не понимающую, снял двустволку с предохранителя. Сквозь лёгкую метель виднелись силуэты."
        th "Один... два... пять... сколько же вас, сука?!"

        play sound sobaki_ogryzautsa loop fadein 1.5

        "К нам стремительно приближалась рыча и подвывая свора собак. В сердце закрался ледяной ужас."
        me "Беги, бросай вещи и беги!"
        "Заорал я."
        "Алиса не двинулась с места."
        dv "Только с тобой!"
        me "{b}Беги, мать твою, Алиса!!!{/b}" with vpunch
        "Уже срывая связки завопил я. Нехотя послушалась."

        stop sound fadeout 1.5

        "Метров тридцать, в стволе вроде бы были патроны. Дрожащими руками прицелился."

        play sound drobovik_gromko

        extend " В уши оглушительно врезалась волна взрыва."
        "Самая наглая и, вероятно, самая голодная тварь свалилась на бок."

        play sound drobovik_gromko
        pause(1.0)
        play sound perelom_drobovika

        extend " Ещё раз выстрелил — промахнулся, переломил ружьё. Патроны всё никак не хотели влезать в ствол, уронил один."
        me "А-а-а, сука!"
        "Схватил ружьё, как дубинку, побежал на тварей, метров десять оставалось, всё равно не успел бы перезарядить."
        th "Лишь бы она убежала, лишь бы..."

        play sound sobaka_gryzet fadein 0.5
        pause(0.5)

        "Одна тварь с ходу прыгнула на меня, отвратительно брюзжа слюной и гноем изо рта."

        play sound2 preklad
        pause(0.1)
        play sound dog_whimpering

        extend " Еле отбил прикладом, собака завизжала, отлетев куда-то в бок."

        play sound padenie_v_sneg
        play sound2 sobaki_gryzut loop fadein 0.5

        "Тут же в руку вцепилась другая, я больно упал на лёд, подскочила ешё одна."

        stop sound3 fadeout 0.7
        pause(0.7)
        play sound3 gas_mask_hard loop fadein 2.0

        th "А где остальные?"
        "Я на секунду бросил взгляд в сторону берега, который казался таким близким. За Алисой погнались ещё два пса."

        play sound udar_loktem

        extend " Что было силы локтем двинул по морде ближайшей, попытался отползти, вцепились в ноги."
        me "Отстань, фу!"
        "Схватил рюкзак Алисы, кинул в псин, те только больнее вцепились в плоть. Было жутко больно."
        "Нащупал столовый нож на поясе, рванул куртку."

        stop sound2 fadeout 2.0
        play sound rezko_vynuli_nozh

        extend " Вонзил в шею одной, та забилась в агонии, но не отпустила ногу, вторая тварь, видимо, испугалась, отцепилась, затяфкала, но отбежать не успела."
        
        play sound sloman_cherep

        extend " Под тяжёлым локтём треснул череп, хрустнули чьи-то кости."

        hide cg
        show bg ext_ice_lake at drunk behind gas_mask, anim
        with dissolve
        play sound2 bystro_po_snegu loop fadein 1.5 

        "Ни секунды не медля, сам не знаю как побежал в сторону берега. Постоянно спотыкался, по левой ноге стекало что-то вязкое и тёплое."
        "С каждым шагом становилось всё больнее, впереди не видел ничего, только небольшие точки, в глазах всё плыло, выронил нож. И боль, боль."

        if pristrelit_psa == False:

            stop sound2 fadeout 0.5
            play sound padenie_v_sneg
            
            "Споткнулся, разбил нос, палец вывихнул. На губах стало солёно. Подняв глаза, я увидел лежащую на льду Алису и собаку, которая вцепилась в неё."
            me "Отстань от неё, мразота! Сюда беги!"
            "Как мог, прокричал я.{w} Но собака даже не повернулась в мою сторону."

            play sound2 bystro_po_snegu loop fadein 1.5

            "Встав, я мигом рванул к Алисе.{w} Всё ближе и ближе."
            "В глазах помутнело. Я видел что-то красное, медленно увеличивающееся в размерах."
            th "Нет, нет, нет... Только не Алисина кровь..."

            stop sound2 fadeout 0.5
            play sound silnyiy_udar
            pause(0.1)
            play sound2 dog_whining_short

            "Как только подбежал, сразу же откинул псину ногой. Она проскулила, отлетела на метра два, после чего сразу же побежала в мою сторону."
            "Я держал нож дрожащими руками, от боли, от страха."

            play sound padenie_v_sneg

            "Тварь вцепилась в меня и повалила на лёд, пытаясь ухватиться за мою шею."

            play sound tykanie_nozom
            $ renpy.pause(2.0,hard=True)

            "Рукой, в которой лежал нож, я замахнулся и начал бить её по спине, по морде."

            play sound rezko_vynuli_nozh

            "Ещё минуту она не хотела отпускать меня, но в конце, всё же, ослабила хватку и упала замертво."
            "Только потом я заметил, что у этой собаки было три лапы и пробитое ухо."

            play music echoes_of_the_past

            th "Вот же тварь, а я пощадил тебя, решил проявить милость! Ладно, на это нет времени, надо помочь..."
            "Я повернулся в сторону Алисы, она лежала на льду, без сил...{w} С перегрызанным горлом."
            me "Алиса!"
            "Подполз к ней, всё ещё дышала, а точнее захлёбывалась. Я прижал руками место укуса и откуда текла кровь."
            "Вся рука стала красной. Взял порваную часть одежды, попытался ещё раз прижать."
            me "Нет... Не умирай... Всё будет хорошо, я-я обещаю!"
            "Алиса смотрела на меня глазами, которые были полны страха, разрушенных надежд. Она не хотела умирать."
            "Я делал всё что мог. Старался зажать так, чтобы крови не шло, но как назло, её количество лишь увеличивалось."

            stop sound3 fadeout 1.5
            $ renpy.pause(1.3,hard=True)
            play sound3 dychanie_w_protivogaze loop fadein 1.5

            "Вдруг, её стало меньше, она перестала так быстро течь. Я обрадовался, посмотрел на лицо Алисы."
            "Глаза были пустыми.{w} В них ничего.{w} Ничего, кроме боли."
            "Она мертва."
            "Ещё некоторое время я так и сидел, смотрел в никуда, не понимал что происходит."
            th "Может мне всё это кажется? Точно, как с дедом, это неправда..."
            "Минута.{w} Две.{w} Три.{w} Четыре."
            "Ничего не менялось даже спустя десять минут."
            "Я не видел ничего.{w} Ничего, кроме Алисы, уже мёртвой.{w} Я не видел будущего."
            "Посмотрел в сторону.{w}"

            play sound2 medleno_po_snegu fadein 1.0
            $ renpy.pause(1,hard=True)
            
            extend " Пошёл туда.{w}"
            
            $ renpy.pause(1.5,hard=True)
            stop sound2 fadeout 2.0
            
            extend " Поднял что-то тяжёлое."

            play sound perelom_drobovika

            "Что-то щёлкнуло."

            menu:
                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

                "Меня ждут в поезде.":
                    $ renpy.pause(1.0,hard=True)
                    jump da

                "Надо двигаться дальше.":
                    $ renpy.pause(1.0,hard=True)
                    jump pojezd

            label da:
            
            menu:
                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

                "Надо двигаться дальше.":
                    $ renpy.pause(1.0,hard=True)
                    jump pizdec

            label pojezd:

            menu:
                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

                "Меня ждут в поезде.":
                    $ renpy.pause(1.0,hard=True)
                    jump pizdec

                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

            label pizdec:

            menu:
                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa

                "Мне нет смысла больше жить.":
                    $ renpy.pause(1.0,hard=True)
                    jump zastrelitsa
            

            label zastrelitsa:

                th "Скоро мы встретимся, моя рыжая."

                window hide
                stop sound3 fadeout 2.0
                show blink
                $ renpy.pause(2,hard=True)
                play sound drobovik_konec
                $ renpy.pause(1.5,hard=True)
                pause(2)
                jump agr_credits
                
        else:

            stop sound2 fadeout 0.5
            play sound padenie_v_sneg
            pause(0.3)
            play sound2 tresk_lda fadein 1.5

            "Споткнулся, разбил нос, палец вывихнул. На губах стало солёно. Я раскрыл глаза и увидел то, чего боялся. Одна псина ковыляла куда-то в сторону, сильно хромая и скуля, а передо мной была небольшая прорубь."
            th "Лёд треснул, она упала. Она там! Я должен!"

            play sound sfx_shoulder_dive_water
            play sound_loop underwater_loop fadein 3
            scene bg int_underwater
            show gas_mask
            with dissolve
            stop sound3 fadeout 1.0

            "Не нырнул — упал в прорубь, тело тут же пронзил лёд и ещё большая, нестерпимая боль. Она тонула."
            "Я рванулся было к ней, но её потихоньку сносило течение."
            "Старался изо всех сил, молча вопя от боли."
            th "Ну нельзя же так глупо, нельзя, нельзя!"
            "Два метра, три вниз."

            stop music fadeout 3

            "Уши заложило. Я схватил её, прижал к себе, рванул наверх."

            stop sound_loop fadeout 6
            play music house_heartbeat_remix fadein 3
            show bg dark_ice_lake behind gas_mask with dissolve

            "Глаза разъедала вода. Только наверху было темно. Лёд. Везде. Сбоку, с другого, метнулся против течения, скорчился от боли в ноге."
            "Попробовал пробить лёд, бешено, со всей дури колотил, как мог, до крови. Сломал палец, кажется.{w} Всё тщетно."

            scene cg ch_9_badend with dissolve

            "Обнял её. Мою Алису, мою девочку. Кислорода уже не оставалось совсем. По ушам бил адреналин, в глазах пена, нога ныла неимоверно."
            "Прощай, рыженькая моя, погибаем. Прости, моя девочка, подвёл я нас, подвёл."
            "Но вот, я всё вспоминаю, как сижу я пятый, вот уже пятый день на лавочке ласковым, тёплым летом..."

            window hide
            stop sound3 fadeout 2.0
            window hide
            show prologue_dream
            with dissolve2
            pause(3)
            jump agr_credits
    else:

        "Обернулся, шагнул чуть вперёд и в бок, закрыл собой жену, видимо, ещё ничего не понимающую,"

        play sound sniatie_s_pred

        extend " снял автомат с предохранителя. Сквозь лёгкую метель виднелись силуэты."
        th "Один... два... пять... сколько же вас, сука?! На каждую патрон найдётся, на каждую!"

        play sound ochered_ak_3

        "Когда расстояние между нами и стаей было шагов двадцать, дал короткую очередь по самой наглой твари. Остальные чуть сбавили ход, зарычали, заскулили, опешили."

        play sound odin_ak
        pause(1)
        play sound odin_ak
        pause(0.7)
        play sound odin_ak

        "Пару раз стрельнув в сторону стаи, ещё одна собака упала замертво."

        play sound protivnyj_voj fadein 1.5
        
        extend " Остальные с противным воем побежали назад."

        stop sound fadeout 3
        window hide
        stop music fadeout 10
        hide cg with dissolve
        window show

        "Мы так и стояли ещё минут десять. Алиса вцепилась в меня сзади, дрожа, как осиновый лист. Меня и самого трясло, то ли от холода, то ли от адреналина."

        play sound sniatie_s_pred

        extend " Осторожно поставил автомат на предохранитель, развернулся к ней."
        
        play music metro_dawnofhope fadein 3
        window hide
        show dv scared coat gasmask behind gas_mask, anim with dissolve
        window auto
        
        me "Ну всё, тихо. Не хочет «Совёнок», видимо, нас отпускать."

        show dv guilty coat gasmask behind gas_mask, anim with dspr

        "Она молчала, но, похоже, успокаивалась."
        "Осторожно развернул её в сторону от бьющейся в агонии собаки, взял рюкзаки, под руку повёл на другой берег. Погладил по голове."

        show dv sad coat gasmask behind gas_mask, anim with dspr

        "Она взглянула на меня самыми преданными глазами, верящими, любящими."
        me "Всё получится. Мы едем туда, где всё будет хорошо!"

        show dv smile coat gasmask behind gas_mask, anim with dspr

        dv "За горизонт, как обещал?"
        me "За горизонт. Домой. В новый дом."

        play sound ag_train

        "Где-то вдалеке засвистел поезд, трубя скорый отъезд."
        "Прощай, «Совёнок». Прощай, страх и ужас, которые мы здесь пережили. Нас ждут новые, не менее опасные испытания в дороге — но мы-то справимся, и не такие переживали."
        "В долгий путь, пионеры."

        window hide
        stop sound3 fadeout 2.0
        window hide
        show prologue_dream
        with dissolve2
        pause(1.0)
        jump agr_credits



    label agr_credits:
        window hide
        show thanks:
            yanchor 0.5 ypos 0.5
            xanchor 0.5 xpos 0.5
        with dissolve
        $ renpy.pause(3, hard=True)
        hide thanks with dissolve
        show credits agr_credits_text at agr_ending_transform
        $ renpy.pause(45, hard=True)
        show agr_logo:
            yanchor 0.5 ypos 0.5
            xanchor 0.5 xpos 0.5
        with dissolve
        $ renpy.pause(3, hard=True)
        stop music fadeout 3
        scene black with dissolve2
        $ renpy.pause(3, hard=True)


    init python:

        agr_credits_text =  """
    Poslesvet Team благодарит вас за время, уделённое модификации!


    Код
    Андрей Волков

    Сценарий
    Михаил Карасёв и Андрей Волков

    Звуковое и визуальное сопровождение
    Даниил Маньковский

    Вычитка
    Андрей Волков

    Суперивенты
    Вадим Каличава

    Автор главного меню
    Артём Михайлов

    Автор некоторых фонов и карты
    Яна Симонец


    Благодарности:
    PyTom'у за движок Ren'Py
    Донатерам, помогавшим проекту
    Команде Soviet Games за замечательную игру «Бесконечное Лето» и за возможность открыто выражать свои мысли в формате модификаций




    {size=80}КОНЕЦ{/size}
    """

    transform agr_ending_transform:
        xalign 0.5
        ypos 1.0
        linear 50.0 ypos -2.0
